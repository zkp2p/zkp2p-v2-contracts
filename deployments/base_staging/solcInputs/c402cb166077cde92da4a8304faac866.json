{
  "language": "Solidity",
  "sources": {
    "@openzeppelin/contracts/access/Ownable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (access/Ownable.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../utils/Context.sol\";\n\n/**\n * @dev Contract module which provides a basic access control mechanism, where\n * there is an account (an owner) that can be granted exclusive access to\n * specific functions.\n *\n * By default, the owner account will be the one that deploys the contract. This\n * can later be changed with {transferOwnership}.\n *\n * This module is used through inheritance. It will make available the modifier\n * `onlyOwner`, which can be applied to your functions to restrict their use to\n * the owner.\n */\nabstract contract Ownable is Context {\n    address private _owner;\n\n    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);\n\n    /**\n     * @dev Initializes the contract setting the deployer as the initial owner.\n     */\n    constructor() {\n        _transferOwnership(_msgSender());\n    }\n\n    /**\n     * @dev Throws if called by any account other than the owner.\n     */\n    modifier onlyOwner() {\n        _checkOwner();\n        _;\n    }\n\n    /**\n     * @dev Returns the address of the current owner.\n     */\n    function owner() public view virtual returns (address) {\n        return _owner;\n    }\n\n    /**\n     * @dev Throws if the sender is not the owner.\n     */\n    function _checkOwner() internal view virtual {\n        require(owner() == _msgSender(), \"Ownable: caller is not the owner\");\n    }\n\n    /**\n     * @dev Leaves the contract without owner. It will not be possible to call\n     * `onlyOwner` functions. Can only be called by the current owner.\n     *\n     * NOTE: Renouncing ownership will leave the contract without an owner,\n     * thereby disabling any functionality that is only available to the owner.\n     */\n    function renounceOwnership() public virtual onlyOwner {\n        _transferOwnership(address(0));\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     * Can only be called by the current owner.\n     */\n    function transferOwnership(address newOwner) public virtual onlyOwner {\n        require(newOwner != address(0), \"Ownable: new owner is the zero address\");\n        _transferOwnership(newOwner);\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     * Internal function without access restriction.\n     */\n    function _transferOwnership(address newOwner) internal virtual {\n        address oldOwner = _owner;\n        _owner = newOwner;\n        emit OwnershipTransferred(oldOwner, newOwner);\n    }\n}\n"
    },
    "@openzeppelin/contracts/interfaces/IERC1271.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (interfaces/IERC1271.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Interface of the ERC1271 standard signature validation method for\n * contracts as defined in https://eips.ethereum.org/EIPS/eip-1271[ERC-1271].\n *\n * _Available since v4.1._\n */\ninterface IERC1271 {\n    /**\n     * @dev Should return whether the signature provided is valid for the provided data\n     * @param hash      Hash of the data to be signed\n     * @param signature Signature byte array associated with _data\n     */\n    function isValidSignature(bytes32 hash, bytes memory signature) external view returns (bytes4 magicValue);\n}\n"
    },
    "@openzeppelin/contracts/security/Pausable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.7.0) (security/Pausable.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../utils/Context.sol\";\n\n/**\n * @dev Contract module which allows children to implement an emergency stop\n * mechanism that can be triggered by an authorized account.\n *\n * This module is used through inheritance. It will make available the\n * modifiers `whenNotPaused` and `whenPaused`, which can be applied to\n * the functions of your contract. Note that they will not be pausable by\n * simply including this module, only once the modifiers are put in place.\n */\nabstract contract Pausable is Context {\n    /**\n     * @dev Emitted when the pause is triggered by `account`.\n     */\n    event Paused(address account);\n\n    /**\n     * @dev Emitted when the pause is lifted by `account`.\n     */\n    event Unpaused(address account);\n\n    bool private _paused;\n\n    /**\n     * @dev Initializes the contract in unpaused state.\n     */\n    constructor() {\n        _paused = false;\n    }\n\n    /**\n     * @dev Modifier to make a function callable only when the contract is not paused.\n     *\n     * Requirements:\n     *\n     * - The contract must not be paused.\n     */\n    modifier whenNotPaused() {\n        _requireNotPaused();\n        _;\n    }\n\n    /**\n     * @dev Modifier to make a function callable only when the contract is paused.\n     *\n     * Requirements:\n     *\n     * - The contract must be paused.\n     */\n    modifier whenPaused() {\n        _requirePaused();\n        _;\n    }\n\n    /**\n     * @dev Returns true if the contract is paused, and false otherwise.\n     */\n    function paused() public view virtual returns (bool) {\n        return _paused;\n    }\n\n    /**\n     * @dev Throws if the contract is paused.\n     */\n    function _requireNotPaused() internal view virtual {\n        require(!paused(), \"Pausable: paused\");\n    }\n\n    /**\n     * @dev Throws if the contract is not paused.\n     */\n    function _requirePaused() internal view virtual {\n        require(paused(), \"Pausable: not paused\");\n    }\n\n    /**\n     * @dev Triggers stopped state.\n     *\n     * Requirements:\n     *\n     * - The contract must not be paused.\n     */\n    function _pause() internal virtual whenNotPaused {\n        _paused = true;\n        emit Paused(_msgSender());\n    }\n\n    /**\n     * @dev Returns to normal state.\n     *\n     * Requirements:\n     *\n     * - The contract must be paused.\n     */\n    function _unpause() internal virtual whenPaused {\n        _paused = false;\n        emit Unpaused(_msgSender());\n    }\n}\n"
    },
    "@openzeppelin/contracts/security/ReentrancyGuard.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (security/ReentrancyGuard.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Contract module that helps prevent reentrant calls to a function.\n *\n * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier\n * available, which can be applied to functions to make sure there are no nested\n * (reentrant) calls to them.\n *\n * Note that because there is a single `nonReentrant` guard, functions marked as\n * `nonReentrant` may not call one another. This can be worked around by making\n * those functions `private`, and then adding `external` `nonReentrant` entry\n * points to them.\n *\n * TIP: If you would like to learn more about reentrancy and alternative ways\n * to protect against it, check out our blog post\n * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].\n */\nabstract contract ReentrancyGuard {\n    // Booleans are more expensive than uint256 or any type that takes up a full\n    // word because each write operation emits an extra SLOAD to first read the\n    // slot's contents, replace the bits taken up by the boolean, and then write\n    // back. This is the compiler's defense against contract upgrades and\n    // pointer aliasing, and it cannot be disabled.\n\n    // The values being non-zero value makes deployment a bit more expensive,\n    // but in exchange the refund on every call to nonReentrant will be lower in\n    // amount. Since refunds are capped to a percentage of the total\n    // transaction's gas, it is best to keep them low in cases like this one, to\n    // increase the likelihood of the full refund coming into effect.\n    uint256 private constant _NOT_ENTERED = 1;\n    uint256 private constant _ENTERED = 2;\n\n    uint256 private _status;\n\n    constructor() {\n        _status = _NOT_ENTERED;\n    }\n\n    /**\n     * @dev Prevents a contract from calling itself, directly or indirectly.\n     * Calling a `nonReentrant` function from another `nonReentrant`\n     * function is not supported. It is possible to prevent this from happening\n     * by making the `nonReentrant` function external, and making it call a\n     * `private` function that does the actual work.\n     */\n    modifier nonReentrant() {\n        _nonReentrantBefore();\n        _;\n        _nonReentrantAfter();\n    }\n\n    function _nonReentrantBefore() private {\n        // On the first call to nonReentrant, _status will be _NOT_ENTERED\n        require(_status != _ENTERED, \"ReentrancyGuard: reentrant call\");\n\n        // Any calls to nonReentrant after this point will fail\n        _status = _ENTERED;\n    }\n\n    function _nonReentrantAfter() private {\n        // By storing the original value once again, a refund is triggered (see\n        // https://eips.ethereum.org/EIPS/eip-2200)\n        _status = _NOT_ENTERED;\n    }\n\n    /**\n     * @dev Returns true if the reentrancy guard is currently set to \"entered\", which indicates there is a\n     * `nonReentrant` function in the call stack.\n     */\n    function _reentrancyGuardEntered() internal view returns (bool) {\n        return _status == _ENTERED;\n    }\n}\n"
    },
    "@openzeppelin/contracts/token/ERC20/ERC20.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/ERC20.sol)\n\npragma solidity ^0.8.0;\n\nimport \"./IERC20.sol\";\nimport \"./extensions/IERC20Metadata.sol\";\nimport \"../../utils/Context.sol\";\n\n/**\n * @dev Implementation of the {IERC20} interface.\n *\n * This implementation is agnostic to the way tokens are created. This means\n * that a supply mechanism has to be added in a derived contract using {_mint}.\n * For a generic mechanism see {ERC20PresetMinterPauser}.\n *\n * TIP: For a detailed writeup see our guide\n * https://forum.openzeppelin.com/t/how-to-implement-erc20-supply-mechanisms/226[How\n * to implement supply mechanisms].\n *\n * The default value of {decimals} is 18. To change this, you should override\n * this function so it returns a different value.\n *\n * We have followed general OpenZeppelin Contracts guidelines: functions revert\n * instead returning `false` on failure. This behavior is nonetheless\n * conventional and does not conflict with the expectations of ERC20\n * applications.\n *\n * Additionally, an {Approval} event is emitted on calls to {transferFrom}.\n * This allows applications to reconstruct the allowance for all accounts just\n * by listening to said events. Other implementations of the EIP may not emit\n * these events, as it isn't required by the specification.\n *\n * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}\n * functions have been added to mitigate the well-known issues around setting\n * allowances. See {IERC20-approve}.\n */\ncontract ERC20 is Context, IERC20, IERC20Metadata {\n    mapping(address => uint256) private _balances;\n\n    mapping(address => mapping(address => uint256)) private _allowances;\n\n    uint256 private _totalSupply;\n\n    string private _name;\n    string private _symbol;\n\n    /**\n     * @dev Sets the values for {name} and {symbol}.\n     *\n     * All two of these values are immutable: they can only be set once during\n     * construction.\n     */\n    constructor(string memory name_, string memory symbol_) {\n        _name = name_;\n        _symbol = symbol_;\n    }\n\n    /**\n     * @dev Returns the name of the token.\n     */\n    function name() public view virtual override returns (string memory) {\n        return _name;\n    }\n\n    /**\n     * @dev Returns the symbol of the token, usually a shorter version of the\n     * name.\n     */\n    function symbol() public view virtual override returns (string memory) {\n        return _symbol;\n    }\n\n    /**\n     * @dev Returns the number of decimals used to get its user representation.\n     * For example, if `decimals` equals `2`, a balance of `505` tokens should\n     * be displayed to a user as `5.05` (`505 / 10 ** 2`).\n     *\n     * Tokens usually opt for a value of 18, imitating the relationship between\n     * Ether and Wei. This is the default value returned by this function, unless\n     * it's overridden.\n     *\n     * NOTE: This information is only used for _display_ purposes: it in\n     * no way affects any of the arithmetic of the contract, including\n     * {IERC20-balanceOf} and {IERC20-transfer}.\n     */\n    function decimals() public view virtual override returns (uint8) {\n        return 18;\n    }\n\n    /**\n     * @dev See {IERC20-totalSupply}.\n     */\n    function totalSupply() public view virtual override returns (uint256) {\n        return _totalSupply;\n    }\n\n    /**\n     * @dev See {IERC20-balanceOf}.\n     */\n    function balanceOf(address account) public view virtual override returns (uint256) {\n        return _balances[account];\n    }\n\n    /**\n     * @dev See {IERC20-transfer}.\n     *\n     * Requirements:\n     *\n     * - `to` cannot be the zero address.\n     * - the caller must have a balance of at least `amount`.\n     */\n    function transfer(address to, uint256 amount) public virtual override returns (bool) {\n        address owner = _msgSender();\n        _transfer(owner, to, amount);\n        return true;\n    }\n\n    /**\n     * @dev See {IERC20-allowance}.\n     */\n    function allowance(address owner, address spender) public view virtual override returns (uint256) {\n        return _allowances[owner][spender];\n    }\n\n    /**\n     * @dev See {IERC20-approve}.\n     *\n     * NOTE: If `amount` is the maximum `uint256`, the allowance is not updated on\n     * `transferFrom`. This is semantically equivalent to an infinite approval.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     */\n    function approve(address spender, uint256 amount) public virtual override returns (bool) {\n        address owner = _msgSender();\n        _approve(owner, spender, amount);\n        return true;\n    }\n\n    /**\n     * @dev See {IERC20-transferFrom}.\n     *\n     * Emits an {Approval} event indicating the updated allowance. This is not\n     * required by the EIP. See the note at the beginning of {ERC20}.\n     *\n     * NOTE: Does not update the allowance if the current allowance\n     * is the maximum `uint256`.\n     *\n     * Requirements:\n     *\n     * - `from` and `to` cannot be the zero address.\n     * - `from` must have a balance of at least `amount`.\n     * - the caller must have allowance for ``from``'s tokens of at least\n     * `amount`.\n     */\n    function transferFrom(address from, address to, uint256 amount) public virtual override returns (bool) {\n        address spender = _msgSender();\n        _spendAllowance(from, spender, amount);\n        _transfer(from, to, amount);\n        return true;\n    }\n\n    /**\n     * @dev Atomically increases the allowance granted to `spender` by the caller.\n     *\n     * This is an alternative to {approve} that can be used as a mitigation for\n     * problems described in {IERC20-approve}.\n     *\n     * Emits an {Approval} event indicating the updated allowance.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     */\n    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {\n        address owner = _msgSender();\n        _approve(owner, spender, allowance(owner, spender) + addedValue);\n        return true;\n    }\n\n    /**\n     * @dev Atomically decreases the allowance granted to `spender` by the caller.\n     *\n     * This is an alternative to {approve} that can be used as a mitigation for\n     * problems described in {IERC20-approve}.\n     *\n     * Emits an {Approval} event indicating the updated allowance.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     * - `spender` must have allowance for the caller of at least\n     * `subtractedValue`.\n     */\n    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {\n        address owner = _msgSender();\n        uint256 currentAllowance = allowance(owner, spender);\n        require(currentAllowance >= subtractedValue, \"ERC20: decreased allowance below zero\");\n        unchecked {\n            _approve(owner, spender, currentAllowance - subtractedValue);\n        }\n\n        return true;\n    }\n\n    /**\n     * @dev Moves `amount` of tokens from `from` to `to`.\n     *\n     * This internal function is equivalent to {transfer}, and can be used to\n     * e.g. implement automatic token fees, slashing mechanisms, etc.\n     *\n     * Emits a {Transfer} event.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `from` must have a balance of at least `amount`.\n     */\n    function _transfer(address from, address to, uint256 amount) internal virtual {\n        require(from != address(0), \"ERC20: transfer from the zero address\");\n        require(to != address(0), \"ERC20: transfer to the zero address\");\n\n        _beforeTokenTransfer(from, to, amount);\n\n        uint256 fromBalance = _balances[from];\n        require(fromBalance >= amount, \"ERC20: transfer amount exceeds balance\");\n        unchecked {\n            _balances[from] = fromBalance - amount;\n            // Overflow not possible: the sum of all balances is capped by totalSupply, and the sum is preserved by\n            // decrementing then incrementing.\n            _balances[to] += amount;\n        }\n\n        emit Transfer(from, to, amount);\n\n        _afterTokenTransfer(from, to, amount);\n    }\n\n    /** @dev Creates `amount` tokens and assigns them to `account`, increasing\n     * the total supply.\n     *\n     * Emits a {Transfer} event with `from` set to the zero address.\n     *\n     * Requirements:\n     *\n     * - `account` cannot be the zero address.\n     */\n    function _mint(address account, uint256 amount) internal virtual {\n        require(account != address(0), \"ERC20: mint to the zero address\");\n\n        _beforeTokenTransfer(address(0), account, amount);\n\n        _totalSupply += amount;\n        unchecked {\n            // Overflow not possible: balance + amount is at most totalSupply + amount, which is checked above.\n            _balances[account] += amount;\n        }\n        emit Transfer(address(0), account, amount);\n\n        _afterTokenTransfer(address(0), account, amount);\n    }\n\n    /**\n     * @dev Destroys `amount` tokens from `account`, reducing the\n     * total supply.\n     *\n     * Emits a {Transfer} event with `to` set to the zero address.\n     *\n     * Requirements:\n     *\n     * - `account` cannot be the zero address.\n     * - `account` must have at least `amount` tokens.\n     */\n    function _burn(address account, uint256 amount) internal virtual {\n        require(account != address(0), \"ERC20: burn from the zero address\");\n\n        _beforeTokenTransfer(account, address(0), amount);\n\n        uint256 accountBalance = _balances[account];\n        require(accountBalance >= amount, \"ERC20: burn amount exceeds balance\");\n        unchecked {\n            _balances[account] = accountBalance - amount;\n            // Overflow not possible: amount <= accountBalance <= totalSupply.\n            _totalSupply -= amount;\n        }\n\n        emit Transfer(account, address(0), amount);\n\n        _afterTokenTransfer(account, address(0), amount);\n    }\n\n    /**\n     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.\n     *\n     * This internal function is equivalent to `approve`, and can be used to\n     * e.g. set automatic allowances for certain subsystems, etc.\n     *\n     * Emits an {Approval} event.\n     *\n     * Requirements:\n     *\n     * - `owner` cannot be the zero address.\n     * - `spender` cannot be the zero address.\n     */\n    function _approve(address owner, address spender, uint256 amount) internal virtual {\n        require(owner != address(0), \"ERC20: approve from the zero address\");\n        require(spender != address(0), \"ERC20: approve to the zero address\");\n\n        _allowances[owner][spender] = amount;\n        emit Approval(owner, spender, amount);\n    }\n\n    /**\n     * @dev Updates `owner` s allowance for `spender` based on spent `amount`.\n     *\n     * Does not update the allowance amount in case of infinite allowance.\n     * Revert if not enough allowance is available.\n     *\n     * Might emit an {Approval} event.\n     */\n    function _spendAllowance(address owner, address spender, uint256 amount) internal virtual {\n        uint256 currentAllowance = allowance(owner, spender);\n        if (currentAllowance != type(uint256).max) {\n            require(currentAllowance >= amount, \"ERC20: insufficient allowance\");\n            unchecked {\n                _approve(owner, spender, currentAllowance - amount);\n            }\n        }\n    }\n\n    /**\n     * @dev Hook that is called before any transfer of tokens. This includes\n     * minting and burning.\n     *\n     * Calling conditions:\n     *\n     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens\n     * will be transferred to `to`.\n     * - when `from` is zero, `amount` tokens will be minted for `to`.\n     * - when `to` is zero, `amount` of ``from``'s tokens will be burned.\n     * - `from` and `to` are never both zero.\n     *\n     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].\n     */\n    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual {}\n\n    /**\n     * @dev Hook that is called after any transfer of tokens. This includes\n     * minting and burning.\n     *\n     * Calling conditions:\n     *\n     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens\n     * has been transferred to `to`.\n     * - when `from` is zero, `amount` tokens have been minted for `to`.\n     * - when `to` is zero, `amount` of ``from``'s tokens have been burned.\n     * - `from` and `to` are never both zero.\n     *\n     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].\n     */\n    function _afterTokenTransfer(address from, address to, uint256 amount) internal virtual {}\n}\n"
    },
    "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../IERC20.sol\";\n\n/**\n * @dev Interface for the optional metadata functions from the ERC20 standard.\n *\n * _Available since v4.1._\n */\ninterface IERC20Metadata is IERC20 {\n    /**\n     * @dev Returns the name of the token.\n     */\n    function name() external view returns (string memory);\n\n    /**\n     * @dev Returns the symbol of the token.\n     */\n    function symbol() external view returns (string memory);\n\n    /**\n     * @dev Returns the decimals places of the token.\n     */\n    function decimals() external view returns (uint8);\n}\n"
    },
    "@openzeppelin/contracts/token/ERC20/extensions/IERC20Permit.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/extensions/IERC20Permit.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Interface of the ERC20 Permit extension allowing approvals to be made via signatures, as defined in\n * https://eips.ethereum.org/EIPS/eip-2612[EIP-2612].\n *\n * Adds the {permit} method, which can be used to change an account's ERC20 allowance (see {IERC20-allowance}) by\n * presenting a message signed by the account. By not relying on {IERC20-approve}, the token holder account doesn't\n * need to send a transaction, and thus is not required to hold Ether at all.\n */\ninterface IERC20Permit {\n    /**\n     * @dev Sets `value` as the allowance of `spender` over ``owner``'s tokens,\n     * given ``owner``'s signed approval.\n     *\n     * IMPORTANT: The same issues {IERC20-approve} has related to transaction\n     * ordering also apply here.\n     *\n     * Emits an {Approval} event.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     * - `deadline` must be a timestamp in the future.\n     * - `v`, `r` and `s` must be a valid `secp256k1` signature from `owner`\n     * over the EIP712-formatted function arguments.\n     * - the signature must use ``owner``'s current nonce (see {nonces}).\n     *\n     * For more information on the signature format, see the\n     * https://eips.ethereum.org/EIPS/eip-2612#specification[relevant EIP\n     * section].\n     */\n    function permit(\n        address owner,\n        address spender,\n        uint256 value,\n        uint256 deadline,\n        uint8 v,\n        bytes32 r,\n        bytes32 s\n    ) external;\n\n    /**\n     * @dev Returns the current nonce for `owner`. This value must be\n     * included whenever a signature is generated for {permit}.\n     *\n     * Every successful call to {permit} increases ``owner``'s nonce by one. This\n     * prevents a signature from being used multiple times.\n     */\n    function nonces(address owner) external view returns (uint256);\n\n    /**\n     * @dev Returns the domain separator used in the encoding of the signature for {permit}, as defined by {EIP712}.\n     */\n    // solhint-disable-next-line func-name-mixedcase\n    function DOMAIN_SEPARATOR() external view returns (bytes32);\n}\n"
    },
    "@openzeppelin/contracts/token/ERC20/IERC20.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/IERC20.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Interface of the ERC20 standard as defined in the EIP.\n */\ninterface IERC20 {\n    /**\n     * @dev Emitted when `value` tokens are moved from one account (`from`) to\n     * another (`to`).\n     *\n     * Note that `value` may be zero.\n     */\n    event Transfer(address indexed from, address indexed to, uint256 value);\n\n    /**\n     * @dev Emitted when the allowance of a `spender` for an `owner` is set by\n     * a call to {approve}. `value` is the new allowance.\n     */\n    event Approval(address indexed owner, address indexed spender, uint256 value);\n\n    /**\n     * @dev Returns the amount of tokens in existence.\n     */\n    function totalSupply() external view returns (uint256);\n\n    /**\n     * @dev Returns the amount of tokens owned by `account`.\n     */\n    function balanceOf(address account) external view returns (uint256);\n\n    /**\n     * @dev Moves `amount` tokens from the caller's account to `to`.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transfer(address to, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Returns the remaining number of tokens that `spender` will be\n     * allowed to spend on behalf of `owner` through {transferFrom}. This is\n     * zero by default.\n     *\n     * This value changes when {approve} or {transferFrom} are called.\n     */\n    function allowance(address owner, address spender) external view returns (uint256);\n\n    /**\n     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * IMPORTANT: Beware that changing an allowance with this method brings the risk\n     * that someone may use both the old and the new allowance by unfortunate\n     * transaction ordering. One possible solution to mitigate this race\n     * condition is to first reduce the spender's allowance to 0 and set the\n     * desired value afterwards:\n     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729\n     *\n     * Emits an {Approval} event.\n     */\n    function approve(address spender, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Moves `amount` tokens from `from` to `to` using the\n     * allowance mechanism. `amount` is then deducted from the caller's\n     * allowance.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transferFrom(address from, address to, uint256 amount) external returns (bool);\n}\n"
    },
    "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.3) (token/ERC20/utils/SafeERC20.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../IERC20.sol\";\nimport \"../extensions/IERC20Permit.sol\";\nimport \"../../../utils/Address.sol\";\n\n/**\n * @title SafeERC20\n * @dev Wrappers around ERC20 operations that throw on failure (when the token\n * contract returns false). Tokens that return no value (and instead revert or\n * throw on failure) are also supported, non-reverting calls are assumed to be\n * successful.\n * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,\n * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.\n */\nlibrary SafeERC20 {\n    using Address for address;\n\n    /**\n     * @dev Transfer `value` amount of `token` from the calling contract to `to`. If `token` returns no value,\n     * non-reverting calls are assumed to be successful.\n     */\n    function safeTransfer(IERC20 token, address to, uint256 value) internal {\n        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));\n    }\n\n    /**\n     * @dev Transfer `value` amount of `token` from `from` to `to`, spending the approval given by `from` to the\n     * calling contract. If `token` returns no value, non-reverting calls are assumed to be successful.\n     */\n    function safeTransferFrom(IERC20 token, address from, address to, uint256 value) internal {\n        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));\n    }\n\n    /**\n     * @dev Deprecated. This function has issues similar to the ones found in\n     * {IERC20-approve}, and its usage is discouraged.\n     *\n     * Whenever possible, use {safeIncreaseAllowance} and\n     * {safeDecreaseAllowance} instead.\n     */\n    function safeApprove(IERC20 token, address spender, uint256 value) internal {\n        // safeApprove should only be called when setting an initial allowance,\n        // or when resetting it to zero. To increase and decrease it, use\n        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'\n        require(\n            (value == 0) || (token.allowance(address(this), spender) == 0),\n            \"SafeERC20: approve from non-zero to non-zero allowance\"\n        );\n        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));\n    }\n\n    /**\n     * @dev Increase the calling contract's allowance toward `spender` by `value`. If `token` returns no value,\n     * non-reverting calls are assumed to be successful.\n     */\n    function safeIncreaseAllowance(IERC20 token, address spender, uint256 value) internal {\n        uint256 oldAllowance = token.allowance(address(this), spender);\n        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, oldAllowance + value));\n    }\n\n    /**\n     * @dev Decrease the calling contract's allowance toward `spender` by `value`. If `token` returns no value,\n     * non-reverting calls are assumed to be successful.\n     */\n    function safeDecreaseAllowance(IERC20 token, address spender, uint256 value) internal {\n        unchecked {\n            uint256 oldAllowance = token.allowance(address(this), spender);\n            require(oldAllowance >= value, \"SafeERC20: decreased allowance below zero\");\n            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, oldAllowance - value));\n        }\n    }\n\n    /**\n     * @dev Set the calling contract's allowance toward `spender` to `value`. If `token` returns no value,\n     * non-reverting calls are assumed to be successful. Meant to be used with tokens that require the approval\n     * to be set to zero before setting it to a non-zero value, such as USDT.\n     */\n    function forceApprove(IERC20 token, address spender, uint256 value) internal {\n        bytes memory approvalCall = abi.encodeWithSelector(token.approve.selector, spender, value);\n\n        if (!_callOptionalReturnBool(token, approvalCall)) {\n            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, 0));\n            _callOptionalReturn(token, approvalCall);\n        }\n    }\n\n    /**\n     * @dev Use a ERC-2612 signature to set the `owner` approval toward `spender` on `token`.\n     * Revert on invalid signature.\n     */\n    function safePermit(\n        IERC20Permit token,\n        address owner,\n        address spender,\n        uint256 value,\n        uint256 deadline,\n        uint8 v,\n        bytes32 r,\n        bytes32 s\n    ) internal {\n        uint256 nonceBefore = token.nonces(owner);\n        token.permit(owner, spender, value, deadline, v, r, s);\n        uint256 nonceAfter = token.nonces(owner);\n        require(nonceAfter == nonceBefore + 1, \"SafeERC20: permit did not succeed\");\n    }\n\n    /**\n     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement\n     * on the return value: the return value is optional (but if data is returned, it must not be false).\n     * @param token The token targeted by the call.\n     * @param data The call data (encoded using abi.encode or one of its variants).\n     */\n    function _callOptionalReturn(IERC20 token, bytes memory data) private {\n        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since\n        // we're implementing it ourselves. We use {Address-functionCall} to perform this call, which verifies that\n        // the target address contains contract code and also asserts for success in the low-level call.\n\n        bytes memory returndata = address(token).functionCall(data, \"SafeERC20: low-level call failed\");\n        require(returndata.length == 0 || abi.decode(returndata, (bool)), \"SafeERC20: ERC20 operation did not succeed\");\n    }\n\n    /**\n     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement\n     * on the return value: the return value is optional (but if data is returned, it must not be false).\n     * @param token The token targeted by the call.\n     * @param data The call data (encoded using abi.encode or one of its variants).\n     *\n     * This is a variant of {_callOptionalReturn} that silents catches all reverts and returns a bool instead.\n     */\n    function _callOptionalReturnBool(IERC20 token, bytes memory data) private returns (bool) {\n        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since\n        // we're implementing it ourselves. We cannot use {Address-functionCall} here since this should return false\n        // and not revert is the subcall reverts.\n\n        (bool success, bytes memory returndata) = address(token).call(data);\n        return\n            success && (returndata.length == 0 || abi.decode(returndata, (bool))) && Address.isContract(address(token));\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Address.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (utils/Address.sol)\n\npragma solidity ^0.8.1;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary Address {\n    /**\n     * @dev Returns true if `account` is a contract.\n     *\n     * [IMPORTANT]\n     * ====\n     * It is unsafe to assume that an address for which this function returns\n     * false is an externally-owned account (EOA) and not a contract.\n     *\n     * Among others, `isContract` will return false for the following\n     * types of addresses:\n     *\n     *  - an externally-owned account\n     *  - a contract in construction\n     *  - an address where a contract will be created\n     *  - an address where a contract lived, but was destroyed\n     *\n     * Furthermore, `isContract` will also return true if the target contract within\n     * the same transaction is already scheduled for destruction by `SELFDESTRUCT`,\n     * which only has an effect at the end of a transaction.\n     * ====\n     *\n     * [IMPORTANT]\n     * ====\n     * You shouldn't rely on `isContract` to protect against flash loan attacks!\n     *\n     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets\n     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract\n     * constructor.\n     * ====\n     */\n    function isContract(address account) internal view returns (bool) {\n        // This method relies on extcodesize/address.code.length, which returns 0\n        // for contracts in construction, since the code is only stored at the end\n        // of the constructor execution.\n\n        return account.code.length > 0;\n    }\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://consensys.net/diligence/blog/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.8.0/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        require(address(this).balance >= amount, \"Address: insufficient balance\");\n\n        (bool success, ) = recipient.call{value: amount}(\"\");\n        require(success, \"Address: unable to send value, recipient may have reverted\");\n    }\n\n    /**\n     * @dev Performs a Solidity function call using a low level `call`. A\n     * plain `call` is an unsafe replacement for a function call: use this\n     * function instead.\n     *\n     * If `target` reverts with a revert reason, it is bubbled up by this\n     * function (like regular Solidity function calls).\n     *\n     * Returns the raw returned data. To convert to the expected return value,\n     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].\n     *\n     * Requirements:\n     *\n     * - `target` must be a contract.\n     * - calling `target` with `data` must not revert.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0, \"Address: low-level call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with\n     * `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but also transferring `value` wei to `target`.\n     *\n     * Requirements:\n     *\n     * - the calling contract must have an ETH balance of at least `value`.\n     * - the called Solidity function must be `payable`.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, value, \"Address: low-level call with value failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but\n     * with `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(\n        address target,\n        bytes memory data,\n        uint256 value,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        require(address(this).balance >= value, \"Address: insufficient balance for call\");\n        (bool success, bytes memory returndata) = target.call{value: value}(data);\n        return verifyCallResultFromTarget(target, success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {\n        return functionStaticCall(target, data, \"Address: low-level static call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal view returns (bytes memory) {\n        (bool success, bytes memory returndata) = target.staticcall(data);\n        return verifyCallResultFromTarget(target, success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionDelegateCall(target, data, \"Address: low-level delegate call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        (bool success, bytes memory returndata) = target.delegatecall(data);\n        return verifyCallResultFromTarget(target, success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Tool to verify that a low level call to smart-contract was successful, and revert (either by bubbling\n     * the revert reason or using the provided one) in case of unsuccessful call or if target was not a contract.\n     *\n     * _Available since v4.8._\n     */\n    function verifyCallResultFromTarget(\n        address target,\n        bool success,\n        bytes memory returndata,\n        string memory errorMessage\n    ) internal view returns (bytes memory) {\n        if (success) {\n            if (returndata.length == 0) {\n                // only check isContract if the call was successful and the return data is empty\n                // otherwise we already know that it was a contract\n                require(isContract(target), \"Address: call to non-contract\");\n            }\n            return returndata;\n        } else {\n            _revert(returndata, errorMessage);\n        }\n    }\n\n    /**\n     * @dev Tool to verify that a low level call was successful, and revert if it wasn't, either by bubbling the\n     * revert reason or using the provided one.\n     *\n     * _Available since v4.3._\n     */\n    function verifyCallResult(\n        bool success,\n        bytes memory returndata,\n        string memory errorMessage\n    ) internal pure returns (bytes memory) {\n        if (success) {\n            return returndata;\n        } else {\n            _revert(returndata, errorMessage);\n        }\n    }\n\n    function _revert(bytes memory returndata, string memory errorMessage) private pure {\n        // Look for revert reason and bubble it up if present\n        if (returndata.length > 0) {\n            // The easiest way to bubble the revert reason is using memory via assembly\n            /// @solidity memory-safe-assembly\n            assembly {\n                let returndata_size := mload(returndata)\n                revert(add(32, returndata), returndata_size)\n            }\n        } else {\n            revert(errorMessage);\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Context.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Provides information about the current execution context, including the\n * sender of the transaction and its data. While these are generally available\n * via msg.sender and msg.data, they should not be accessed in such a direct\n * manner, since when dealing with meta-transactions the account sending and\n * paying for execution may not be the actual sender (as far as an application\n * is concerned).\n *\n * This contract is only required for intermediate, library-like contracts.\n */\nabstract contract Context {\n    function _msgSender() internal view virtual returns (address) {\n        return msg.sender;\n    }\n\n    function _msgData() internal view virtual returns (bytes calldata) {\n        return msg.data;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/cryptography/ECDSA.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (utils/cryptography/ECDSA.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../Strings.sol\";\n\n/**\n * @dev Elliptic Curve Digital Signature Algorithm (ECDSA) operations.\n *\n * These functions can be used to verify that a message was signed by the holder\n * of the private keys of a given address.\n */\nlibrary ECDSA {\n    enum RecoverError {\n        NoError,\n        InvalidSignature,\n        InvalidSignatureLength,\n        InvalidSignatureS,\n        InvalidSignatureV // Deprecated in v4.8\n    }\n\n    function _throwError(RecoverError error) private pure {\n        if (error == RecoverError.NoError) {\n            return; // no error: do nothing\n        } else if (error == RecoverError.InvalidSignature) {\n            revert(\"ECDSA: invalid signature\");\n        } else if (error == RecoverError.InvalidSignatureLength) {\n            revert(\"ECDSA: invalid signature length\");\n        } else if (error == RecoverError.InvalidSignatureS) {\n            revert(\"ECDSA: invalid signature 's' value\");\n        }\n    }\n\n    /**\n     * @dev Returns the address that signed a hashed message (`hash`) with\n     * `signature` or error string. This address can then be used for verification purposes.\n     *\n     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:\n     * this function rejects them by requiring the `s` value to be in the lower\n     * half order, and the `v` value to be either 27 or 28.\n     *\n     * IMPORTANT: `hash` _must_ be the result of a hash operation for the\n     * verification to be secure: it is possible to craft signatures that\n     * recover to arbitrary addresses for non-hashed data. A safe way to ensure\n     * this is by receiving a hash of the original message (which may otherwise\n     * be too long), and then calling {toEthSignedMessageHash} on it.\n     *\n     * Documentation for signature generation:\n     * - with https://web3js.readthedocs.io/en/v1.3.4/web3-eth-accounts.html#sign[Web3.js]\n     * - with https://docs.ethers.io/v5/api/signer/#Signer-signMessage[ethers]\n     *\n     * _Available since v4.3._\n     */\n    function tryRecover(bytes32 hash, bytes memory signature) internal pure returns (address, RecoverError) {\n        if (signature.length == 65) {\n            bytes32 r;\n            bytes32 s;\n            uint8 v;\n            // ecrecover takes the signature parameters, and the only way to get them\n            // currently is to use assembly.\n            /// @solidity memory-safe-assembly\n            assembly {\n                r := mload(add(signature, 0x20))\n                s := mload(add(signature, 0x40))\n                v := byte(0, mload(add(signature, 0x60)))\n            }\n            return tryRecover(hash, v, r, s);\n        } else {\n            return (address(0), RecoverError.InvalidSignatureLength);\n        }\n    }\n\n    /**\n     * @dev Returns the address that signed a hashed message (`hash`) with\n     * `signature`. This address can then be used for verification purposes.\n     *\n     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:\n     * this function rejects them by requiring the `s` value to be in the lower\n     * half order, and the `v` value to be either 27 or 28.\n     *\n     * IMPORTANT: `hash` _must_ be the result of a hash operation for the\n     * verification to be secure: it is possible to craft signatures that\n     * recover to arbitrary addresses for non-hashed data. A safe way to ensure\n     * this is by receiving a hash of the original message (which may otherwise\n     * be too long), and then calling {toEthSignedMessageHash} on it.\n     */\n    function recover(bytes32 hash, bytes memory signature) internal pure returns (address) {\n        (address recovered, RecoverError error) = tryRecover(hash, signature);\n        _throwError(error);\n        return recovered;\n    }\n\n    /**\n     * @dev Overload of {ECDSA-tryRecover} that receives the `r` and `vs` short-signature fields separately.\n     *\n     * See https://eips.ethereum.org/EIPS/eip-2098[EIP-2098 short signatures]\n     *\n     * _Available since v4.3._\n     */\n    function tryRecover(bytes32 hash, bytes32 r, bytes32 vs) internal pure returns (address, RecoverError) {\n        bytes32 s = vs & bytes32(0x7fffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff);\n        uint8 v = uint8((uint256(vs) >> 255) + 27);\n        return tryRecover(hash, v, r, s);\n    }\n\n    /**\n     * @dev Overload of {ECDSA-recover} that receives the `r and `vs` short-signature fields separately.\n     *\n     * _Available since v4.2._\n     */\n    function recover(bytes32 hash, bytes32 r, bytes32 vs) internal pure returns (address) {\n        (address recovered, RecoverError error) = tryRecover(hash, r, vs);\n        _throwError(error);\n        return recovered;\n    }\n\n    /**\n     * @dev Overload of {ECDSA-tryRecover} that receives the `v`,\n     * `r` and `s` signature fields separately.\n     *\n     * _Available since v4.3._\n     */\n    function tryRecover(bytes32 hash, uint8 v, bytes32 r, bytes32 s) internal pure returns (address, RecoverError) {\n        // EIP-2 still allows signature malleability for ecrecover(). Remove this possibility and make the signature\n        // unique. Appendix F in the Ethereum Yellow paper (https://ethereum.github.io/yellowpaper/paper.pdf), defines\n        // the valid range for s in (301): 0 < s < secp256k1n  2 + 1, and for v in (302): v  {27, 28}. Most\n        // signatures from current libraries generate a unique signature with an s-value in the lower half order.\n        //\n        // If your library generates malleable signatures, such as s-values in the upper range, calculate a new s-value\n        // with 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141 - s1 and flip v from 27 to 28 or\n        // vice versa. If your library also generates signatures with 0/1 for v instead 27/28, add 27 to v to accept\n        // these malleable signatures as well.\n        if (uint256(s) > 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0) {\n            return (address(0), RecoverError.InvalidSignatureS);\n        }\n\n        // If the signature is valid (and not malleable), return the signer address\n        address signer = ecrecover(hash, v, r, s);\n        if (signer == address(0)) {\n            return (address(0), RecoverError.InvalidSignature);\n        }\n\n        return (signer, RecoverError.NoError);\n    }\n\n    /**\n     * @dev Overload of {ECDSA-recover} that receives the `v`,\n     * `r` and `s` signature fields separately.\n     */\n    function recover(bytes32 hash, uint8 v, bytes32 r, bytes32 s) internal pure returns (address) {\n        (address recovered, RecoverError error) = tryRecover(hash, v, r, s);\n        _throwError(error);\n        return recovered;\n    }\n\n    /**\n     * @dev Returns an Ethereum Signed Message, created from a `hash`. This\n     * produces hash corresponding to the one signed with the\n     * https://eth.wiki/json-rpc/API#eth_sign[`eth_sign`]\n     * JSON-RPC method as part of EIP-191.\n     *\n     * See {recover}.\n     */\n    function toEthSignedMessageHash(bytes32 hash) internal pure returns (bytes32 message) {\n        // 32 is the length in bytes of hash,\n        // enforced by the type signature above\n        /// @solidity memory-safe-assembly\n        assembly {\n            mstore(0x00, \"\\x19Ethereum Signed Message:\\n32\")\n            mstore(0x1c, hash)\n            message := keccak256(0x00, 0x3c)\n        }\n    }\n\n    /**\n     * @dev Returns an Ethereum Signed Message, created from `s`. This\n     * produces hash corresponding to the one signed with the\n     * https://eth.wiki/json-rpc/API#eth_sign[`eth_sign`]\n     * JSON-RPC method as part of EIP-191.\n     *\n     * See {recover}.\n     */\n    function toEthSignedMessageHash(bytes memory s) internal pure returns (bytes32) {\n        return keccak256(abi.encodePacked(\"\\x19Ethereum Signed Message:\\n\", Strings.toString(s.length), s));\n    }\n\n    /**\n     * @dev Returns an Ethereum Signed Typed Data, created from a\n     * `domainSeparator` and a `structHash`. This produces hash corresponding\n     * to the one signed with the\n     * https://eips.ethereum.org/EIPS/eip-712[`eth_signTypedData`]\n     * JSON-RPC method as part of EIP-712.\n     *\n     * See {recover}.\n     */\n    function toTypedDataHash(bytes32 domainSeparator, bytes32 structHash) internal pure returns (bytes32 data) {\n        /// @solidity memory-safe-assembly\n        assembly {\n            let ptr := mload(0x40)\n            mstore(ptr, \"\\x19\\x01\")\n            mstore(add(ptr, 0x02), domainSeparator)\n            mstore(add(ptr, 0x22), structHash)\n            data := keccak256(ptr, 0x42)\n        }\n    }\n\n    /**\n     * @dev Returns an Ethereum Signed Data with intended validator, created from a\n     * `validator` and `data` according to the version 0 of EIP-191.\n     *\n     * See {recover}.\n     */\n    function toDataWithIntendedValidatorHash(address validator, bytes memory data) internal pure returns (bytes32) {\n        return keccak256(abi.encodePacked(\"\\x19\\x00\", validator, data));\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/cryptography/SignatureChecker.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (utils/cryptography/SignatureChecker.sol)\n\npragma solidity ^0.8.0;\n\nimport \"./ECDSA.sol\";\nimport \"../../interfaces/IERC1271.sol\";\n\n/**\n * @dev Signature verification helper that can be used instead of `ECDSA.recover` to seamlessly support both ECDSA\n * signatures from externally owned accounts (EOAs) as well as ERC1271 signatures from smart contract wallets like\n * Argent and Gnosis Safe.\n *\n * _Available since v4.1._\n */\nlibrary SignatureChecker {\n    /**\n     * @dev Checks if a signature is valid for a given signer and data hash. If the signer is a smart contract, the\n     * signature is validated against that smart contract using ERC1271, otherwise it's validated using `ECDSA.recover`.\n     *\n     * NOTE: Unlike ECDSA signatures, contract signatures are revocable, and the outcome of this function can thus\n     * change through time. It could return true at block N and false at block N+1 (or the opposite).\n     */\n    function isValidSignatureNow(address signer, bytes32 hash, bytes memory signature) internal view returns (bool) {\n        (address recovered, ECDSA.RecoverError error) = ECDSA.tryRecover(hash, signature);\n        return\n            (error == ECDSA.RecoverError.NoError && recovered == signer) ||\n            isValidERC1271SignatureNow(signer, hash, signature);\n    }\n\n    /**\n     * @dev Checks if a signature is valid for a given signer and data hash. The signature is validated\n     * against the signer smart contract using ERC1271.\n     *\n     * NOTE: Unlike ECDSA signatures, contract signatures are revocable, and the outcome of this function can thus\n     * change through time. It could return true at block N and false at block N+1 (or the opposite).\n     */\n    function isValidERC1271SignatureNow(\n        address signer,\n        bytes32 hash,\n        bytes memory signature\n    ) internal view returns (bool) {\n        (bool success, bytes memory result) = signer.staticcall(\n            abi.encodeWithSelector(IERC1271.isValidSignature.selector, hash, signature)\n        );\n        return (success &&\n            result.length >= 32 &&\n            abi.decode(result, (bytes32)) == bytes32(IERC1271.isValidSignature.selector));\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/math/Math.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (utils/math/Math.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Standard math utilities missing in the Solidity language.\n */\nlibrary Math {\n    enum Rounding {\n        Down, // Toward negative infinity\n        Up, // Toward infinity\n        Zero // Toward zero\n    }\n\n    /**\n     * @dev Returns the largest of two numbers.\n     */\n    function max(uint256 a, uint256 b) internal pure returns (uint256) {\n        return a > b ? a : b;\n    }\n\n    /**\n     * @dev Returns the smallest of two numbers.\n     */\n    function min(uint256 a, uint256 b) internal pure returns (uint256) {\n        return a < b ? a : b;\n    }\n\n    /**\n     * @dev Returns the average of two numbers. The result is rounded towards\n     * zero.\n     */\n    function average(uint256 a, uint256 b) internal pure returns (uint256) {\n        // (a + b) / 2 can overflow.\n        return (a & b) + (a ^ b) / 2;\n    }\n\n    /**\n     * @dev Returns the ceiling of the division of two numbers.\n     *\n     * This differs from standard division with `/` in that it rounds up instead\n     * of rounding down.\n     */\n    function ceilDiv(uint256 a, uint256 b) internal pure returns (uint256) {\n        // (a + b - 1) / b can overflow on addition, so we distribute.\n        return a == 0 ? 0 : (a - 1) / b + 1;\n    }\n\n    /**\n     * @notice Calculates floor(x * y / denominator) with full precision. Throws if result overflows a uint256 or denominator == 0\n     * @dev Original credit to Remco Bloemen under MIT license (https://xn--2-umb.com/21/muldiv)\n     * with further edits by Uniswap Labs also under MIT license.\n     */\n    function mulDiv(uint256 x, uint256 y, uint256 denominator) internal pure returns (uint256 result) {\n        unchecked {\n            // 512-bit multiply [prod1 prod0] = x * y. Compute the product mod 2^256 and mod 2^256 - 1, then use\n            // use the Chinese Remainder Theorem to reconstruct the 512 bit result. The result is stored in two 256\n            // variables such that product = prod1 * 2^256 + prod0.\n            uint256 prod0; // Least significant 256 bits of the product\n            uint256 prod1; // Most significant 256 bits of the product\n            assembly {\n                let mm := mulmod(x, y, not(0))\n                prod0 := mul(x, y)\n                prod1 := sub(sub(mm, prod0), lt(mm, prod0))\n            }\n\n            // Handle non-overflow cases, 256 by 256 division.\n            if (prod1 == 0) {\n                // Solidity will revert if denominator == 0, unlike the div opcode on its own.\n                // The surrounding unchecked block does not change this fact.\n                // See https://docs.soliditylang.org/en/latest/control-structures.html#checked-or-unchecked-arithmetic.\n                return prod0 / denominator;\n            }\n\n            // Make sure the result is less than 2^256. Also prevents denominator == 0.\n            require(denominator > prod1, \"Math: mulDiv overflow\");\n\n            ///////////////////////////////////////////////\n            // 512 by 256 division.\n            ///////////////////////////////////////////////\n\n            // Make division exact by subtracting the remainder from [prod1 prod0].\n            uint256 remainder;\n            assembly {\n                // Compute remainder using mulmod.\n                remainder := mulmod(x, y, denominator)\n\n                // Subtract 256 bit number from 512 bit number.\n                prod1 := sub(prod1, gt(remainder, prod0))\n                prod0 := sub(prod0, remainder)\n            }\n\n            // Factor powers of two out of denominator and compute largest power of two divisor of denominator. Always >= 1.\n            // See https://cs.stackexchange.com/q/138556/92363.\n\n            // Does not overflow because the denominator cannot be zero at this stage in the function.\n            uint256 twos = denominator & (~denominator + 1);\n            assembly {\n                // Divide denominator by twos.\n                denominator := div(denominator, twos)\n\n                // Divide [prod1 prod0] by twos.\n                prod0 := div(prod0, twos)\n\n                // Flip twos such that it is 2^256 / twos. If twos is zero, then it becomes one.\n                twos := add(div(sub(0, twos), twos), 1)\n            }\n\n            // Shift in bits from prod1 into prod0.\n            prod0 |= prod1 * twos;\n\n            // Invert denominator mod 2^256. Now that denominator is an odd number, it has an inverse modulo 2^256 such\n            // that denominator * inv = 1 mod 2^256. Compute the inverse by starting with a seed that is correct for\n            // four bits. That is, denominator * inv = 1 mod 2^4.\n            uint256 inverse = (3 * denominator) ^ 2;\n\n            // Use the Newton-Raphson iteration to improve the precision. Thanks to Hensel's lifting lemma, this also works\n            // in modular arithmetic, doubling the correct bits in each step.\n            inverse *= 2 - denominator * inverse; // inverse mod 2^8\n            inverse *= 2 - denominator * inverse; // inverse mod 2^16\n            inverse *= 2 - denominator * inverse; // inverse mod 2^32\n            inverse *= 2 - denominator * inverse; // inverse mod 2^64\n            inverse *= 2 - denominator * inverse; // inverse mod 2^128\n            inverse *= 2 - denominator * inverse; // inverse mod 2^256\n\n            // Because the division is now exact we can divide by multiplying with the modular inverse of denominator.\n            // This will give us the correct result modulo 2^256. Since the preconditions guarantee that the outcome is\n            // less than 2^256, this is the final result. We don't need to compute the high bits of the result and prod1\n            // is no longer required.\n            result = prod0 * inverse;\n            return result;\n        }\n    }\n\n    /**\n     * @notice Calculates x * y / denominator with full precision, following the selected rounding direction.\n     */\n    function mulDiv(uint256 x, uint256 y, uint256 denominator, Rounding rounding) internal pure returns (uint256) {\n        uint256 result = mulDiv(x, y, denominator);\n        if (rounding == Rounding.Up && mulmod(x, y, denominator) > 0) {\n            result += 1;\n        }\n        return result;\n    }\n\n    /**\n     * @dev Returns the square root of a number. If the number is not a perfect square, the value is rounded down.\n     *\n     * Inspired by Henry S. Warren, Jr.'s \"Hacker's Delight\" (Chapter 11).\n     */\n    function sqrt(uint256 a) internal pure returns (uint256) {\n        if (a == 0) {\n            return 0;\n        }\n\n        // For our first guess, we get the biggest power of 2 which is smaller than the square root of the target.\n        //\n        // We know that the \"msb\" (most significant bit) of our target number `a` is a power of 2 such that we have\n        // `msb(a) <= a < 2*msb(a)`. This value can be written `msb(a)=2**k` with `k=log2(a)`.\n        //\n        // This can be rewritten `2**log2(a) <= a < 2**(log2(a) + 1)`\n        //  `sqrt(2**k) <= sqrt(a) < sqrt(2**(k+1))`\n        //  `2**(k/2) <= sqrt(a) < 2**((k+1)/2) <= 2**(k/2 + 1)`\n        //\n        // Consequently, `2**(log2(a) / 2)` is a good first approximation of `sqrt(a)` with at least 1 correct bit.\n        uint256 result = 1 << (log2(a) >> 1);\n\n        // At this point `result` is an estimation with one bit of precision. We know the true value is a uint128,\n        // since it is the square root of a uint256. Newton's method converges quadratically (precision doubles at\n        // every iteration). We thus need at most 7 iteration to turn our partial result with one bit of precision\n        // into the expected uint128 result.\n        unchecked {\n            result = (result + a / result) >> 1;\n            result = (result + a / result) >> 1;\n            result = (result + a / result) >> 1;\n            result = (result + a / result) >> 1;\n            result = (result + a / result) >> 1;\n            result = (result + a / result) >> 1;\n            result = (result + a / result) >> 1;\n            return min(result, a / result);\n        }\n    }\n\n    /**\n     * @notice Calculates sqrt(a), following the selected rounding direction.\n     */\n    function sqrt(uint256 a, Rounding rounding) internal pure returns (uint256) {\n        unchecked {\n            uint256 result = sqrt(a);\n            return result + (rounding == Rounding.Up && result * result < a ? 1 : 0);\n        }\n    }\n\n    /**\n     * @dev Return the log in base 2, rounded down, of a positive value.\n     * Returns 0 if given 0.\n     */\n    function log2(uint256 value) internal pure returns (uint256) {\n        uint256 result = 0;\n        unchecked {\n            if (value >> 128 > 0) {\n                value >>= 128;\n                result += 128;\n            }\n            if (value >> 64 > 0) {\n                value >>= 64;\n                result += 64;\n            }\n            if (value >> 32 > 0) {\n                value >>= 32;\n                result += 32;\n            }\n            if (value >> 16 > 0) {\n                value >>= 16;\n                result += 16;\n            }\n            if (value >> 8 > 0) {\n                value >>= 8;\n                result += 8;\n            }\n            if (value >> 4 > 0) {\n                value >>= 4;\n                result += 4;\n            }\n            if (value >> 2 > 0) {\n                value >>= 2;\n                result += 2;\n            }\n            if (value >> 1 > 0) {\n                result += 1;\n            }\n        }\n        return result;\n    }\n\n    /**\n     * @dev Return the log in base 2, following the selected rounding direction, of a positive value.\n     * Returns 0 if given 0.\n     */\n    function log2(uint256 value, Rounding rounding) internal pure returns (uint256) {\n        unchecked {\n            uint256 result = log2(value);\n            return result + (rounding == Rounding.Up && 1 << result < value ? 1 : 0);\n        }\n    }\n\n    /**\n     * @dev Return the log in base 10, rounded down, of a positive value.\n     * Returns 0 if given 0.\n     */\n    function log10(uint256 value) internal pure returns (uint256) {\n        uint256 result = 0;\n        unchecked {\n            if (value >= 10 ** 64) {\n                value /= 10 ** 64;\n                result += 64;\n            }\n            if (value >= 10 ** 32) {\n                value /= 10 ** 32;\n                result += 32;\n            }\n            if (value >= 10 ** 16) {\n                value /= 10 ** 16;\n                result += 16;\n            }\n            if (value >= 10 ** 8) {\n                value /= 10 ** 8;\n                result += 8;\n            }\n            if (value >= 10 ** 4) {\n                value /= 10 ** 4;\n                result += 4;\n            }\n            if (value >= 10 ** 2) {\n                value /= 10 ** 2;\n                result += 2;\n            }\n            if (value >= 10 ** 1) {\n                result += 1;\n            }\n        }\n        return result;\n    }\n\n    /**\n     * @dev Return the log in base 10, following the selected rounding direction, of a positive value.\n     * Returns 0 if given 0.\n     */\n    function log10(uint256 value, Rounding rounding) internal pure returns (uint256) {\n        unchecked {\n            uint256 result = log10(value);\n            return result + (rounding == Rounding.Up && 10 ** result < value ? 1 : 0);\n        }\n    }\n\n    /**\n     * @dev Return the log in base 256, rounded down, of a positive value.\n     * Returns 0 if given 0.\n     *\n     * Adding one to the result gives the number of pairs of hex symbols needed to represent `value` as a hex string.\n     */\n    function log256(uint256 value) internal pure returns (uint256) {\n        uint256 result = 0;\n        unchecked {\n            if (value >> 128 > 0) {\n                value >>= 128;\n                result += 16;\n            }\n            if (value >> 64 > 0) {\n                value >>= 64;\n                result += 8;\n            }\n            if (value >> 32 > 0) {\n                value >>= 32;\n                result += 4;\n            }\n            if (value >> 16 > 0) {\n                value >>= 16;\n                result += 2;\n            }\n            if (value >> 8 > 0) {\n                result += 1;\n            }\n        }\n        return result;\n    }\n\n    /**\n     * @dev Return the log in base 256, following the selected rounding direction, of a positive value.\n     * Returns 0 if given 0.\n     */\n    function log256(uint256 value, Rounding rounding) internal pure returns (uint256) {\n        unchecked {\n            uint256 result = log256(value);\n            return result + (rounding == Rounding.Up && 1 << (result << 3) < value ? 1 : 0);\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/math/SignedMath.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.8.0) (utils/math/SignedMath.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Standard signed math utilities missing in the Solidity language.\n */\nlibrary SignedMath {\n    /**\n     * @dev Returns the largest of two signed numbers.\n     */\n    function max(int256 a, int256 b) internal pure returns (int256) {\n        return a > b ? a : b;\n    }\n\n    /**\n     * @dev Returns the smallest of two signed numbers.\n     */\n    function min(int256 a, int256 b) internal pure returns (int256) {\n        return a < b ? a : b;\n    }\n\n    /**\n     * @dev Returns the average of two signed numbers without overflow.\n     * The result is rounded towards zero.\n     */\n    function average(int256 a, int256 b) internal pure returns (int256) {\n        // Formula from the book \"Hacker's Delight\"\n        int256 x = (a & b) + ((a ^ b) >> 1);\n        return x + (int256(uint256(x) >> 255) & (a ^ b));\n    }\n\n    /**\n     * @dev Returns the absolute unsigned value of a signed value.\n     */\n    function abs(int256 n) internal pure returns (uint256) {\n        unchecked {\n            // must be unchecked in order to support `n = type(int256).min`\n            return uint256(n >= 0 ? n : -n);\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Strings.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (utils/Strings.sol)\n\npragma solidity ^0.8.0;\n\nimport \"./math/Math.sol\";\nimport \"./math/SignedMath.sol\";\n\n/**\n * @dev String operations.\n */\nlibrary Strings {\n    bytes16 private constant _SYMBOLS = \"0123456789abcdef\";\n    uint8 private constant _ADDRESS_LENGTH = 20;\n\n    /**\n     * @dev Converts a `uint256` to its ASCII `string` decimal representation.\n     */\n    function toString(uint256 value) internal pure returns (string memory) {\n        unchecked {\n            uint256 length = Math.log10(value) + 1;\n            string memory buffer = new string(length);\n            uint256 ptr;\n            /// @solidity memory-safe-assembly\n            assembly {\n                ptr := add(buffer, add(32, length))\n            }\n            while (true) {\n                ptr--;\n                /// @solidity memory-safe-assembly\n                assembly {\n                    mstore8(ptr, byte(mod(value, 10), _SYMBOLS))\n                }\n                value /= 10;\n                if (value == 0) break;\n            }\n            return buffer;\n        }\n    }\n\n    /**\n     * @dev Converts a `int256` to its ASCII `string` decimal representation.\n     */\n    function toString(int256 value) internal pure returns (string memory) {\n        return string(abi.encodePacked(value < 0 ? \"-\" : \"\", toString(SignedMath.abs(value))));\n    }\n\n    /**\n     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.\n     */\n    function toHexString(uint256 value) internal pure returns (string memory) {\n        unchecked {\n            return toHexString(value, Math.log256(value) + 1);\n        }\n    }\n\n    /**\n     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.\n     */\n    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {\n        bytes memory buffer = new bytes(2 * length + 2);\n        buffer[0] = \"0\";\n        buffer[1] = \"x\";\n        for (uint256 i = 2 * length + 1; i > 1; --i) {\n            buffer[i] = _SYMBOLS[value & 0xf];\n            value >>= 4;\n        }\n        require(value == 0, \"Strings: hex length insufficient\");\n        return string(buffer);\n    }\n\n    /**\n     * @dev Converts an `address` with fixed length of 20 bytes to its not checksummed ASCII `string` hexadecimal representation.\n     */\n    function toHexString(address addr) internal pure returns (string memory) {\n        return toHexString(uint256(uint160(addr)), _ADDRESS_LENGTH);\n    }\n\n    /**\n     * @dev Returns true if the two strings are equal.\n     */\n    function equal(string memory a, string memory b) internal pure returns (bool) {\n        return keccak256(bytes(a)) == keccak256(bytes(b));\n    }\n}\n"
    },
    "contracts/Escrow.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\nimport { ECDSA } from \"@openzeppelin/contracts/utils/cryptography/ECDSA.sol\";\nimport { IERC20 } from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport { SafeERC20 } from \"@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol\";\nimport { Ownable } from \"@openzeppelin/contracts/access/Ownable.sol\";\nimport { Pausable } from \"@openzeppelin/contracts/security/Pausable.sol\";\nimport { SignatureChecker } from \"@openzeppelin/contracts/utils/cryptography/SignatureChecker.sol\";\n\nimport { AddressArrayUtils } from \"./external/AddressArrayUtils.sol\";\nimport { Bytes32ArrayUtils } from \"./external/Bytes32ArrayUtils.sol\";\nimport { StringArrayUtils } from \"./external/StringArrayUtils.sol\";\nimport { Uint256ArrayUtils } from \"./external/Uint256ArrayUtils.sol\";\n\nimport { IEscrow } from \"./interfaces/IEscrow.sol\";\nimport { IOrchestrator } from \"./interfaces/IOrchestrator.sol\";\nimport { IPostIntentHook } from \"./interfaces/IPostIntentHook.sol\";\nimport { IPaymentVerifier } from \"./interfaces/IPaymentVerifier.sol\";\nimport { IPaymentVerifierRegistry } from \"./interfaces/IPaymentVerifierRegistry.sol\";\nimport { IPostIntentHookRegistry } from \"./interfaces/IPostIntentHookRegistry.sol\";\nimport { IRelayerRegistry } from \"./interfaces/IRelayerRegistry.sol\";\npragma solidity ^0.8.18;\n\n/**\n * @title Escrow\n * @notice Escrows deposits and manages deposit lifecycle.\n */\ncontract Escrow is Ownable, Pausable, IEscrow {\n\n    using AddressArrayUtils for address[];\n    using Bytes32ArrayUtils for bytes32[];\n    using ECDSA for bytes32;\n    using SafeERC20 for IERC20;\n    using SignatureChecker for address;\n    using StringArrayUtils for string[];\n    using Uint256ArrayUtils for uint256[];\n\n    /* ============ Constants ============ */\n    uint256 internal constant PRECISE_UNIT = 1e18;\n    uint256 internal constant MAX_DUST_THRESHOLD = 1e6;            // 1 USDC\n    uint256 internal constant MAX_TOTAL_INTENT_EXPIRATION_PERIOD = 86400 * 5; // 5 days\n    uint256 internal constant PRUNE_ALL_EXPIRED_INTENTS = type(uint256).max;\n    \n    /* ============ State Variables ============ */\n\n    IOrchestrator public orchestrator;                               // Address of the orchestrator contract\n    IPaymentVerifierRegistry public paymentVerifierRegistry;         // Address of the payment verifier registry contract\n    uint256 immutable public chainId;                                // chainId of the chain the escrow is deployed on\n\n    mapping(address => uint256[]) internal accountDeposits;          // Mapping of address to depositIds\n\n    // Mapping of depositId to verifier address to deposit's verification data. A single deposit can support multiple payment \n    // services. Each payment service has it's own verification data which includes the payee details hash and the data used for \n    // payment verification.\n    // Example: Deposit 1 => Venmo => payeeDetails: 0x123, data: 0x456\n    //                    => Revolut => payeeDetails: 0x789, data: 0xabc\n    mapping(uint256 => mapping(bytes32 => DepositPaymentMethodData)) internal depositPaymentMethodData;\n    mapping(uint256 => bytes32[]) internal depositPaymentMethods;          // Handy mapping to get all payment methods for a deposit\n    mapping(uint256 => mapping(bytes32 => bool)) internal depositPaymentMethodActive; // Handy mapping for checking if a payment method is active for a deposit\n    // Track if a payment method has ever been listed for this deposit (to avoid array contains checks and duplicates in depositPaymentMethods)\n    mapping(uint256 => mapping(bytes32 => bool)) internal depositPaymentMethodListed;\n    \n    // Mapping of depositId to verifier address to mapping of fiat currency to min conversion rate. Each payment service can support\n    // multiple currencies. Depositor can specify list of currencies and min conversion rates for each payment service.\n    // Example: Deposit 1 => Venmo => USD: 1e18\n    //                    => Revolut => USD: 1e18, EUR: 1.2e18, SGD: 1.5e18\n    mapping(uint256 => mapping(bytes32 => mapping(bytes32 => uint256))) internal depositCurrencyMinRate;\n    mapping(uint256 => mapping(bytes32 => bytes32[])) internal depositCurrencies; // Handy mapping to get all currencies for a deposit and verifier\n    // Do not need to track if a currency is active; if it's min rate is 0 then it's not active\n    // Track if a currency code has ever been listed for this deposit+paymentMethod (avoid contains scans and duplicates in depositCurrencies)\n    mapping(uint256 => mapping(bytes32 => mapping(bytes32 => bool))) internal depositCurrencyListed;\n\n    mapping(uint256 => Deposit) internal deposits;                          // Mapping of depositIds to deposit structs\n    mapping(uint256 => bytes32[]) internal depositIntentHashes;             // Mapping of depositId to array of intentHashes\n    mapping(uint256 => mapping(bytes32 => Intent)) internal depositIntents; // Mapping of depositId to intentHash to intent\n\n    uint256 public depositCounter;          // Counter for depositIds\n    \n    address public dustRecipient;           // Address that receives dust\n    uint256 public dustThreshold;           // Amount below which deposits are considered dust and can be closed\n    uint256 public maxIntentsPerDeposit;    // Maximum active intents per deposit (suggested to keep below 100 to prevent deposit withdraw DOS)\n    uint256 public intentExpirationPeriod;  // Time period after which an intent expires\n\n    /* ============ Modifiers ============ */\n\n    /**\n     * @notice Modifier to check if caller is depositor or their delegate for a specific deposit\n     * @param _depositId The deposit ID to check authorization for\n     */\n    modifier onlyDepositorOrDelegate(uint256 _depositId) {\n        Deposit storage deposit = deposits[_depositId];\n        if (!(deposit.depositor == msg.sender || \n            (deposit.delegate != address(0) && deposit.delegate == msg.sender))) {\n            revert UnauthorizedCallerOrDelegate(msg.sender, deposit.depositor, deposit.delegate);\n        }\n        _;\n    }\n\n    /**\n     * @notice Modifier to restrict access to orchestrator-only functions\n     */\n    modifier onlyOrchestrator() {\n        if (msg.sender != address(orchestrator)) revert UnauthorizedCaller(msg.sender, address(orchestrator));\n        _;\n    }\n\n    /* ============ Constructor ============ */\n    constructor(\n        address _owner,\n        uint256 _chainId,\n        address _paymentVerifierRegistry,\n        address _dustRecipient,\n        uint256 _dustThreshold,\n        uint256 _maxIntentsPerDeposit,\n        uint256 _intentExpirationPeriod\n    )\n        Ownable()\n    {\n        chainId = _chainId;\n        paymentVerifierRegistry = IPaymentVerifierRegistry(_paymentVerifierRegistry);\n        dustRecipient = _dustRecipient;\n        dustThreshold = _dustThreshold;\n        maxIntentsPerDeposit = _maxIntentsPerDeposit;\n        intentExpirationPeriod = _intentExpirationPeriod;\n\n        transferOwnership(_owner);\n    }\n\n    /* ============ Deposit Owner Only (External Functions) ============ */\n\n    /**\n     * @notice Creates a deposit entry by locking liquidity in the escrow contract that can be taken by signaling intents. This function will \n     * not add to previous deposits. Every deposit has it's own unique identifier. User must approve the contract to transfer the deposit amount\n     * of deposit token. Every deposit specifies the payment methods it supports by specifying their verification data, supported currencies and \n     * their min conversion rates for each payment method. Optionally, a delegate to manage the deposit can be specified.\n     * Note that the order of the payment methods, verification data, and currency data must match.\n     */\n    function createDeposit(CreateDepositParams calldata _params) external whenNotPaused {\n        // Checks\n        if (_params.intentAmountRange.min == 0) revert ZeroMinValue();\n        if (_params.intentAmountRange.min > _params.intentAmountRange.max) { \n            revert InvalidRange(_params.intentAmountRange.min, _params.intentAmountRange.max);\n        }\n        if (_params.amount < _params.intentAmountRange.min) {\n            revert AmountBelowMin(_params.amount, _params.intentAmountRange.min);\n        }\n        \n        // Effects\n        uint256 depositId = depositCounter++;\n        accountDeposits[msg.sender].push(depositId);\n        deposits[depositId] = Deposit({\n            depositor: msg.sender,\n            delegate: _params.delegate,\n            token: _params.token,\n            intentAmountRange: _params.intentAmountRange,\n            acceptingIntents: true,\n            remainingDeposits: _params.amount,\n            outstandingIntentAmount: 0,\n            intentGuardian: _params.intentGuardian,\n            retainOnEmpty: _params.retainOnEmpty\n        });\n\n        emit DepositReceived(\n            depositId, \n            msg.sender, \n            _params.token,\n            _params.amount,\n            _params.intentAmountRange, \n            _params.delegate, \n            _params.intentGuardian\n        );\n\n        _addPaymentMethodsToDeposit(depositId, _params.paymentMethods, _params.paymentMethodData, _params.currencies);\n\n        // Interactions\n        _params.token.safeTransferFrom(msg.sender, address(this), _params.amount);\n    }\n\n    /**\n     * @notice Adds additional funds to an existing deposit. Only the depositor can add funds.\n     * The funds will be added to the remaining deposits amount, making it available for new intents.\n     *\n     * @param _depositId    The deposit ID to add funds to\n     * @param _amount       The amount of tokens to add\n     */\n    function addFunds(uint256 _depositId, uint256 _amount)\n        external\n        whenNotPaused\n    {\n        // Checks\n        Deposit storage deposit = deposits[_depositId];\n        if (deposit.depositor != msg.sender) revert UnauthorizedCaller(msg.sender, deposit.depositor);\n        if (_amount == 0) revert ZeroValue();\n        \n        // Effects\n        deposit.remainingDeposits += _amount;\n        \n        emit DepositFundsAdded(_depositId, msg.sender, _amount);\n        \n        // Interactions\n        deposit.token.safeTransferFrom(msg.sender, address(this), _amount);\n    }\n\n    /**\n     * @notice Removes funds from an existing deposit. Only the depositor can remove funds. If the amount to remove is greater\n     * than the remaining deposits, then expired intents will be pruned to reclaim liquidity. If the remaining deposits is less than\n     * the min intent amount, then the deposit will be marked as not accepting intents. \n     *\n     * @param _depositId    The deposit ID to remove funds from\n     * @param _amount       The amount of tokens to remove\n     */\n    function removeFunds(uint256 _depositId, uint256 _amount)\n        external\n        whenNotPaused\n    {\n        // Checks\n        Deposit storage deposit = deposits[_depositId];\n        if (deposit.depositor != msg.sender) revert UnauthorizedCaller(msg.sender, deposit.depositor);\n        if (_amount == 0) revert ZeroValue();\n        \n        // Effects\n        bytes32[] memory expiredIntents = _reclaimLiquidityIfNecessary(deposit, _depositId, _amount);\n        if (deposit.remainingDeposits < _amount) {\n            revert InsufficientDepositLiquidity(_depositId, deposit.remainingDeposits, _amount);\n        }\n\n        deposit.remainingDeposits -= _amount;\n        _handleAcceptingIntentsState(_depositId);\n\n        emit DepositWithdrawn(_depositId, msg.sender, _amount);\n        \n        // Interactions\n        deposit.token.safeTransfer(msg.sender, _amount);\n\n        // Prune intents on the orchestrator\n        if (expiredIntents.length > 0) {\n            _callOrchestratorToPruneIntents(expiredIntents);\n        }\n    }\n\n    /**\n     * @notice Depositor is returned all remaining deposits and any outstanding intents that are expired. Only the depositor can withdraw. \n     * If an intent is not expired then those funds will not be returned. Deposit is marked as to not accept new intents and the funds\n     * locked due to intents can be withdrawn once they expire by calling this function again. Deposit will be deleted as long as there are\n     * no more outstanding intents.\n     *\n     * @param _depositId   DepositId the depositor is attempting to withdraw\n     */\n    function withdrawDeposit(uint256 _depositId) external {\n        // Checks\n        Deposit storage deposit = deposits[_depositId];\n        if (deposit.depositor != msg.sender) revert UnauthorizedCaller(msg.sender, deposit.depositor);\n\n        // Effects\n        bytes32[] memory expiredIntents = _reclaimLiquidityIfNecessary(deposit, _depositId, PRUNE_ALL_EXPIRED_INTENTS);\n\n        uint256 returnAmount = deposit.remainingDeposits;\n        IERC20 token = deposit.token;\n        delete deposit.remainingDeposits;\n        \n        _handleAcceptingIntentsState(_depositId);\n\n        emit DepositWithdrawn(_depositId, deposit.depositor, returnAmount);\n\n        _closeDepositIfNecessary(_depositId, deposit);\n        \n        // Interactions\n        token.safeTransfer(msg.sender, returnAmount);\n\n        // Prune intents on the orchestrator\n        if (expiredIntents.length > 0) {\n            _callOrchestratorToPruneIntents(expiredIntents);\n        }\n    }\n\n    /* ============ Deposit Delegate management ============ */\n\n    /**\n     * @notice Allows depositor to set a delegate address that can manage a specific deposit\n     *\n     * @param _depositId    The deposit ID\n     * @param _delegate     The address to set as delegate (address(0) to remove delegate)\n     */\n    \n    function setDelegate(uint256 _depositId, address _delegate) external {\n        Deposit storage deposit = deposits[_depositId];\n        if (deposit.depositor != msg.sender) revert UnauthorizedCaller(msg.sender, deposit.depositor);\n        if (_delegate == address(0)) revert ZeroAddress();\n        \n        deposit.delegate = _delegate;\n        \n        emit DepositDelegateSet(_depositId, msg.sender, _delegate);\n    }\n\n    /**\n     * @notice Allows depositor to remove the delegate for a specific deposit\n     *\n     * @param _depositId    The deposit ID\n     */\n    function removeDelegate(uint256 _depositId) external {\n        Deposit storage deposit = deposits[_depositId];\n        if (deposit.depositor != msg.sender) revert UnauthorizedCaller(msg.sender, deposit.depositor);\n        if (deposit.delegate == address(0)) revert DelegateNotFound(_depositId);\n        \n        delete deposit.delegate;\n        \n        emit DepositDelegateRemoved(_depositId, msg.sender);\n    }\n\n    /* ============ Deposit Owner OR Delegate Only (External Functions) ============ */\n\n    /**\n     * @notice Only callable by the depositor/delegate for a deposit. Allows depositor/delegate to update the min conversion rate for a \n     * currency for a payment verifier provided the currency was previously listed (otherwise use addCurrenciesToDepositPaymentMethod). \n     * Since intent's store the conversion rate at the time of intent, changing the min conversion rate will not affect any intents that \n     * have already been signaled.\n     *\n     * @param _depositId                The deposit ID\n     * @param _paymentMethod            The payment method to update the min conversion rate for\n     * @param _fiatCurrency             The fiat currency code to update the min conversion rate for\n     * @param _newMinConversionRate     The new min conversion rate. Must be greater than 0.\n     */\n    function setCurrencyMinRate(\n        uint256 _depositId, \n        bytes32 _paymentMethod, \n        bytes32 _fiatCurrency, \n        uint256 _newMinConversionRate\n    )\n        external\n        whenNotPaused\n        onlyDepositorOrDelegate(_depositId)\n    {\n        // Allow reactivation from 0 if currency was previously listed\n        if (!depositCurrencyListed[_depositId][_paymentMethod][_fiatCurrency]) {\n            revert CurrencyNotSupported(_paymentMethod, _fiatCurrency);\n        }\n        if (_newMinConversionRate == 0) revert ZeroConversionRate();\n\n        depositCurrencyMinRate[_depositId][_paymentMethod][_fiatCurrency] = _newMinConversionRate;\n\n        emit DepositMinConversionRateUpdated(_depositId, _paymentMethod, _fiatCurrency, _newMinConversionRate);\n    }\n\n    /**\n     * @notice Allows depositor to update the intent amount range for a deposit. Since intent's are already created within the\n     * previous intent amount range, changing the intent amount range will not affect any intents that have already been signaled.\n     *\n     * @param _depositId                The deposit ID\n     * @param _intentAmountRange        The new intent amount range\n     */\n    function setIntentRange(\n        uint256 _depositId, \n        Range calldata _intentAmountRange\n    )\n        external\n        whenNotPaused\n        onlyDepositorOrDelegate(_depositId)\n    {\n        Deposit storage deposit = deposits[_depositId];\n        if (_intentAmountRange.min == 0) revert ZeroMinValue();\n        if (_intentAmountRange.min > _intentAmountRange.max) revert InvalidRange(_intentAmountRange.min, _intentAmountRange.max);\n\n        deposit.intentAmountRange = _intentAmountRange;\n        \n        _handleAcceptingIntentsState(_depositId);\n\n        emit DepositIntentAmountRangeUpdated(_depositId, _intentAmountRange);\n    }\n\n    /**\n     * @notice Allows depositor to add a new payment verifier and its associated currencies to an existing deposit.\n     * @dev WARNING: Adding excessive payment methods or currencies may cause withdrawal to exceed gas limits. Depositors\n     * can remove entries individually if needed. Recommended: <10 payment methods, <50 currencies each.\n     *\n     * @param _depositId             The deposit ID\n     * @param _paymentMethods        The payment methods to add\n     * @param _paymentMethodData     The payment verification data for the payment methods\n     * @param _currencies            The currencies for the payment methods\n     */\n    function addPaymentMethods(\n        uint256 _depositId,\n        bytes32[] calldata _paymentMethods,\n        DepositPaymentMethodData[] calldata _paymentMethodData,\n        Currency[][] calldata _currencies\n    )\n        external\n        whenNotPaused\n        onlyDepositorOrDelegate(_depositId)\n    {\n        _addPaymentMethodsToDeposit(_depositId, _paymentMethods, _paymentMethodData, _currencies);\n    }\n\n    /**\n     * @notice Allows depositor or delegate to toggle a payment method's active state without removing it.\n     * This function allows depositor to remove an existing payment verifier from a deposit by setting active to false. The\n     * deposit and currencies data is not deleted immediately and instead deferred to when the deposit is fully closed.\n     * NOTE: This function does not delete the payment method data to allow existing intents to be fulfilled.\n     *\n     * @param _depositId     The deposit ID\n     * @param _paymentMethod The payment method\n     * @param _isActive      New active state\n     */\n    function setPaymentMethodActive(\n        uint256 _depositId,\n        bytes32 _paymentMethod,\n        bool _isActive\n    )\n        external\n        whenNotPaused\n        onlyDepositorOrDelegate(_depositId)\n    {\n        if (!depositPaymentMethodListed[_depositId][_paymentMethod]) revert PaymentMethodNotListed(_depositId, _paymentMethod);\n        if (depositPaymentMethodActive[_depositId][_paymentMethod] == _isActive) {\n            revert DepositAlreadyInState(_depositId, _isActive);\n        }\n        depositPaymentMethodActive[_depositId][_paymentMethod] = _isActive;\n        emit DepositPaymentMethodActiveUpdated(_depositId, _paymentMethod, _isActive);\n    }\n\n    /**\n     * @notice Allows depositor to add a new currencies to an existing verifier for a deposit.\n     * @dev WARNING: Adding excessive currencies may cause withdrawal to exceed gas limits. Depositors\n     * can remove entries individually if needed. Recommended: <50 currencies per payment method.\n     *\n     * @param _depositId             The deposit ID\n     * @param _paymentMethod         The payment method\n     * @param _currencies            The currencies to add (code and conversion rate)\n     */\n    function addCurrencies(\n        uint256 _depositId,\n        bytes32 _paymentMethod,\n        Currency[] calldata _currencies\n    )\n        external\n        whenNotPaused\n        onlyDepositorOrDelegate(_depositId)\n    {\n        if (!depositPaymentMethodActive[_depositId][_paymentMethod]) revert PaymentMethodNotActive(_depositId, _paymentMethod);\n        \n        for (uint256 i = 0; i < _currencies.length; i++) {\n            _addCurrencyToDeposit(\n                _depositId, \n                _paymentMethod, \n                _currencies[i].code, \n                _currencies[i].minConversionRate\n            );\n        }\n    }\n\n    /**\n     * @notice Allows depositor to deactivate a currency from a verifier for a deposit. The currency is deactivated.\n     *\n     * @param _depositId             The deposit ID\n     * @param _paymentMethod         The payment method\n     * @param _currencyCode          The currency code to deactivate\n     */\n    function deactivateCurrency(\n        uint256 _depositId,\n        bytes32 _paymentMethod,\n        bytes32 _currencyCode\n    )   \n        external\n        whenNotPaused\n        onlyDepositorOrDelegate(_depositId)\n    {\n        if (!depositPaymentMethodActive[_depositId][_paymentMethod]) revert PaymentMethodNotActive(_depositId, _paymentMethod);\n        if (!depositCurrencyListed[_depositId][_paymentMethod][_currencyCode]) revert CurrencyNotFound(_paymentMethod, _currencyCode);\n        \n        depositCurrencyMinRate[_depositId][_paymentMethod][_currencyCode] = 0;\n\n        emit DepositMinConversionRateUpdated(_depositId, _paymentMethod, _currencyCode, 0);\n    }\n\n    /**\n     * @notice Allows depositor or delegateto set the accepting intents state for a deposit.\n     *\n     * @param _depositId             The deposit ID\n     * @param _acceptingIntents      The new accepting intents state\n     */\n    function setAcceptingIntents(\n        uint256 _depositId, \n        bool _acceptingIntents\n    )\n        external\n        whenNotPaused\n        onlyDepositorOrDelegate(_depositId)\n    {\n        Deposit storage deposit = deposits[_depositId];\n        if (deposit.acceptingIntents == _acceptingIntents) revert DepositAlreadyInState(_depositId, _acceptingIntents);\n\n        // If accepting intents, check if there is enough liquidity to accept the minimum intent amount\n        if (\n            _acceptingIntents && \n            deposit.remainingDeposits < deposit.intentAmountRange.min\n        ) revert InsufficientDepositLiquidity(_depositId, deposit.remainingDeposits, deposit.intentAmountRange.min);\n        \n        \n        deposit.acceptingIntents = _acceptingIntents;\n        emit DepositAcceptingIntentsUpdated(_depositId, _acceptingIntents);\n    }\n\n    /**\n     * @notice Allows depositor or delegate to set the retain-on-empty behavior for a deposit. When true, the deposit\n     * will not auto-close or sweep dust when empty; config (payment methods, currencies, rate etc) stays for reuse.\n     *\n     * @param _depositId        The deposit ID\n     * @param _retainOnEmpty    New retain-on-empty flag value\n     */\n    function setRetainOnEmpty(\n        uint256 _depositId,\n        bool _retainOnEmpty\n    )\n        external\n        whenNotPaused\n        onlyDepositorOrDelegate(_depositId)\n    {\n        Deposit storage deposit = deposits[_depositId];\n        if (deposit.retainOnEmpty == _retainOnEmpty) revert DepositAlreadyInState(_depositId, _retainOnEmpty);\n        \n        deposit.retainOnEmpty = _retainOnEmpty;\n        \n        emit DepositRetainOnEmptyUpdated(_depositId, _retainOnEmpty);\n    }\n\n    /* ============ Anyone callable (External Functions) ============ */\n\n    /**\n     * @notice ANYONE: Can be called by anyone to clean up expired intents.\n     * \n     * @param _depositId The deposit ID to prune expired intents for\n     */\n    function pruneExpiredIntents(uint256 _depositId) external {\n        bytes32[] memory expiredIntents = _reclaimLiquidityIfNecessary(\n            deposits[_depositId], \n            _depositId, \n            PRUNE_ALL_EXPIRED_INTENTS     // Prune all expired intents\n        );\n\n        if (expiredIntents.length > 0) {\n            _callOrchestratorToPruneIntents(expiredIntents);\n        }\n    }\n\n    /* ============ Orchestrator-Only Locking and Unlocking Functions ============ */\n\n    /**\n     * @notice ORCHESTRATOR ONLY: Locks funds for an intent with expiry time. Only callable by orchestrator.\n     *\n     * @param _depositId The deposit ID to lock funds from\n     * @param _amount The amount to lock\n     * @param _intentHash The intent hash this intent corresponds to\n     */\n    function lockFunds(\n        uint256 _depositId, \n        bytes32 _intentHash,\n        uint256 _amount\n    ) \n        external \n        onlyOrchestrator \n    {\n        // Checks\n        Deposit storage deposit = deposits[_depositId];\n        if (deposit.depositor == address(0)) revert DepositNotFound(_depositId);\n        if (!deposit.acceptingIntents) revert DepositNotAcceptingIntents(_depositId);\n        if (_amount < deposit.intentAmountRange.min) revert AmountBelowMin(_amount, deposit.intentAmountRange.min);\n        if (_amount > deposit.intentAmountRange.max) revert AmountAboveMax(_amount, deposit.intentAmountRange.max);\n        // Prevent duplicate intent hashes which can corrupt liquidity accounting\n        if (depositIntents[_depositId][_intentHash].intentHash != bytes32(0)) {\n            revert IntentAlreadyExists(_depositId, _intentHash);\n        }\n        \n        // Effects\n        // Check if we need to reclaim expired liquidity first; if so, then reclaims and updates the deposit state\n        bytes32[] memory expiredIntents = _reclaimLiquidityIfNecessary(deposit, _depositId, _amount);\n    \n        // Check if we have enough liquidity after reclaiming expired liquidity\n        if (deposit.remainingDeposits < _amount) {\n            revert InsufficientDepositLiquidity(_depositId, deposit.remainingDeposits, _amount);\n        }\n\n        // Check if we exceeded the maximum number of intents after expiring intents and adding the new intent\n        uint256 newIntentCount = depositIntentHashes[_depositId].length + 1;\n        if (newIntentCount > maxIntentsPerDeposit) {\n            revert MaxIntentsExceeded(_depositId, newIntentCount, maxIntentsPerDeposit);\n        }\n        \n        // Update deposit state due to the new intent\n        deposit.remainingDeposits -= _amount;\n        deposit.outstandingIntentAmount += _amount;\n        \n        depositIntentHashes[_depositId].push(_intentHash);\n        uint256 expiryTime = block.timestamp + intentExpirationPeriod;\n        depositIntents[_depositId][_intentHash] = Intent({\n            intentHash: _intentHash,\n            amount: _amount,\n            timestamp: block.timestamp,\n            expiryTime: expiryTime\n        });\n        \n        emit FundsLocked(_depositId, _intentHash, _amount, expiryTime);\n\n        // Interactions\n        if (expiredIntents.length > 0) {\n            _callOrchestratorToPruneIntents(expiredIntents);\n        }\n    }\n\n    /**\n     * @notice ORCHESTRATOR ONLY: Unlocks funds from a cancelled intent by removing the specific intent. \n     * Only callable by orchestrator.\n     * \n     * @param _depositId The deposit ID to unlock funds from\n     * @param _intentHash The intent hash to find and remove the intent for\n     */\n    function unlockFunds(uint256 _depositId, bytes32 _intentHash) \n        external \n        onlyOrchestrator \n    {\n        // Checks\n        Deposit storage deposit = deposits[_depositId];\n        Intent memory intent = depositIntents[_depositId][_intentHash];\n\n        if (deposit.depositor == address(0)) revert DepositNotFound(_depositId);\n        if (intent.intentHash == bytes32(0)) revert IntentNotFound(_intentHash);\n\n        // Effects\n        deposit.remainingDeposits += intent.amount;\n        deposit.outstandingIntentAmount -= intent.amount;\n\n        _pruneIntent(_depositId, _intentHash);\n\n        emit FundsUnlocked(_depositId, _intentHash, intent.amount);\n    }\n\n    /**\n     * @notice ORCHESTRATOR ONLY: Unlocks and transfers funds from a fulfilled intent by removing the specific intent.\n     * Only callable by orchestrator.\n     * \n     * @param _depositId The deposit ID to transfer from\n     * @param _intentHash The intent hash to find and remove the intent for\n     * @param _transferAmount The amount to actually transfer (may be less than intent amount)\n     * @param _to The address to transfer to (orchestrator)\n     */\n    function unlockAndTransferFunds(\n        uint256 _depositId, \n        bytes32 _intentHash,\n        uint256 _transferAmount, \n        address _to\n    ) \n        external \n        onlyOrchestrator \n    {\n        // Checks\n        Deposit storage deposit = deposits[_depositId];\n        Intent memory intent = depositIntents[_depositId][_intentHash];\n        \n        if (deposit.depositor == address(0)) revert DepositNotFound(_depositId);\n        if (intent.intentHash == bytes32(0)) revert IntentNotFound(_intentHash);\n        if (_transferAmount == 0) revert ZeroValue();\n        if (_transferAmount > intent.amount) revert AmountExceedsAvailable(_transferAmount, intent.amount);\n        \n        // Effects\n        deposit.outstandingIntentAmount -= intent.amount;\n        \n        // If this is a partial release, return the unused portion to remainingDeposits\n        if (_transferAmount < intent.amount) {\n            deposit.remainingDeposits += (intent.amount - _transferAmount);\n        }\n\n        _pruneIntent(_depositId, _intentHash);\n        \n        IERC20 token = deposit.token;\n        _closeDepositIfNecessary(_depositId, deposit);\n        \n        emit FundsUnlockedAndTransferred(\n            _depositId, _intentHash, intent.amount, _transferAmount, _to\n        );\n\n        // Interactions\n        token.safeTransfer(_to, _transferAmount);\n    }\n\n    /* ============ Intent Guardian Only (External Functions) ============ */\n\n    /**\n     * @notice INTENT GUARDIAN ONLY: Extends the expiry time of an existing intent. Only callable by intent guardian.\n     * This function reverts if the total intent expiry period is greater than the maximum allowed to prevent griefing\n     * by extending the intent expiry period indefinitely.\n     * \n     * @param _depositId The deposit ID containing the intent\n     * @param _intentHash The intent hash to extend expiry for\n     * @param _additionalTime The additional time to extend the expiry by\n     */\n    function extendIntentExpiry(\n        uint256 _depositId, \n        bytes32 _intentHash,\n        uint256 _additionalTime\n    ) \n        external \n    {\n        // Checks\n        Deposit storage deposit = deposits[_depositId];\n        Intent storage intent = depositIntents[_depositId][_intentHash];\n        \n        if (deposit.depositor == address(0)) revert DepositNotFound(_depositId);\n        if (intent.intentHash == bytes32(0)) revert IntentNotFound(_intentHash);\n        if (deposit.intentGuardian != msg.sender) revert UnauthorizedCaller(msg.sender, deposit.intentGuardian);\n        if (_additionalTime == 0) revert ZeroValue();\n        if (intent.expiryTime + _additionalTime > intent.timestamp + MAX_TOTAL_INTENT_EXPIRATION_PERIOD) {\n            revert AmountAboveMax(_additionalTime, MAX_TOTAL_INTENT_EXPIRATION_PERIOD);\n        }\n        \n        // Effects\n        intent.expiryTime += _additionalTime;\n        \n        emit IntentExpiryExtended(_depositId, _intentHash, intent.expiryTime);\n    }\n\n    /* ============ Governance Functions ============ */\n    \n    /**\n     * @notice GOVERNANCE ONLY: Sets the orchestrator contract address. Only callable by owner.\n     *\n     * @param _orchestrator The orchestrator contract address\n     */\n    function setOrchestrator(address _orchestrator) external onlyOwner {\n        if (_orchestrator == address(0)) revert ZeroAddress();\n        \n        orchestrator = IOrchestrator(_orchestrator);\n        emit OrchestratorUpdated(_orchestrator);\n    }\n\n    /**\n     * @notice GOVERNANCE ONLY: Updates the payment verifier registry address.\n     *\n     * @param _paymentVerifierRegistry   New payment verifier registry address\n     */\n    function setPaymentVerifierRegistry(address _paymentVerifierRegistry) external onlyOwner {\n        if (_paymentVerifierRegistry == address(0)) revert ZeroAddress();\n        \n        paymentVerifierRegistry = IPaymentVerifierRegistry(_paymentVerifierRegistry);\n        emit PaymentVerifierRegistryUpdated(_paymentVerifierRegistry);\n    }\n\n    /** \n     * @notice GOVERNANCE ONLY: Sets the dust recipient address.\n     *\n     * @param _dustRecipient The new dust recipient address\n     */\n    function setDustRecipient(address _dustRecipient) external onlyOwner {\n        if (_dustRecipient == address(0)) revert ZeroAddress();\n        \n        dustRecipient = _dustRecipient;\n        emit DustRecipientUpdated(_dustRecipient);\n    }\n\n    /**\n     * @notice GOVERNANCE ONLY: Sets the dust threshold below which deposits can be closed automatically.\n     *\n     * @param _dustThreshold The new dust threshold amount\n     */\n    function setDustThreshold(uint256 _dustThreshold) external onlyOwner {\n        if (_dustThreshold > MAX_DUST_THRESHOLD) revert AmountAboveMax(_dustThreshold, MAX_DUST_THRESHOLD);\n        \n        dustThreshold = _dustThreshold;\n        emit DustThresholdUpdated(_dustThreshold);\n    }\n\n    /**\n     * @notice GOVERNANCE ONLY: Sets the maximum number of active intents per deposit.\n     *\n     * @param _maxIntentsPerDeposit The new maximum number of active intents per deposit\n     */\n    function setMaxIntentsPerDeposit(uint256 _maxIntentsPerDeposit) external onlyOwner {\n        if (_maxIntentsPerDeposit == 0) revert ZeroValue();\n        \n        maxIntentsPerDeposit = _maxIntentsPerDeposit;\n        emit MaxIntentsPerDepositUpdated(_maxIntentsPerDeposit);\n    }\n\n    /**\n     * @notice GOVERNANCE ONLY: Sets the intent expiration period.\n     *\n     * @param _intentExpirationPeriod The new intent expiration period in seconds\n     */\n    function setIntentExpirationPeriod(uint256 _intentExpirationPeriod) external onlyOwner {\n        if (_intentExpirationPeriod == 0) revert ZeroValue();\n        \n        intentExpirationPeriod = _intentExpirationPeriod;\n        emit IntentExpirationPeriodUpdated(_intentExpirationPeriod);\n    }\n\n    /**\n     * @notice GOVERNANCE ONLY: Pauses deposit modifications and new deposit creation.\n     * \n     * Functionalities that are paused:\n     * - Deposit creation (createDeposit)\n     * - Adding/removing funds to deposits (addFundsToDeposit, removeFundsFromDeposit)\n     * - Updating deposit parameters (conversion rates, intent ranges, accepting intents state)\n     * - Adding/removing payment methods and currencies\n     *\n     * Functionalities that remain unpaused to allow users to retrieve funds:\n     * - Full deposit withdrawal (withdrawDeposit)\n     * - Delegate management (setDepositDelegate, removeDepositDelegate)\n     * - Expired intent pruning (pruneExpiredIntentsAndReclaimLiquidity)\n     * - Orchestrator operations (lockFunds, unlockFunds, unlockAndTransferFunds)\n     * - Intent expiry extensions by guardian\n     * - All view functions\n     */\n    function pauseEscrow() external onlyOwner {\n        _pause();\n    }\n\n    /**\n     * @notice GOVERNANCE ONLY: Restarts paused functionality for the escrow.\n     */\n    function unpauseEscrow() external onlyOwner {\n        _unpause();\n    }\n\n    /* ============ External View Functions ============ */\n\n    function getDeposit(uint256 _depositId) external view returns (Deposit memory) {\n        return deposits[_depositId];\n    }\n\n    function getDepositIntentHashes(uint256 _depositId) external view returns (bytes32[] memory) {\n        return depositIntentHashes[_depositId];\n    }\n\n    function getDepositIntent(uint256 _depositId, bytes32 _intentHash) external view returns (Intent memory) {\n        return depositIntents[_depositId][_intentHash];\n    }\n\n    function getDepositPaymentMethods(uint256 _depositId) external view returns (bytes32[] memory) {\n        return depositPaymentMethods[_depositId];\n    }\n\n    function getDepositCurrencies(uint256 _depositId, bytes32 _paymentMethod) external view returns (bytes32[] memory) {\n        return depositCurrencies[_depositId][_paymentMethod];\n    }\n\n    function getDepositCurrencyMinRate(uint256 _depositId, bytes32 _paymentMethod, bytes32 _currencyCode) external view returns (uint256) {\n        return depositCurrencyMinRate[_depositId][_paymentMethod][_currencyCode];\n    }\n\n    function getDepositCurrencyListed(uint256 _depositId, bytes32 _paymentMethod, bytes32 _currencyCode) external view returns (bool) {\n        return depositCurrencyListed[_depositId][_paymentMethod][_currencyCode];\n    }\n\n    function getDepositPaymentMethodListed(uint256 _depositId, bytes32 _paymentMethod) external view returns (bool) {\n        return depositPaymentMethodListed[_depositId][_paymentMethod];\n    }\n\n    function getDepositPaymentMethodData(uint256 _depositId, bytes32 _paymentMethod) external view returns (DepositPaymentMethodData memory) {\n        return depositPaymentMethodData[_depositId][_paymentMethod];\n    }\n\n    function getDepositPaymentMethodActive(uint256 _depositId, bytes32 _paymentMethod) external view returns (bool) {\n        return depositPaymentMethodActive[_depositId][_paymentMethod];\n    }\n\n    function getDepositGatingService(uint256 _depositId, bytes32 _paymentMethod) external view returns (address) {\n        return depositPaymentMethodData[_depositId][_paymentMethod].intentGatingService;\n    }\n\n    function getAccountDeposits(address _account) external view returns (uint256[] memory) {\n        return accountDeposits[_account];\n    }\n    \n    function getExpiredIntents(uint256 _depositId) external view returns (bytes32[] memory expiredIntents, uint256 reclaimableAmount) {\n        return _getExpiredIntents(_depositId);\n    }\n\n\n    /* ============ Internal Functions ============ */\n\n    /**\n     * @notice Cycles through all intents currently open on a deposit and sees if any have expired. If they have expired\n     * the outstanding amounts are summed and returned alongside the intentHashes.\n     * Note: This function compacts the expired intents array to remove any empty slots.\n     */\n    function _getExpiredIntents(\n        uint256 _depositId\n    )\n        internal\n        view\n        returns(bytes32[] memory expiredIntents, uint256 reclaimableAmount)\n    {\n        bytes32[] memory intentHashes = depositIntentHashes[_depositId];\n        bytes32[] memory verboseExpiredIntents = new bytes32[](intentHashes.length);\n        uint256 numExpiredIntents = 0;\n\n        for (uint256 i = 0; i < intentHashes.length; ++i) {\n            Intent memory intent = depositIntents[_depositId][intentHashes[i]];\n            if (intent.expiryTime < block.timestamp) {\n                verboseExpiredIntents[i] = intentHashes[i];\n                reclaimableAmount += intent.amount;\n                numExpiredIntents++;\n            }\n        }\n\n        // Compact the expired intents array\n        expiredIntents = new bytes32[](numExpiredIntents);\n        uint256 compactedIndex = 0;\n        for (uint256 i = 0; i < intentHashes.length; ++i) {\n            if (verboseExpiredIntents[i] != bytes32(0)) {\n                expiredIntents[compactedIndex++] = verboseExpiredIntents[i];\n            }\n        }\n    }\n\n    /**\n     * @notice Free up deposit liquidity by reclaiming liquidity from expired intents. Only reclaims if remaining deposits amount\n     * is less than the minimum required amount. Returns the expired intents that need to be pruned. Only does local state updates, \n     * does not call any external contracts. Whenever this function is called, the calling function should also call _callOrchestratorToPruneIntents\n     * with the returned intents to expire the intents on the orchestrator contract.\n     */\n    function _reclaimLiquidityIfNecessary(\n        Deposit storage _deposit, \n        uint256 _depositId, \n        uint256 _minRequiredAmount\n    )\n        internal\n        returns (bytes32[] memory expiredIntents)\n    {\n        if (\n            _deposit.remainingDeposits < _minRequiredAmount || \n            depositIntentHashes[_depositId].length == maxIntentsPerDeposit\n        ) {\n            // If the deposit has insufficient liquidity or is at max intents, reclaim liquidity from expired intents\n            uint256 reclaimedAmount;\n            \n            (expiredIntents, reclaimedAmount) = _getExpiredIntents(_depositId);    \n            _deposit.remainingDeposits += reclaimedAmount;\n            _deposit.outstandingIntentAmount -= reclaimedAmount;\n\n            // Prune intents locally and emit funds unlocked events\n            for (uint256 i = 0; i < expiredIntents.length; i++) {\n                Intent memory intent = depositIntents[_depositId][expiredIntents[i]];\n                _pruneIntent(_depositId, intent.intentHash);\n                \n                emit FundsUnlocked(_depositId, intent.intentHash, intent.amount);\n            }\n        }\n    }\n\n    /**\n     * @notice Prunes an intent from a deposit locally. Does not call orchestrator.\n     */\n    function _pruneIntent(uint256 _depositId, bytes32 _intentHash) internal {\n        delete depositIntents[_depositId][_intentHash];\n        depositIntentHashes[_depositId].removeStorage(_intentHash);\n    }\n\n    /**\n      * @notice Calls the orchestrator to clean up intents. \n      * Note: If the orchestrator reverts, it is caught and ignored to allow the function to continue execution.\n      */\n    function _callOrchestratorToPruneIntents(bytes32[] memory _intents) internal {\n        try IOrchestrator(orchestrator).pruneIntents(_intents) {} catch {}\n    }\n\n    /**\n     * @notice Updates acceptingIntents based on remaining liquidity. One-way demotion only: if remainingDeposits \n     * falls below min and acceptingIntents is true, set it to false. If acceptingIntents is false, it will remain \n     * false even if remainingDeposits is increased above min. The depositor has to manually re-enable accepting \n     * intents by calling setDepositAcceptingIntents.\n     */\n    function _handleAcceptingIntentsState(uint256 _depositId) internal {\n        Deposit storage deposit = deposits[_depositId];\n        if (deposit.acceptingIntents && deposit.remainingDeposits < deposit.intentAmountRange.min) {\n            deposit.acceptingIntents = false;\n            emit DepositAcceptingIntentsUpdated(_depositId, false);\n        }\n    }\n\n    /**\n     * @notice Removes a deposit if no outstanding intents AND remaining funds is dust. Before deletion, transfers any remaining\n     * dust to the protocol dust recipient.\n     */\n    function _closeDepositIfNecessary(uint256 _depositId, Deposit storage _deposit) internal {\n        // Close if no outstanding intents and remaining deposits are at or below dust\n        uint256 totalRemaining = _deposit.remainingDeposits;\n        if (_deposit.outstandingIntentAmount == 0 && totalRemaining <= dustThreshold && !_deposit.retainOnEmpty) {\n            // Close deposit (and sweep any dust) when retention is not enabled\n            IERC20 token = _deposit.token;\n            _closeDeposit(_depositId, _deposit);\n\n            if (totalRemaining > 0) {\n                token.safeTransfer(dustRecipient, totalRemaining);\n                emit DustCollected(_depositId, totalRemaining, dustRecipient);\n            }\n        }\n    }\n\n    /**\n     * @notice Closes a deposit. Deleting a deposit deletes it from the deposits mapping and removes tracking\n     * it in the user's accountDeposits mapping. Also deletes the verification and currency data for the deposit.\n     */\n    function _closeDeposit(uint256 _depositId, Deposit storage _deposit) internal {\n        address depositor = _deposit.depositor;\n        accountDeposits[depositor].removeStorage(_depositId);\n        \n        _deleteDepositPaymentMethodAndCurrencyData(_depositId);\n        \n        delete deposits[_depositId];\n        delete _deposit.acceptingIntents;  // Might have been set to false, but delete it to be safe\n        \n        emit DepositClosed(_depositId, depositor);\n    }\n\n    /**\n     * @notice Iterates through all verifiers for a deposit and deletes the corresponding verifier data and currencies.\n     */\n    function _deleteDepositPaymentMethodAndCurrencyData(uint256 _depositId) internal {\n        bytes32[] memory paymentMethods = depositPaymentMethods[_depositId];\n        for (uint256 i = 0; i < paymentMethods.length; i++) {\n            bytes32 paymentMethod = paymentMethods[i];\n            delete depositPaymentMethodData[_depositId][paymentMethod];\n            delete depositPaymentMethodActive[_depositId][paymentMethod];\n            delete depositPaymentMethodListed[_depositId][paymentMethod];\n            bytes32[] memory currencies = depositCurrencies[_depositId][paymentMethod];\n            for (uint256 j = 0; j < currencies.length; j++) {\n                bytes32 currencyCode = currencies[j];\n                delete depositCurrencyMinRate[_depositId][paymentMethod][currencyCode];\n                delete depositCurrencyListed[_depositId][paymentMethod][currencyCode];\n            }\n            delete depositCurrencies[_depositId][paymentMethod];\n        }\n        delete depositPaymentMethods[_depositId];\n    }\n\n    /**\n     * @notice Adds list of payment methods and corresponding verification data and currencies to a deposit.\n     */\n    function _addPaymentMethodsToDeposit(\n        uint256 _depositId,\n        bytes32[] calldata _paymentMethods,\n        DepositPaymentMethodData[] calldata _paymentMethodData,\n        Currency[][] calldata _currencies\n    ) internal {\n\n        // Check that the length of the payment methods, depositPaymentMethodData, and currencies arrays are the same\n        if (_paymentMethods.length != _paymentMethodData.length) revert ArrayLengthMismatch(_paymentMethods.length, _paymentMethodData.length);\n        if (_paymentMethods.length != _currencies.length) revert ArrayLengthMismatch(_paymentMethods.length, _currencies.length);\n\n        for (uint256 i = 0; i < _paymentMethods.length; i++) {\n            bytes32 paymentMethod = _paymentMethods[i];\n            \n            // Validate payment method\n            if (paymentMethod == bytes32(0)) revert ZeroAddress();\n            if (!paymentVerifierRegistry.isPaymentMethod(paymentMethod)) {\n                revert PaymentMethodNotWhitelisted(paymentMethod);\n            }\n            if (_paymentMethodData[i].payeeDetails == bytes32(0)) revert EmptyPayeeDetails();\n            // Prevent duplicates using O(1) listed flag; if already listed (active or inactive) revert\n            if (depositPaymentMethodListed[_depositId][paymentMethod]) revert PaymentMethodAlreadyExists(_depositId, paymentMethod);\n\n            // Add payment method\n            depositPaymentMethodData[_depositId][paymentMethod] = _paymentMethodData[i];\n            depositPaymentMethods[_depositId].push(paymentMethod);\n            depositPaymentMethodListed[_depositId][paymentMethod] = true;\n            depositPaymentMethodActive[_depositId][paymentMethod] = true;\n\n            emit DepositPaymentMethodAdded(_depositId, paymentMethod, _paymentMethodData[i].payeeDetails, _paymentMethodData[i].intentGatingService);\n\n            for (uint256 j = 0; j < _currencies[i].length; j++) {\n                Currency memory currency = _currencies[i][j];\n\n                _addCurrencyToDeposit(\n                    _depositId, \n                    paymentMethod, \n                    currency.code, \n                    currency.minConversionRate\n                );\n            }\n        }\n    }\n\n    /**\n     * @notice Adds a currency to a deposit.\n     */\n    function _addCurrencyToDeposit(\n        uint256 _depositId,\n        bytes32 _paymentMethod,\n        bytes32 _currencyCode,\n        uint256 _minConversionRate\n    ) internal {\n        // Validate currency\n        if (!paymentVerifierRegistry.isCurrency(_paymentMethod, _currencyCode)) {\n            revert CurrencyNotSupported(_paymentMethod, _currencyCode);\n        }\n        if (_minConversionRate == 0) revert ZeroConversionRate();\n        if (depositCurrencyListed[_depositId][_paymentMethod][_currencyCode]) {\n            revert CurrencyAlreadyExists(_paymentMethod, _currencyCode);\n        }\n\n        // Add currency\n        depositCurrencyMinRate[_depositId][_paymentMethod][_currencyCode] = _minConversionRate;\n        depositCurrencies[_depositId][_paymentMethod].push(_currencyCode);\n        depositCurrencyListed[_depositId][_paymentMethod][_currencyCode] = true;\n\n        emit DepositCurrencyAdded(_depositId, _paymentMethod, _currencyCode, _minConversionRate);\n    }\n}\n"
    },
    "contracts/external/AddressArrayUtils.sol": {
      "content": "/*\n    Copyright 2020 Set Labs Inc.\n\n    Licensed under the Apache License, Version 2.0 (the \"License\");\n    you may not use this file except in compliance with the License.\n    You may obtain a copy of the License at\n\n    http://www.apache.org/licenses/LICENSE-2.0\n\n    Unless required by applicable law or agreed to in writing, software\n    distributed under the License is distributed on an \"AS IS\" BASIS,\n    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.\n    See the License for the specific language governing permissions and\n    limitations under the License.\n\n    SPDX-License-Identifier: MIT\n*/\n\npragma solidity ^0.8.17;\n\n/**\n * @title AddressArrayUtils\n * @author Set Protocol\n *\n * Utility functions to handle Address Arrays\n *\n * CHANGELOG:\n * - 4/21/21: Added validatePairsWithArray methods\n */\nlibrary AddressArrayUtils {\n\n    uint256 constant internal MAX_INT = 2**256 - 1;\n\n    /**\n     * Finds the index of the first occurrence of the given element.\n     * @param A The input array to search\n     * @param a The value to find\n     * @return Returns (index and isIn) for the first occurrence starting from index 0\n     */\n    function indexOf(address[] memory A, address a) internal pure returns (uint256, bool) {\n        uint256 length = A.length;\n        for (uint256 i = 0; i < length; i++) {\n            if (A[i] == a) {\n                return (i, true);\n            }\n        }\n        return (MAX_INT, false);\n    }\n\n    /**\n    * Returns true if the value is present in the list. Uses indexOf internally.\n    * @param A The input array to search\n    * @param a The value to find\n    * @return Returns isIn for the first occurrence starting from index 0\n    */\n    function contains(address[] memory A, address a) internal pure returns (bool) {\n        (, bool isIn) = indexOf(A, a);\n        return isIn;\n    }\n\n    /**\n    * Returns true if there are 2 elements that are the same in an array\n    * @param A The input array to search\n    * @return Returns boolean for the first occurrence of a duplicate\n    */\n    function hasDuplicate(address[] memory A) internal pure returns(bool) {\n        require(A.length > 0, \"A is empty\");\n\n        for (uint256 i = 0; i < A.length - 1; i++) {\n            address current = A[i];\n            for (uint256 j = i + 1; j < A.length; j++) {\n                if (current == A[j]) {\n                    return true;\n                }\n            }\n        }\n        return false;\n    }\n\n    /**\n     * @param A The input array to search\n     * @param a The address to remove\n     * @return Returns the array with the object removed.\n     */\n    function remove(address[] memory A, address a)\n        internal\n        pure\n        returns (address[] memory)\n    {\n        (uint256 index, bool isIn) = indexOf(A, a);\n        if (!isIn) {\n            revert(\"Address not in array.\");\n        } else {\n            (address[] memory _A,) = pop(A, index);\n            return _A;\n        }\n    }\n\n    /**\n     * @param A The input array to search\n     * @param a The address to remove\n     */\n    function removeStorage(address[] storage A, address a)\n        internal\n    {\n        (uint256 index, bool isIn) = indexOf(A, a);\n        if (!isIn) {\n            revert(\"Address not in array.\");\n        } else {\n            uint256 lastIndex = A.length - 1; // If the array would be empty, the previous line would throw, so no underflow here\n            if (index != lastIndex) { A[index] = A[lastIndex]; }\n            A.pop();\n        }\n    }\n\n    /**\n    * Removes specified index from array\n    * @param A The input array to search\n    * @param index The index to remove\n    * @return Returns the new array and the removed entry\n    */\n    function pop(address[] memory A, uint256 index)\n        internal\n        pure\n        returns (address[] memory, address)\n    {\n        uint256 length = A.length;\n        require(index < A.length, \"Index must be < A length\");\n        address[] memory newAddresses = new address[](length - 1);\n        for (uint256 i = 0; i < index; i++) {\n            newAddresses[i] = A[i];\n        }\n        for (uint256 j = index + 1; j < length; j++) {\n            newAddresses[j - 1] = A[j];\n        }\n        return (newAddresses, A[index]);\n    }\n}\n"
    },
    "contracts/external/Bytes32ArrayUtils.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.17;\n\n/**\n * @title Bytes32ArrayUtils\n * @author ZKP2P\n *\n * Fork of Set Protocol's AddressArrayUtils library adapted for usage with bytes32 arrays.\n */\nlibrary Bytes32ArrayUtils {\n\n    uint256 constant internal MAX_INT = 2**256 - 1;\n\n    /**\n     * Finds the index of the first occurrence of the given element.\n     * @param A The input array to search\n     * @param a The value to find\n     * @return Returns (index and isIn) for the first occurrence starting from index 0\n     */\n    function indexOf(bytes32[] memory A, bytes32 a) internal pure returns (uint256, bool) {\n        uint256 length = A.length;\n        for (uint256 i = 0; i < length; i++) {\n            if (A[i] == a) {\n                return (i, true);\n            }\n        }\n        return (MAX_INT, false);\n    }\n\n    /**\n    * Returns true if the value is present in the list. Uses indexOf internally.\n    * @param A The input array to search\n    * @param a The value to find\n    * @return Returns isIn for the first occurrence starting from index 0\n    */\n    function contains(bytes32[] memory A, bytes32 a) internal pure returns (bool) {\n        (, bool isIn) = indexOf(A, a);\n        return isIn;\n    }\n\n    /**\n    * Returns true if there are 2 elements that are the same in an array\n    * @param A The input array to search\n    * @return Returns boolean for the first occurrence of a duplicate\n    */\n    function hasDuplicate(bytes32[] memory A) internal pure returns(bool) {\n        require(A.length > 0, \"A is empty\");\n\n        for (uint256 i = 0; i < A.length - 1; i++) {\n            bytes32 current = A[i];\n            for (uint256 j = i + 1; j < A.length; j++) {\n                if (current == A[j]) {\n                    return true;\n                }\n            }\n        }\n        return false;\n    }\n\n    /**\n     * @param A The input array to search\n     * @param a The bytes32 to remove\n     * @return Returns the array with the object removed.\n     */\n    function remove(bytes32[] memory A, bytes32 a)\n        internal\n        pure\n        returns (bytes32[] memory)\n    {\n        (uint256 index, bool isIn) = indexOf(A, a);\n        if (!isIn) {\n            revert(\"bytes32 not in array.\");\n        } else {\n            (bytes32[] memory _A,) = pop(A, index);\n            return _A;\n        }\n    }\n\n    /**\n     * @param A The input array to search\n     * @param a The bytes32 to remove\n     */\n    function removeStorage(bytes32[] storage A, bytes32 a)\n        internal\n    {\n        (uint256 index, bool isIn) = indexOf(A, a);\n        if (!isIn) {\n            revert(\"bytes32 not in array.\");\n        } else {\n            uint256 lastIndex = A.length - 1; // If the array would be empty, the previous line would throw, so no underflow here\n            if (index != lastIndex) { A[index] = A[lastIndex]; }\n            A.pop();\n        }\n    }\n\n    /**\n    * Removes specified index from array\n    * @param A The input array to search\n    * @param index The index to remove\n    * @return Returns the new array and the removed entry\n    */\n    function pop(bytes32[] memory A, uint256 index)\n        internal\n        pure\n        returns (bytes32[] memory, bytes32)\n    {\n        uint256 length = A.length;\n        require(index < A.length, \"Index must be < A length\");\n        bytes32[] memory newBytes = new bytes32[](length - 1);\n        for (uint256 i = 0; i < index; i++) {\n            newBytes[i] = A[i];\n        }\n        for (uint256 j = index + 1; j < length; j++) {\n            newBytes[j - 1] = A[j];\n        }\n        return (newBytes, A[index]);\n    }\n}\n"
    },
    "contracts/external/StringArrayUtils.sol": {
      "content": "/*\n    Copyright 2020 Set Labs Inc.\n\n    Licensed under the Apache License, Version 2.0 (the \"License\");\n    you may not use this file except in compliance with the License.\n    You may obtain a copy of the License at\n\n    http://www.apache.org/licenses/LICENSE-2.0\n\n    Unless required by applicable law or agreed to in writing, software\n    distributed under the License is distributed on an \"AS IS\" BASIS,\n    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.\n    See the License for the specific language governing permissions and\n    limitations under the License.\n\n    SPDX-License-Identifier: Apache-2.0\n*/\n\npragma solidity ^0.8.18;\n\n/**\n * @title StringArrayUtils\n * @author Set Protocol\n *\n * Utility functions to handle String Arrays\n */\nlibrary StringArrayUtils {\n\n    /**\n     * Finds the index of the first occurrence of the given element.\n     * @param A The input string to search\n     * @param a The value to find\n     * @return Returns (index and isIn) for the first occurrence starting from index 0\n     */\n    function indexOf(string[] memory A, string memory a) internal pure returns (uint256, bool) {\n        uint256 length = A.length;\n        for (uint256 i = 0; i < length; i++) {\n            if (keccak256(bytes(A[i])) == keccak256(bytes(a))) {\n                return (i, true);\n            }\n        }\n        return (type(uint256).max, false);\n    }\n\n    /**\n     * @param A The input array to search\n     * @param a The string to remove\n     */\n    function removeStorage(string[] storage A, string memory a)\n        internal\n    {\n        (uint256 index, bool isIn) = indexOf(A, a);\n        if (!isIn) {\n            revert(\"String not in array.\");\n        } else {\n            uint256 lastIndex = A.length - 1; // If the array would be empty, the previous line would throw, so no underflow here\n            if (index != lastIndex) { A[index] = A[lastIndex]; }\n            A.pop();\n        }\n    }\n}"
    },
    "contracts/external/Uint256ArrayUtils.sol": {
      "content": "/*\n    Copyright 2020 Set Labs Inc.\n\n    Licensed under the Apache License, Version 2.0 (the \"License\");\n    you may not use this file except in compliance with the License.\n    You may obtain a copy of the License at\n\n    http://www.apache.org/licenses/LICENSE-2.0\n\n    Unless required by applicable law or agreed to in writing, software\n    distributed under the License is distributed on an \"AS IS\" BASIS,\n    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.\n    See the License for the specific language governing permissions and\n    limitations under the License.\n\n    SPDX-License-Identifier: Apache-2.0\n*/\n\npragma solidity ^0.8.18;\n\n/**\n * @title Uint256ArrayUtils\n * @author Set Protocol\n *\n * Utility functions to handle Uint256 Arrays\n */\nlibrary Uint256ArrayUtils {\n\n    uint256 constant internal MAX_INT = 2**256 - 1;\n\n    /**\n     * Finds the index of the first occurrence of the given element.\n     * @param A The input array to search\n     * @param a The value to find\n     * @return Returns (index and isIn) for the first occurrence starting from index 0\n     */\n    function indexOf(uint256[] memory A, uint256 a) internal pure returns (uint256, bool) {\n        uint256 length = A.length;\n        for (uint256 i = 0; i < length; i++) {\n            if (A[i] == a) {\n                return (i, true);\n            }\n        }\n        return (MAX_INT, false);\n    }\n\n    /**\n     * Returns the combination of the two arrays\n     * @param A The first array\n     * @param B The second array\n     * @return Returns A extended by B\n     */\n    function extend(uint256[] memory A, uint256[] memory B) internal pure returns (uint256[] memory) {\n        uint256 aLength = A.length;\n        uint256 bLength = B.length;\n        uint256[] memory newUints = new uint256[](aLength + bLength);\n        for (uint256 i = 0; i < aLength; i++) {\n            newUints[i] = A[i];\n        }\n        for (uint256 j = 0; j < bLength; j++) {\n            newUints[aLength + j] = B[j];\n        }\n        return newUints;\n    }\n\n    /**\n     * @param A The input array to search\n     * @param a The bytes32 to remove\n     */\n    function removeStorage(uint256[] storage A, uint256 a)\n        internal\n    {\n        (uint256 index, bool isIn) = indexOf(A, a);\n        if (!isIn) {\n            revert(\"uint256 not in array.\");\n        } else {\n            uint256 lastIndex = A.length - 1; // If the array would be empty, the previous line would throw, so no underflow here\n            if (index != lastIndex) { A[index] = A[lastIndex]; }\n            A.pop();\n        }\n    }\n}\n"
    },
    "contracts/interfaces/IAttestationVerifier.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.18;\n\n/**\n * @title IAttestationVerifier\n * @notice Interface for verifying attestations from various sources (witnesses, TEE, etc.)\n */\ninterface IAttestationVerifier {\n    /**\n     * @notice Verifies attestations for a given digest\n     * @param _digest The message digest to verify (EIP-712 formatted)\n     * @param _sigs Array of signatures from attestors\n     * @param _data Verification data containing attestor identities or hints\n     * @return isValid Returns true if the attestation is valid, false otherwise\n     */\n    function verify(\n        bytes32 _digest,\n        bytes[] calldata _sigs,\n        bytes calldata _data\n    ) external view returns (bool isValid);\n}"
    },
    "contracts/interfaces/IEscrow.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\nimport { IERC20 } from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport { IPostIntentHook } from \"./IPostIntentHook.sol\";\n\ninterface IEscrow {\n    \n    /* ============ Structs ============ */\n\n    struct Intent {\n        bytes32 intentHash;                        // Unique identifier for the intent\n        uint256 amount;                            // Amount locked\n        uint256 timestamp;                         // When this intent was created\n        uint256 expiryTime;                        // When this intent expires\n    }\n\n    struct Range {\n        uint256 min;                                // Minimum value\n        uint256 max;                                // Maximum value\n    }\n\n    struct Deposit {\n        address depositor;                          // Address of depositor\n        address delegate;                           // Address that can manage this deposit (address(0) if no delegate)\n        IERC20 token;                               // Address of deposit token\n        Range intentAmountRange;                    // Range of take amount per intent\n        // Deposit state\n        bool acceptingIntents;                      // State: True if the deposit is accepting intents, False otherwise\n        uint256 remainingDeposits;                  // State: Amount of liquidity immediately available to lock\n        uint256 outstandingIntentAmount;            // State: Amount of outstanding intents (may include expired intents)\n        // Intent guardian\n        address intentGuardian;                     // Address that can extend intent expiry times (address(0) if no guardian)\n        // Retention behavior\n        bool retainOnEmpty;                         // If true, do not auto-close/sweep when empty; keep config for reuse\n    }\n\n    struct Currency {\n        bytes32 code;                               // Currency code (keccak256 hash of the currency code)\n        uint256 minConversionRate;                  // Minimum rate of deposit token to fiat currency (in preciseUnits)\n    }\n\n    struct DepositPaymentMethodData {\n        address intentGatingService;                // Public key of gating service that will be used to verify intents\n        bytes32 payeeDetails;                       // Payee details, has to be hash of payee details\n        bytes data;                                 // Verification Data: Additional data used for payment verification; Can hold attester address\n                                                    // in case of TLS proofs, domain key hash in case of zkEmail proofs, currency code etc.\n    }\n\n    struct CreateDepositParams {\n        IERC20 token;                                // The token to be deposited\n        uint256 amount;                              // The amount of token to deposit\n        Range intentAmountRange;                     // The max and min take amount for each intent\n        bytes32[] paymentMethods;                    // The payment methods that deposit supports\n        DepositPaymentMethodData[] paymentMethodData;// The payment verification data for each payment method that deposit supports\n        Currency[][] currencies;                     // The currencies for each payment method that deposit supports\n        address delegate;                            // Optional delegate address that can manage this deposit (address(0) for no delegate)\n        address intentGuardian;                      // Optional intent guardian address that can extend intent expiry times (address(0) for no guardian)\n        bool retainOnEmpty;                          // Opt-in: keep deposit and config when empty\n    }\n\n    /* ============ Events ============ */\n\n    event DepositReceived(uint256 indexed depositId, address indexed depositor, IERC20 indexed token, uint256 amount, Range intentAmountRange, address delegate, address intentGuardian);\n\n    event DepositPaymentMethodAdded(uint256 indexed depositId, bytes32 indexed paymentMethod, bytes32 indexed payeeDetails, address intentGatingService);\n    event DepositPaymentMethodActiveUpdated(uint256 indexed depositId, bytes32 indexed paymentMethod, bool active);\n\n    event DepositCurrencyAdded(uint256 indexed depositId, bytes32 indexed paymentMethod, bytes32 indexed currency, uint256 minConversionRate);\n    event DepositMinConversionRateUpdated(uint256 indexed depositId, bytes32 indexed paymentMethod, bytes32 indexed currency, uint256 newMinConversionRate);\n    \n    event DepositFundsAdded(uint256 indexed depositId, address indexed depositor, uint256 amount);\n    event DepositWithdrawn(uint256 indexed depositId, address indexed depositor, uint256 amount);\n    event DepositClosed(uint256 depositId, address depositor);\n    event DepositAcceptingIntentsUpdated(uint256 indexed depositId, bool acceptingIntents);\n\n    event DepositIntentAmountRangeUpdated(uint256 indexed depositId, Range intentAmountRange);\n    event DepositRetainOnEmptyUpdated(uint256 indexed depositId, bool retainOnEmpty);\n\n    event DepositDelegateSet(uint256 indexed depositId, address indexed depositor, address indexed delegate);\n    event DepositDelegateRemoved(uint256 indexed depositId, address indexed depositor);\n\n    event MinDepositAmountSet(uint256 minDepositAmount);\n\n    event OrchestratorUpdated(address indexed orchestrator);\n    event PaymentVerifierRegistryUpdated(address indexed paymentVerifierRegistry);\n\n    event FundsLocked(uint256 indexed depositId, bytes32 indexed intentHash, uint256 amount, uint256 expiryTime);\n    event FundsUnlocked(uint256 indexed depositId, bytes32 indexed intentHash, uint256 amount);\n    event FundsUnlockedAndTransferred(\n        uint256 indexed depositId, \n        bytes32 indexed intentHash, \n        uint256 unlockedAmount, \n        uint256 transferredAmount, \n        address to\n    );\n    event IntentExpiryExtended(uint256 indexed depositId, bytes32 indexed intentHash, uint256 newExpiryTime);\n\n    event DustRecipientUpdated(address indexed dustRecipient);\n    event DustCollected(uint256 indexed depositId, uint256 dustAmount, address indexed dustRecipient);\n    event DustThresholdUpdated(uint256 dustThreshold);\n    event MaxIntentsPerDepositUpdated(uint256 maxIntentsPerDeposit);\n    event IntentExpirationPeriodUpdated(uint256 intentExpirationPeriod);\n\n    /* ============ Standardized Custom Errors ============ */\n    \n    // Zero value errors\n    error ZeroAddress();\n    error ZeroValue();\n    error ZeroMinValue();\n    error ZeroConversionRate();\n\n    // Authorization errors\n    error UnauthorizedCaller(address caller, address authorized);\n    error UnauthorizedCallerOrDelegate(address caller, address owner, address delegate);\n\n    // Range and amount errors\n    error InvalidRange(uint256 min, uint256 max);\n    error AmountBelowMin(uint256 amount, uint256 min);\n    error AmountAboveMax(uint256 amount, uint256 max);\n    error AmountExceedsAvailable(uint256 requested, uint256 available);\n\n    // Not found errors\n    error DepositNotFound(uint256 depositId);\n    error IntentNotFound(bytes32 intentHash);\n    error PaymentMethodNotActive(uint256 depositId, bytes32 paymentMethod);\n    error PaymentMethodNotListed(uint256 depositId, bytes32 paymentMethod);\n    error CurrencyNotFound(bytes32 paymentMethod, bytes32 currency);\n    error DelegateNotFound(uint256 depositId);\n\n    // Already exists errors\n    error PaymentMethodAlreadyExists(uint256 depositId, bytes32 paymentMethod);\n    error CurrencyAlreadyExists(bytes32 paymentMethod, bytes32 currency);\n    error IntentAlreadyExists(uint256 depositId, bytes32 intentHash);\n\n    // State errors\n    error DepositNotAcceptingIntents(uint256 depositId);\n    error DepositAlreadyInState(uint256 depositId, bool currentState);\n    error InsufficientDepositLiquidity(uint256 depositId, uint256 available, uint256 required);\n    error MaxIntentsExceeded(uint256 depositId, uint256 current, uint256 max);\n\n    // Validation errors\n    error EmptyPayeeDetails();\n    error ArrayLengthMismatch(uint256 length1, uint256 length2);\n\n    // Payment method errors\n    error PaymentMethodNotWhitelisted(bytes32 paymentMethod);\n    error CurrencyNotSupported(bytes32 paymentMethod, bytes32 currency);\n\n    \n    /* ============ External Functions for Orchestrator ============ */\n\n    function lockFunds(uint256 _depositId, bytes32 _intentHash, uint256 _amount) external;\n    function unlockFunds(uint256 _depositId, bytes32 _intentHash) external;\n    function unlockAndTransferFunds(uint256 _depositId, bytes32 _intentHash, uint256 _transferAmount, address _to) external;\n    function extendIntentExpiry(uint256 _depositId, bytes32 _intentHash, uint256 _newExpiryTime) external;\n\n    /* ============ View Functions ============ */\n\n    function getDeposit(uint256 _depositId) external view returns (Deposit memory);\n    function getDepositIntent(uint256 _depositId, bytes32 _intentHash) external view returns (Intent memory);\n    function getDepositPaymentMethods(uint256 _depositId) external view returns (bytes32[] memory);\n    function getDepositCurrencies(uint256 _depositId, bytes32 _paymentMethod) external view returns (bytes32[] memory);\n    function getDepositCurrencyMinRate(uint256 _depositId, bytes32 _paymentMethod, bytes32 _currencyCode) external view returns (uint256);\n    function getDepositPaymentMethodData(uint256 _depositId, bytes32 _paymentMethod) external view returns (DepositPaymentMethodData memory);\n    function getDepositPaymentMethodActive(uint256 _depositId, bytes32 _paymentMethod) external view returns (bool);\n    function getDepositGatingService(uint256 _depositId, bytes32 _paymentMethod) external view returns (address);\n    function getAccountDeposits(address _account) external view returns (uint256[] memory);\n    function getDepositIntentHashes(uint256 _depositId) external view returns (bytes32[] memory);\n    function getExpiredIntents(uint256 _depositId) external view returns (bytes32[] memory expiredIntents, uint256 reclaimableAmount);\n}\n"
    },
    "contracts/interfaces/IEscrowRegistry.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\ninterface IEscrowRegistry {\n    function isWhitelistedEscrow(address _escrow) external view returns (bool);\n    function isAcceptingAllEscrows() external view returns (bool);\n    function getWhitelistedEscrows() external view returns (address[] memory);\n}"
    },
    "contracts/interfaces/INullifierRegistry.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\ninterface INullifierRegistry {\n    function addNullifier(bytes32 _nullifier) external;\n    function isNullified(bytes32 _nullifier) external view returns(bool);\n}\n"
    },
    "contracts/interfaces/IOrchestrator.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\nimport { IERC20 } from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport { IPostIntentHook } from \"./IPostIntentHook.sol\";\n\ninterface IOrchestrator {\n    \n    /* ============ Structs ============ */\n\n    struct Intent {\n        address owner;                              // Address of the intent owner  \n        address to;                                 // Address to forward funds to (can be same as owner)\n        address escrow;                             // Address of the escrow contract holding the deposit\n        uint256 depositId;                          // ID of the deposit the intent is associated with\n        uint256 amount;                             // Amount of the deposit.token the owner wants to take\n        uint256 timestamp;                          // Timestamp of the intent\n        bytes32 paymentMethod;                      // The payment method to be used for the offchain payment\n        bytes32 fiatCurrency;                       // Currency code that the owner is paying in offchain (keccak256 hash of the currency code)\n        uint256 conversionRate;                     // Conversion rate of deposit token to fiat currency at the time of intent\n        address referrer;                           // Address of the referrer who brought this intent (if any)\n        uint256 referrerFee;                        // Fee to be paid to the referrer in preciseUnits (1e16 = 1%)\n        IPostIntentHook postIntentHook;             // Address of the post-intent hook that will execute any post-intent actions\n        bytes data;                                 // Additional data to be passed to the post-intent hook contract\n    }\n\n    struct SignalIntentParams {\n        address escrow;                             // The escrow contract where the deposit is held\n        uint256 depositId;                          // The ID of the deposit the taker intends to use\n        uint256 amount;                             // The amount of deposit.token the user wants to take\n        address to;                                 // Address to forward funds to\n        bytes32 paymentMethod;                      // The payment method to be used for the offchain payment\n        bytes32 fiatCurrency;                       // The currency code for offchain payment\n        uint256 conversionRate;                     // The conversion rate agreed offchain\n        address referrer;                           // Address of the referrer (address(0) if no referrer)\n        uint256 referrerFee;                        // Fee to be paid to the referrer\n        bytes gatingServiceSignature;               // Signature from the deposit's gating service\n        uint256 signatureExpiration;                // Timestamp when the gating service signature expires\n        IPostIntentHook postIntentHook;             // Optional post-intent hook (address(0) for no hook)\n        bytes data;                                 // Additional data for the intent\n    }\n\n    struct FulfillIntentParams {\n        bytes paymentProof;                         // Payment proof. Can be Groth16 Proof, TLSNotary proof, TLSProxy proof, attestation etc.\n        bytes32 intentHash;                         // Identifier of intent being fulfilled\n        bytes verificationData;                     // Additional data for payment verifier\n        bytes postIntentHookData;                   // Additional data for post intent hook\n    }\n\n    /* ============ Events ============ */\n\n    event IntentSignaled(\n        bytes32 indexed intentHash, \n        address indexed escrow,\n        uint256 indexed depositId, \n        bytes32 paymentMethod, \n        address owner, \n        address to, \n        uint256 amount, \n        bytes32 fiatCurrency, \n        uint256 conversionRate, \n        uint256 timestamp\n    );\n\n    event IntentPruned(\n        bytes32 indexed intentHash\n    );\n\n    event IntentFulfilled(\n        bytes32 indexed intentHash,\n        address indexed fundsTransferredTo,   // Address that funds were transferred to; can be intent.to or postIntentHook address\n        uint256 amount,\n        bool isManualRelease\n    );\n\n    event AllowMultipleIntentsUpdated(bool allowMultiple);\n\n    event PaymentVerifierRegistryUpdated(address indexed paymentVerifierRegistry);\n    event PostIntentHookRegistryUpdated(address indexed postIntentHookRegistry);\n    event RelayerRegistryUpdated(address indexed relayerRegistry);\n    event EscrowRegistryUpdated(address indexed escrowRegistry);\n\n    event ProtocolFeeUpdated(uint256 protocolFee);\n    event ProtocolFeeRecipientUpdated(address indexed protocolFeeRecipient);\n    event PartialManualReleaseDelayUpdated(uint256 partialManualReleaseDelay);\n\n    /* ============ Standardized Custom Errors ============ */\n    \n    // Zero value errors\n    error ZeroAddress();\n    error ZeroValue();\n    \n    // Authorization errors\n    error UnauthorizedEscrowCaller(address caller);\n    error UnauthorizedCaller(address caller, address authorized);\n    \n    // Not found errors\n    error IntentNotFound(bytes32 intentHash);\n    error PaymentMethodDoesNotExist(bytes32 paymentMethod);\n    error PaymentMethodNotSupported(bytes32 paymentMethod);\n    error CurrencyNotSupported(bytes32 paymentMethod, bytes32 currency);\n    \n    // Whitelist errors\n    error PaymentMethodNotWhitelisted(bytes32 paymentMethod);\n    error PostIntentHookNotWhitelisted(address hook);\n    error EscrowNotWhitelisted(address escrow);\n    \n    // Amount and fee errors\n    error AmountBelowMin(uint256 amount, uint256 min);\n    error AmountAboveMax(uint256 amount, uint256 max);\n    error AmountExceedsLimit(uint256 amount, uint256 limit);\n    error FeeExceedsMaximum(uint256 fee, uint256 maximum);\n    error RateBelowMinimum(uint256 rate, uint256 minRate);\n    \n    // Validation errors\n    error AccountHasActiveIntent(address account, bytes32 existingIntent);\n    error InvalidReferrerFeeConfiguration();\n    error InvalidSignature();\n    error SignatureExpired(uint256 expiration, uint256 currentTime);\n    error PartialReleaseNotAllowedYet(uint256 currentTime, uint256 allowedTime);\n\n    // Verification errors\n    error PaymentVerificationFailed();\n    error HashMismatch(bytes32 expected, bytes32 actual);\n     \n    // Transfer errors\n    error TransferFailed(address recipient, uint256 amount);\n    error EscrowLockFailed();\n\n    /* ============ View Functions ============ */\n\n    function getIntent(bytes32 intentHash) external view returns (Intent memory);\n    function getAccountIntents(address account) external view returns (bytes32[] memory);\n    \n    /* ============ External Functions for Users ============ */\n\n    function signalIntent(SignalIntentParams calldata params) external;\n\n    function cancelIntent(bytes32 intentHash) external;\n\n    function fulfillIntent(FulfillIntentParams calldata params) external;\n\n    function releaseFundsToPayer(bytes32 intentHash) external;\n\n    /* ============ External Functions for Escrow ============ */\n\n    function pruneIntents(bytes32[] calldata intentIds) external;\n}"
    },
    "contracts/interfaces/IPaymentVerifier.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\n\ninterface IPaymentVerifier {\n\n    /* ============ Structs ============ */\n\n    struct VerifyPaymentData {\n        bytes32 intentHash;                     // The hash of the intent being fulfilled\n        bytes paymentProof;                     // Payment proof\n        bytes data;                             // Additional data provided by the taker\n    }\n\n    struct PaymentVerificationResult {\n        bool success;                           // Whether the payment verification succeeded\n        bytes32 intentHash;                     // The hash of the intent being fulfilled\n        uint256 releaseAmount;                  // The amount of tokens to release\n    }\n\n    /* ============ External Functions ============ */\n\n    function verifyPayment(\n        VerifyPaymentData calldata _verifyPaymentData\n    )   \n        external\n        returns(PaymentVerificationResult memory result);\n\n}\n"
    },
    "contracts/interfaces/IPaymentVerifierRegistry.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\ninterface IPaymentVerifierRegistry {\n    function isPaymentMethod(bytes32 _paymentMethod) external view returns (bool);\n    function getPaymentMethods() external view returns (bytes32[] memory);\n    function getVerifier(bytes32 _paymentMethod) external view returns (address);\n    function isCurrency(bytes32 _paymentMethod, bytes32 _currencyCode) external view returns (bool);\n    function getCurrencies(bytes32 _paymentMethod) external view returns (bytes32[] memory);\n}\n"
    },
    "contracts/interfaces/IPostIntentHook.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\nimport { IOrchestrator } from \"./IOrchestrator.sol\";\n\n/**\n * @title IPostIntentHook\n * @notice Interface for post-intent hooks\n */\ninterface IPostIntentHook {\n\n    /**\n     * @notice Post-intent hook\n     * @param _intent The intent data structure containing all intent information\n     * @param _fulfillIntentData The data passed to fulfillIntent\n     */\n    function execute(\n        IOrchestrator.Intent memory _intent,\n        uint256 _amountNetFees,\n        bytes calldata _fulfillIntentData\n    ) external;\n}\n"
    },
    "contracts/interfaces/IPostIntentHookRegistry.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\ninterface IPostIntentHookRegistry {\n    function isWhitelistedHook(address _hook) external view returns (bool);\n    function getWhitelistedHooks() external view returns (address[] memory);\n}\n"
    },
    "contracts/interfaces/IProtocolViewer.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\nimport { IEscrow } from \"./IEscrow.sol\";\nimport { IOrchestrator } from \"./IOrchestrator.sol\";\n\ninterface IProtocolViewer {\n\n    /* ============ Structs ============ */\n\n    struct PaymentMethodDataView {\n        bytes32 paymentMethod;\n        IEscrow.DepositPaymentMethodData verificationData;\n        IEscrow.Currency[] currencies;\n    }\n\n    struct DepositView {\n        uint256 depositId;\n        IEscrow.Deposit deposit;\n        uint256 availableLiquidity;                 // Amount of liquidity available to signal intents (net of expired intents)\n        PaymentMethodDataView[] paymentMethods;\n        bytes32[] intentHashes;\n    }\n\n    struct IntentView {\n        bytes32 intentHash;\n        IOrchestrator.Intent intent;\n        DepositView deposit;\n    }\n\n    /* ============ Functions ============ */\n\n    function getDepositFromIds(\n        uint256[] memory _depositIds\n    ) external view returns (DepositView[] memory);\n\n    function getAccountIntents(\n        address _account\n    ) external view returns (IntentView[] memory intentViews);\n}\n"
    },
    "contracts/interfaces/IRelayerRegistry.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\ninterface IRelayerRegistry {\n    function isWhitelistedRelayer(address _relayer) external view returns (bool);\n    function getWhitelistedRelayers() external view returns (address[] memory);\n}\n"
    },
    "contracts/lib/ThresholdSigVerifierUtils.sol": {
      "content": "  \n//SPDX-License-Identifier: MIT\n\nimport { SignatureChecker } from \"@openzeppelin/contracts/utils/cryptography/SignatureChecker.sol\";\nimport { ECDSA } from \"@openzeppelin/contracts/utils/cryptography/ECDSA.sol\";\n\npragma solidity ^0.8.18;\n\n\n// This library is used to verify that signatures meet the required threshold from accepted witnesses\nlibrary ThresholdSigVerifierUtils {\n\n    using SignatureChecker for address;\n    using ECDSA for bytes32;\n\n    /**\n     * Verifies that signatures meet the required threshold from accepted witnesses\n     * \n     * @param _digest The message digest to verify (EIP-712 or pre-hashed for EIP-191)\n     * @param _signatures Array of signatures (must have at least minWitnessSignatures)\n     * @param _witnesses Array of accepted witness addresses\n     * @param _reqThreshold The minimum number of witness signatures required\n     * @return success Returns true if the threshold is met, otherwise reverts\n     * @dev The digest should already be properly formatted:\n     *      - For EIP-712: keccak256(abi.encodePacked(\"\\x19\\x01\", domainSeparator, structHash))\n     *      - For EIP-191: the message hash that will be signed with personal_sign\n     *      SignatureChecker.isValidSignatureNow will handle both cases correctly\n     */\n    function verifyWitnessSignatures(\n        bytes32 _digest,\n        bytes[] memory _signatures,\n        address[] memory _witnesses,\n        uint256 _reqThreshold\n    )\n        internal\n        view\n        returns (bool success)\n    {\n        success = false;\n        require(_reqThreshold > 0, \"ThresholdSigVerifierUtils: req threshold must be > 0\");\n        require(_reqThreshold <= _signatures.length, \"ThresholdSigVerifierUtils: req threshold exceeds signatures\");\n        require(_reqThreshold <= _witnesses.length, \"ThresholdSigVerifierUtils: req threshold exceeds witnesses\");\n        \n        // For EIP-712, the digest is already properly formatted\n        // For EIP-191, we need to convert to Ethereum signed message hash\n        // SignatureChecker handles both EIP-712 and EIP-191 signatures\n        // We'll pass the digest directly and let SignatureChecker handle it\n        \n        // Track unique signers using an array\n        address[] memory seenSigners = new address[](_witnesses.length);\n        uint256 validWitnessSignatures = 0;\n        \n        // Check each signature to find which witness signed it\n        for (uint256 i = 0; i < _signatures.length; i++) {\n            // Check if any witness created this signature\n            for (uint256 j = 0; j < _witnesses.length; j++) {\n                if (_witnesses[j].isValidSignatureNow(_digest, _signatures[i])) {\n                    // Check if we've already counted this witness\n                    bool alreadySeen = false;\n                    for (uint256 k = 0; k < validWitnessSignatures; k++) {\n                        if (seenSigners[k] == _witnesses[j]) {\n                            alreadySeen = true;\n                            break;\n                        }\n                    }\n                    if (!alreadySeen) {\n                        seenSigners[validWitnessSignatures] = _witnesses[j];\n                        validWitnessSignatures++;\n                        \n                        // Early exit if threshold is met\n                        if (validWitnessSignatures >= _reqThreshold) {\n                            success = true;\n                        }\n                        break;\n                    }\n                }\n            }\n        }\n        \n        // Check threshold\n        require(\n            validWitnessSignatures >= _reqThreshold,\n            \"ThresholdSigVerifierUtils: Not enough valid witness signatures\"\n        );\n    }\n}"
    },
    "contracts/mocks/ERC20Mock.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\nimport { ERC20 } from \"@openzeppelin/contracts/token/ERC20/ERC20.sol\";\n\npragma solidity ^0.8.18;\n\ncontract ERC20Mock is ERC20 {\n\n    constructor(\n        uint256 _mintAmount,\n        string memory name,\n        string memory symbol\n    )\n        ERC20(name, symbol)\n    {\n        _mint(msg.sender, _mintAmount);\n    }\n\n    function decimals() public pure override returns (uint8) {\n        return 18;\n    }\n}\n"
    },
    "contracts/mocks/FailingAttestationVerifier.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.18;\n\nimport { IAttestationVerifier } from \"../interfaces/IAttestationVerifier.sol\";\n\n/// @notice Test helper that always returns false without reverting.\ncontract FailingAttestationVerifier is IAttestationVerifier {\n    function verify(\n        bytes32,\n        bytes[] calldata,\n        bytes calldata\n    ) external pure override returns (bool) {\n        return false;\n    }\n}\n"
    },
    "contracts/mocks/OrchestratorMock.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\nimport { IEscrow } from \"../interfaces/IEscrow.sol\";\n/**\n * @title OrchestratorMock\n * @notice Mock orchestrator contract for testing escrow orchestrator-only functions\n */\ncontract OrchestratorMock {\n    \n    IEscrow public escrow;\n    bytes32[] public lastPrunedIntents;\n    \n    // Events for testing\n    event IntentsPruned(bytes32 intent);\n    \n    constructor(address _escrow) {\n        escrow = IEscrow(_escrow);\n        lastPrunedIntents = new bytes32[](1);\n    }\n    \n    /**\n     * @notice Implementation of pruneIntents required by IOrchestrator\n     * @param _intents Array of intent hashes to prune\n     */\n    function pruneIntents(bytes32[] calldata _intents) external {\n        lastPrunedIntents[0] = _intents[0];\n        emit IntentsPruned(_intents[0]);\n    }\n    \n    // Test helper functions to call orchestrator-only functions on escrow\n    \n    function lockFunds(\n        uint256 _depositId,\n        bytes32 _intentHash,\n        uint256 _amount\n    ) external {\n        escrow.lockFunds(_depositId, _intentHash, _amount);\n    }\n    \n    function unlockFunds(uint256 _depositId, bytes32 _intentHash) external {\n        escrow.unlockFunds(_depositId, _intentHash);\n    }\n    \n    function unlockAndTransferFunds(\n        uint256 _depositId,\n        bytes32 _intentHash,\n        uint256 _transferAmount,\n        address _to\n    ) external {\n        escrow.unlockAndTransferFunds(_depositId, _intentHash, _transferAmount, _to);\n    }\n    \n    // Getter for testing\n    function getLastPrunedIntents() external view returns (bytes32[] memory) {\n        return lastPrunedIntents;\n    }\n}"
    },
    "contracts/mocks/PartialPullPostIntentHookMock.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\nimport { IERC20 } from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport { IPostIntentHook } from \"../interfaces/IPostIntentHook.sol\";\nimport { IOrchestrator } from \"../interfaces/IOrchestrator.sol\";\n\n/**\n * @title PartialPullPostIntentHookMock\n * @notice Mock hook that deliberately pulls only a portion of the approved amount to simulate buggy hooks.\n */\ncontract PartialPullPostIntentHookMock is IPostIntentHook {\n    IERC20 public immutable token;\n    address public immutable orchestrator;\n\n    constructor(address _token, address _orchestrator) {\n        token = IERC20(_token);\n        orchestrator = _orchestrator;\n    }\n\n    function execute(\n        IOrchestrator.Intent memory _intent,\n        uint256 _amountNetFees,\n        bytes calldata /* _fulfillIntentData */\n    ) external override {\n        // Decode target address from intent.data (same format as PostIntentHookMock)\n        address targetAddress = abi.decode(_intent.data, (address));\n        require(targetAddress != address(0), \"target=0\");\n\n        // Pull only half of the approved amount to simulate a faulty hook\n        uint256 half = _amountNetFees / 2;\n        if (half > 0) {\n            token.transferFrom(orchestrator, targetAddress, half);\n        }\n        // Intentionally do NOT pull the remainder\n    }\n}\n\n"
    },
    "contracts/mocks/PaymentVerifierMock.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.18;\n\nimport { IPaymentVerifier } from \"../interfaces/IPaymentVerifier.sol\";\nimport { IOrchestrator } from \"../interfaces/IOrchestrator.sol\";\nimport { IEscrow } from \"../interfaces/IEscrow.sol\";\n\ncontract PaymentVerifierMock is IPaymentVerifier {\n\n    struct PaymentDetails {\n        uint256 amount;\n        uint256 timestamp;\n        bytes32 payeeDetails;\n        bytes32 fiatCurrency;\n        bytes32 intentHash;\n    }\n\n    struct Snapshot {\n        uint256 amount;\n        uint256 conversionRate;\n        uint256 signalTimestamp;\n        bytes32 payeeDetails;\n        bool found;\n    }\n\n    uint256 internal constant PRECISE_UNIT = 1e18;\n\n    bool public shouldVerifyPayment;\n    bool public shouldReturnFalse;\n    address public orchestratorAddress;\n    address public escrowAddress;\n\n    function setShouldVerifyPayment(bool _shouldVerifyPayment) external {\n        shouldVerifyPayment = _shouldVerifyPayment;\n    }\n\n    function setShouldReturnFalse(bool _shouldReturnFalse) external {\n        shouldReturnFalse = _shouldReturnFalse;\n    }\n\n    function setVerificationContext(address _orchestrator, address _escrow) external {\n        orchestratorAddress = _orchestrator;\n        escrowAddress = _escrow;\n    }\n\n    function extractIntentHash(bytes calldata _proof) external pure returns (bytes32) {\n        (, , , , bytes32 intentHash) = abi.decode(_proof, (uint256, uint256, bytes32, bytes32, bytes32));\n        return intentHash;\n    }\n\n    function verifyPayment(\n        IPaymentVerifier.VerifyPaymentData calldata _verifyPaymentData\n    ) external view override returns (PaymentVerificationResult memory) {\n        PaymentDetails memory paymentDetails = _extractPaymentDetails(_verifyPaymentData.paymentProof);\n\n        Snapshot memory snapshot = _fetchSnapshot(paymentDetails.intentHash);\n\n        if (shouldVerifyPayment) {\n            require(snapshot.found, \"UPV: Unknown intent\");\n            require(snapshot.conversionRate != 0, \"UPV: Snapshot rate mismatch\");\n            require(\n                snapshot.payeeDetails == bytes32(0) || paymentDetails.payeeDetails == snapshot.payeeDetails,\n                \"UPV: Snapshot payee mismatch\"\n            );\n            if (snapshot.signalTimestamp != 0) {\n                require(\n                    paymentDetails.timestamp >= snapshot.signalTimestamp,\n                    \"UPV: Snapshot timestamp mismatch\"\n                );\n            }\n        }\n\n        if (shouldReturnFalse) {\n            return PaymentVerificationResult({ success: false, intentHash: bytes32(0), releaseAmount: 0 });\n        }\n\n        uint256 releaseAmount = paymentDetails.amount;\n        if (snapshot.conversionRate > 0) {\n            releaseAmount = (paymentDetails.amount * PRECISE_UNIT) / snapshot.conversionRate;\n        }\n\n        if (snapshot.amount > 0 && releaseAmount > snapshot.amount) {\n            releaseAmount = snapshot.amount;\n        }\n\n        return PaymentVerificationResult({\n            success: true,\n            intentHash: paymentDetails.intentHash,\n            releaseAmount: releaseAmount\n        });\n    }\n\n    function _fetchSnapshot(bytes32 intentHash) internal view returns (Snapshot memory snapshot) {\n        if (orchestratorAddress == address(0) || escrowAddress == address(0)) {\n            return snapshot;\n        }\n\n        IOrchestrator.Intent memory intentData = IOrchestrator(orchestratorAddress).getIntent(intentHash);\n        if (intentData.owner == address(0)) {\n            return snapshot;\n        }\n\n        IEscrow.DepositPaymentMethodData memory methodData = IEscrow(escrowAddress)\n            .getDepositPaymentMethodData(intentData.depositId, intentData.paymentMethod);\n\n        snapshot = Snapshot({\n            amount: intentData.amount,\n            conversionRate: intentData.conversionRate,\n            signalTimestamp: intentData.timestamp,\n            payeeDetails: methodData.payeeDetails,\n            found: true\n        });\n    }\n\n    function _extractPaymentDetails(bytes calldata _proof) internal pure returns (PaymentDetails memory) {\n        (uint256 amount, uint256 timestamp, bytes32 payeeDetails, bytes32 fiatCurrency, bytes32 intentHash) =\n            abi.decode(_proof, (uint256, uint256, bytes32, bytes32, bytes32));\n\n        return PaymentDetails(amount, timestamp, payeeDetails, fiatCurrency, intentHash);\n    }\n}\n"
    },
    "contracts/mocks/PostIntentHookMock.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\nimport { IERC20 } from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport { IPostIntentHook } from \"../interfaces/IPostIntentHook.sol\";\nimport { IOrchestrator } from \"../interfaces/IOrchestrator.sol\";\n\n/**\n * @title PostIntentHookMock\n * @notice Mock implementation of IPostIntentHook that transfers funds to a target address\n */\ncontract PostIntentHookMock is IPostIntentHook {\n    \n    /* ============ State Variables ============ */\n\n    IERC20 public immutable usdc;\n    address public immutable orchestrator;\n\n    /* ============ Constructor ============ */\n\n    constructor(address _usdc, address _orchestrator) {\n        usdc = IERC20(_usdc);\n        orchestrator = _orchestrator;\n    }\n\n    /**\n     * @notice Executes post-intent action by transferring funds to target address\n     * @param _intent The intent data containing the target address in the data field\n     * @param _amountNetFees Amount of funds to transfer to the target address after fees are deducted\n     */\n    function execute(\n        IOrchestrator.Intent memory _intent,\n        uint256 _amountNetFees,\n        bytes calldata /*_fulfillIntentData*/\n    ) external override {\n        // Decode target address and token from intent data\n        address targetAddress = abi.decode(_intent.data, (address));\n\n        // Check if target address and token are not zero (use this to test failure of post-intent hook)\n        require(targetAddress != address(0), \"Target address cannot be zero\");\n        \n        // Pull usdc from escrow and transfer to target address\n        usdc.transferFrom(orchestrator, targetAddress, _amountNetFees);\n    }\n} "
    },
    "contracts/mocks/PushPostIntentHookMock.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\nimport { IERC20 } from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport { IPostIntentHook } from \"../interfaces/IPostIntentHook.sol\";\nimport { IOrchestrator } from \"../interfaces/IOrchestrator.sol\";\n\n/**\n * @title PushPostIntentHookMock\n * @notice Mock hook that pushes tokens into the Orchestrator during execution to trigger the\n *         balance-increase invariant in the Orchestrator.\n */\ncontract PushPostIntentHookMock is IPostIntentHook {\n    IERC20 public immutable token;\n    address public immutable orchestrator;\n\n    constructor(address _token, address _orchestrator) {\n        token = IERC20(_token);\n        orchestrator = _orchestrator;\n    }\n\n    function execute(\n        IOrchestrator.Intent memory /* _intent */,\n        uint256 /* _amountNetFees */,\n        bytes calldata /* _fulfillIntentData */\n    ) external override {\n        // Push a tiny amount to the orchestrator to simulate an unexpected balance increase\n        token.transfer(orchestrator, 1);\n    }\n}\n\n"
    },
    "contracts/mocks/ReentrantPostIntentHook.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\nimport { IERC20 } from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport { IPostIntentHook } from \"../interfaces/IPostIntentHook.sol\";\nimport { IOrchestrator } from \"../interfaces/IOrchestrator.sol\";\n\n/**\n * @title ReentrantPostIntentHook\n * @notice Malicious hook contract that attempts reentrancy attack on fulfillIntent\n * @dev Used for testing reentrancy protection in Orchestrator contract\n */\ncontract ReentrantPostIntentHook is IPostIntentHook {\n    \n    /* ============ State Variables ============ */\n\n    IOrchestrator public immutable orchestrator;\n    IERC20 public immutable usdc;\n    \n    // Attack configuration\n    bool public attemptReentry = true;\n    uint256 public reentrancyAttempts = 0;\n    \n    // Stored params for reentrancy attempt\n    IOrchestrator.FulfillIntentParams public storedFulfillParams;\n    \n    /* ============ Events ============ */\n    \n    event ReentrancyAttempted(bool success);\n    event ExecutionCompleted(address recipient, uint256 amount);\n\n    /* ============ Constructor ============ */\n\n    constructor(address _usdc, address _orchestrator) {\n        usdc = IERC20(_usdc);\n        orchestrator = IOrchestrator(_orchestrator);\n    }\n\n    /* ============ External Functions ============ */\n\n    /**\n     * @notice Stores fulfill params for reentrancy attack attempt\n     */\n    function setFulfillParams(\n        bytes calldata paymentProof,\n        bytes32 intentHash,\n        bytes calldata verificationData,\n        bytes calldata postIntentHookData\n    ) external {\n        storedFulfillParams = IOrchestrator.FulfillIntentParams({\n            paymentProof: paymentProof,\n            intentHash: intentHash,\n            verificationData: verificationData,\n            postIntentHookData: postIntentHookData\n        });\n    }\n\n    /**\n     * @notice Toggles whether to attempt reentrancy\n     */\n    function setAttemptReentry(bool _attempt) external {\n        attemptReentry = _attempt;\n    }\n\n    /**\n     * @notice Executes post-intent action and attempts reentrancy attack\n     * @param _intent The intent data\n     * @param _amountNetFees Amount of funds to transfer after fees are deducted\n     */\n    function execute(\n        IOrchestrator.Intent memory _intent,\n        uint256 _amountNetFees,\n        bytes calldata /* _fulfillIntentData */\n    ) external override {\n        // Increment attempt counter\n        reentrancyAttempts++;\n        \n        // Attempt reentrancy attack if enabled\n        if (attemptReentry && reentrancyAttempts == 1) {\n            // Only attempt once to avoid infinite loop\n            attemptReentry = false;\n            \n            // Try to call fulfillIntent again (should fail with ReentrancyGuard)\n            try orchestrator.fulfillIntent(storedFulfillParams) {\n                // This should never execute due to reentrancy guard\n                emit ReentrancyAttempted(true);\n            } catch {\n                // Expected behavior - reentrancy guard blocks the call\n                emit ReentrancyAttempted(false);\n            }\n        }\n        \n        // Normal execution - transfer funds to intended recipient\n        // Pull USDC from orchestrator (which approved this amount)\n        usdc.transferFrom(msg.sender, _intent.to, _amountNetFees);\n        \n        emit ExecutionCompleted(_intent.to, _amountNetFees);\n    }\n    \n    /**\n     * @notice Returns the number of reentrancy attempts made\n     */\n    function getReentrancyAttempts() external view returns (uint256) {\n        return reentrancyAttempts;\n    }\n}"
    },
    "contracts/mocks/ThresholdSigVerifierUtilsMock.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.18;\n\nimport { ThresholdSigVerifierUtils } from \"../lib/ThresholdSigVerifierUtils.sol\";\n\n/**\n * @title ThresholdSigVerifierUtilsMock\n * @notice Mock contract for testing ThresholdSigVerifierUtils library\n */\ncontract ThresholdSigVerifierUtilsMock {\n    \n    using ThresholdSigVerifierUtils for bytes32;\n    \n    /**\n     * @notice Exposes the internal verifyWitnessSignatures function for testing\n     */\n    function verifyWitnessSignatures(\n        bytes32 _messageHash,\n        bytes[] memory _signatures,\n        address[] memory _witnesses,\n        uint256 _reqThreshold\n    )\n        external\n        view\n        returns (bool)\n    {\n        return _messageHash.verifyWitnessSignatures(\n            _signatures,\n            _witnesses,\n            _reqThreshold\n        );\n    }\n}\n"
    },
    "contracts/mocks/USDCMock.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\nimport { ERC20 } from \"@openzeppelin/contracts/token/ERC20/ERC20.sol\";\n\npragma solidity ^0.8.18;\n\ncontract USDCMock is ERC20 {\n\n    constructor(\n        uint256 _mintAmount,\n        string memory name,\n        string memory symbol\n    )\n        ERC20(name, symbol)\n    {\n        _mint(msg.sender, _mintAmount);\n    }\n\n    function decimals() public pure override returns (uint8) {\n        return 6;\n    }\n}\n"
    },
    "contracts/Orchestrator.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.18;\n\nimport { ECDSA } from \"@openzeppelin/contracts/utils/cryptography/ECDSA.sol\";\nimport { IERC20 } from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport { SafeERC20 } from \"@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol\";\nimport { Ownable } from \"@openzeppelin/contracts/access/Ownable.sol\";\nimport { Pausable } from \"@openzeppelin/contracts/security/Pausable.sol\";\nimport { SignatureChecker } from \"@openzeppelin/contracts/utils/cryptography/SignatureChecker.sol\";\nimport { ReentrancyGuard } from \"@openzeppelin/contracts/security/ReentrancyGuard.sol\";\nimport { AddressArrayUtils } from \"./external/AddressArrayUtils.sol\";\nimport { Bytes32ArrayUtils } from \"./external/Bytes32ArrayUtils.sol\";\nimport { IOrchestrator } from \"./interfaces/IOrchestrator.sol\";\nimport { IEscrow } from \"./interfaces/IEscrow.sol\";\nimport { IEscrowRegistry } from \"./interfaces/IEscrowRegistry.sol\";\nimport { IPostIntentHook } from \"./interfaces/IPostIntentHook.sol\";\nimport { IPaymentVerifier } from \"./interfaces/IPaymentVerifier.sol\";\nimport { IPaymentVerifierRegistry } from \"./interfaces/IPaymentVerifierRegistry.sol\";\nimport { IPostIntentHookRegistry } from \"./interfaces/IPostIntentHookRegistry.sol\";\nimport { IRelayerRegistry } from \"./interfaces/IRelayerRegistry.sol\";\n\n/**\n * @title Orchestrator\n * @notice Orchestrator contract for the ZKP2P protocol. This contract is responsible for managing the intent (order) \n * lifecycle and orchestrating the P2P trading of fiat currency and onchain assets.\n */\ncontract Orchestrator is Ownable, Pausable, ReentrancyGuard, IOrchestrator {\n\n    using AddressArrayUtils for address[];\n    using Bytes32ArrayUtils for bytes32[];\n    using ECDSA for bytes32;\n    using SafeERC20 for IERC20;\n    using SignatureChecker for address;\n\n\n    /* ============ Constants ============ */\n    uint256 internal constant PRECISE_UNIT = 1e18;\n    uint256 constant CIRCOM_PRIME_FIELD = 21888242871839275222246405745257275088548364400416034343698204186575808495617;\n    uint256 constant MAX_REFERRER_FEE = 5e16;      // 5% max referrer fee\n    uint256 constant MAX_PROTOCOL_FEE = 5e16;      // 5% max protocol fee\n\n    /* ============ State Variables ============ */\n\n    uint256 immutable public chainId;              // chainId of the chain the orchestrator is deployed on\n\n    mapping(bytes32 => Intent) internal intents;                       // Mapping of intentHashes to intent structs\n    mapping(address => bytes32[]) internal accountIntents;             // Mapping of address to array of intentHashes\n\n    // Snapshot of the minimum per-intent amount at the time of lock (signal)\n    // Used to prevent fulfillments that pay out less than the deposit's min intent amount.\n    mapping(bytes32 => uint256) internal intentMinAtSignal;\n\n    // Contract references\n    IEscrowRegistry public escrowRegistry;                              // Registry of escrow contracts\n    IPaymentVerifierRegistry public  paymentVerifierRegistry;          // Registry of payment verifiers\n    IPostIntentHookRegistry public postIntentHookRegistry;             // Registry of post intent hooks\n    IRelayerRegistry public relayerRegistry;                           // Registry of relayers\n\n    // Protocol fee configuration\n    uint256 public protocolFee;                                     // Protocol fee taken from taker (in preciseUnits, 1e16 = 1%)\n    address public protocolFeeRecipient;                            // Address that receives protocol fees\n\n    bool public allowMultipleIntents;                               // Whether to allow multiple intents per account\n\n    uint256 public intentCounter;                                 // Counter for number of intents created; nonce for unique intent hashes\n\n    /* ============ Constructor ============ */\n    constructor(\n        address _owner,\n        uint256 _chainId,\n        address _escrowRegistry,\n        address _paymentVerifierRegistry,\n        address _postIntentHookRegistry,\n        address _relayerRegistry,\n        uint256 _protocolFee,\n        address _protocolFeeRecipient\n    )\n        Ownable()\n    {\n        chainId = _chainId;\n        escrowRegistry = IEscrowRegistry(_escrowRegistry);\n        paymentVerifierRegistry = IPaymentVerifierRegistry(_paymentVerifierRegistry);\n        postIntentHookRegistry = IPostIntentHookRegistry(_postIntentHookRegistry);\n        relayerRegistry = IRelayerRegistry(_relayerRegistry);\n        protocolFee = _protocolFee;\n        protocolFeeRecipient = _protocolFeeRecipient;\n\n        transferOwnership(_owner);\n    }\n\n    /* ============ External Functions ============ */\n\n    /**\n     * @notice Signals intent to pay the depositor defined in the _depositId the _amount * deposit conversionRate off-chain\n     * in order to unlock _amount of funds on-chain. Caller must provide a signature from the deposit's gating service to prove\n     * their eligibility to take liquidity. If there are prunable intents then they will be deleted from the deposit to be able \n     * to maintain state hygiene. Locks liquidity for the corresponding deposit on the escrow contract.\n     *\n     * @param _params                   Struct containing all the intent parameters\n     */\n    function signalIntent(SignalIntentParams calldata _params)\n        external\n        whenNotPaused\n    {\n        // Checks\n        _validateSignalIntent(_params);\n\n        // Effects\n        bytes32 intentHash = _calculateIntentHash();\n        IEscrow.Deposit memory dep = IEscrow(_params.escrow).getDeposit(_params.depositId);\n        \n        intentMinAtSignal[intentHash] = dep.intentAmountRange.min;\n        intents[intentHash] = Intent({\n            owner: msg.sender,\n            to: _params.to,\n            escrow: _params.escrow,\n            depositId: _params.depositId,\n            amount: _params.amount,\n            paymentMethod: _params.paymentMethod,\n            fiatCurrency: _params.fiatCurrency,\n            conversionRate: _params.conversionRate,\n            timestamp: block.timestamp,\n            referrer: _params.referrer,\n            referrerFee: _params.referrerFee,\n            postIntentHook: _params.postIntentHook,\n            data: _params.data\n        });\n\n        accountIntents[msg.sender].push(intentHash);\n        intentCounter++;\n\n        emit IntentSignaled(\n            intentHash, \n            _params.escrow,\n            _params.depositId, \n            _params.paymentMethod, \n            msg.sender, \n            _params.to, \n            _params.amount, \n            _params.fiatCurrency, \n            _params.conversionRate, \n            block.timestamp\n        );\n\n        // Interactions\n        IEscrow(_params.escrow).lockFunds(_params.depositId, intentHash, _params.amount);\n    }\n\n    /**\n     * @notice Only callable by the originator of the intent. Cancels an outstanding intent. Unlocks liquidity\n     * for the corresponding deposit on the escrow contract.\n     *\n     * @param _intentHash    Hash of intent being cancelled\n     */\n    function cancelIntent(bytes32 _intentHash) external {\n        // Checks\n        Intent memory intent = intents[_intentHash];\n        \n        if (intent.timestamp == 0) revert IntentNotFound(_intentHash);\n        if (intent.owner != msg.sender) revert UnauthorizedCaller(msg.sender, intent.owner);\n\n        // Effects\n        _pruneIntent(_intentHash);\n\n        // Interactions\n        IEscrow(intent.escrow).unlockFunds(intent.depositId, _intentHash);\n    }\n\n    /**\n     * @notice Anyone can submit a fulfill intent transaction, even if caller isn't the intent owner. Upon submission the\n     * offchain payment proof is verified, payment details are validated, intent is removed, and escrow state is updated. \n     * Deposit token is transferred to the intent.to address.\n     * @dev This function adds a reentrancy guard as it's calling the post intent hook contract which itself might call \n     * malicious contracts.\n     *\n     * @param _params               Struct containing all the fulfill intent parameters\n     */\n    function fulfillIntent(FulfillIntentParams calldata _params) external nonReentrant whenNotPaused {\n        // Checks\n        Intent memory intent = intents[_params.intentHash];\n        if (intent.paymentMethod == bytes32(0)) revert IntentNotFound(_params.intentHash);\n        \n        IEscrow.Deposit memory deposit = IEscrow(intent.escrow).getDeposit(intent.depositId);\n        \n        address verifier = paymentVerifierRegistry.getVerifier(intent.paymentMethod);\n        if (verifier == address(0)) revert PaymentMethodDoesNotExist(intent.paymentMethod);\n        \n        IPaymentVerifier.PaymentVerificationResult memory verificationResult = IPaymentVerifier(verifier).verifyPayment(\n            IPaymentVerifier.VerifyPaymentData({\n                intentHash: _params.intentHash,\n                paymentProof: _params.paymentProof,\n                data: _params.verificationData\n            })\n        );\n        if (!verificationResult.success) revert PaymentVerificationFailed();\n        if (verificationResult.intentHash != _params.intentHash) revert HashMismatch(_params.intentHash, verificationResult.intentHash);\n\n        // Enforce snapshot min-at-signal to prevent sub-min partial fulfillments\n        uint256 minAtSignal = intentMinAtSignal[_params.intentHash];\n        if (minAtSignal > 0 && verificationResult.releaseAmount < minAtSignal) {\n            revert AmountBelowMin(verificationResult.releaseAmount, minAtSignal);\n        }\n\n        // Effects\n        _pruneIntent(_params.intentHash);\n\n        // Interactions\n        IEscrow(intent.escrow).unlockAndTransferFunds(intent.depositId, _params.intentHash, verificationResult.releaseAmount, address(this));\n\n        _collectFeesTransferFundsAndExecuteAction(\n            deposit.token, \n            _params.intentHash, \n            intent, \n            verificationResult.releaseAmount,\n            _params.postIntentHookData\n        );\n    }\n\n    /**\n     * @notice Allows depositor to release funds to the payer in case of a failed fulfill intent or because of some other arrangement\n     * between the two parties. Upon submission we check to make sure the msg.sender is the depositor, the intent is removed, and \n     * escrow state is updated. Deposit token is transferred to the payer.\n     *\n     * @param _intentHash        Hash of intent to resolve by releasing the funds\n     */\n    function releaseFundsToPayer(bytes32 _intentHash) external nonReentrant {\n        // Checks\n        Intent memory intent = intents[_intentHash];\n        if (intent.owner == address(0)) revert IntentNotFound(_intentHash);\n\n        IEscrow.Deposit memory deposit = IEscrow(intent.escrow).getDeposit(intent.depositId);\n        if (deposit.depositor != msg.sender) revert UnauthorizedCaller(msg.sender, deposit.depositor);\n        \n        // Effects\n        _pruneIntent(_intentHash);\n\n        // Interactions\n        IEscrow(intent.escrow).unlockAndTransferFunds(intent.depositId, _intentHash, intent.amount, address(this));\n\n        _collectFeesAndTransferFunds(deposit.token, _intentHash, intent, intent.amount);\n    }\n\n    /* ============ Escrow Functions ============ */\n\n    /**\n     * @notice Only the escrow contract owns the intent can call this function. Called by escrow to prune specific\n     * expired intents. Escrow leads the cleanup process.\n     * \n     * @param _intents   Array of intent hashes to prune\n     */\n    function pruneIntents(bytes32[] calldata _intents) external {\n        for (uint256 i = 0; i < _intents.length; i++) {\n            bytes32 intentHash = _intents[i];\n            if (intentHash != bytes32(0)) {\n                Intent memory intent = intents[intentHash];\n                if (\n                    intent.timestamp != 0 && // Only prune if intent exists on this contract; otherwise skip\n                    intent.escrow == msg.sender // Ensure only the escrow that owns the intent can prune it; otherwise skip\n                ) {\n                    _pruneIntent(intentHash);\n                }\n            }\n        }\n    }\n\n    /* ============ Governance Functions ============ */\n\n    /**\n     * @notice GOVERNANCE ONLY: Updates the escrow registry address.\n     *\n     * @param _escrowRegistry   New escrow registry address\n     */\n    function setEscrowRegistry(address _escrowRegistry) external onlyOwner {\n        if (_escrowRegistry == address(0)) revert ZeroAddress();\n        \n        escrowRegistry = IEscrowRegistry(_escrowRegistry);\n        emit EscrowRegistryUpdated(_escrowRegistry);\n    }\n\n    /**\n     * @notice GOVERNANCE ONLY: Updates the protocol fee. This fee is charged to takers upon a successful\n     * fulfillment of an intent.\n     *\n     * @param _protocolFee   New protocol fee in preciseUnits (1e16 = 1%)\n     */\n    function setProtocolFee(uint256 _protocolFee) external onlyOwner {\n        if (_protocolFee > MAX_PROTOCOL_FEE) revert FeeExceedsMaximum(_protocolFee, MAX_PROTOCOL_FEE);\n        \n        protocolFee = _protocolFee;\n        emit ProtocolFeeUpdated(_protocolFee);\n    }\n\n    /**\n     * @notice GOVERNANCE ONLY: Updates the protocol fee recipient address.\n     *\n     * @param _protocolFeeRecipient   New protocol fee recipient address\n     */\n    function setProtocolFeeRecipient(address _protocolFeeRecipient) external onlyOwner {\n        if (_protocolFeeRecipient == address(0)) revert ZeroAddress();\n        \n        protocolFeeRecipient = _protocolFeeRecipient;\n        emit ProtocolFeeRecipientUpdated(_protocolFeeRecipient);\n    }\n\n    /**\n     * @notice GOVERNANCE ONLY: Sets whether all accounts can signal multiple intents.\n     *\n     * @param _allowMultiple   True to allow all accounts to signal multiple intents, false to restrict to whitelisted relayers only\n     */\n    function setAllowMultipleIntents(bool _allowMultiple) external onlyOwner {\n        allowMultipleIntents = _allowMultiple;\n        \n        emit AllowMultipleIntentsUpdated(_allowMultiple);\n    }\n\n    /**\n     * @notice GOVERNANCE ONLY: Updates the post intent hook registry address.\n     *\n     * @param _postIntentHookRegistry   New post intent hook registry address\n     */\n    function setPostIntentHookRegistry(address _postIntentHookRegistry) external onlyOwner {\n        if (_postIntentHookRegistry == address(0)) revert ZeroAddress();\n        \n        postIntentHookRegistry = IPostIntentHookRegistry(_postIntentHookRegistry);\n        emit PostIntentHookRegistryUpdated(_postIntentHookRegistry);\n    }\n\n    /**\n     * @notice GOVERNANCE ONLY: Updates the relayer registry address.\n     *\n     * @param _relayerRegistry   New relayer registry address\n     */\n    function setRelayerRegistry(address _relayerRegistry) external onlyOwner {\n        if (_relayerRegistry == address(0)) revert ZeroAddress();\n        \n        relayerRegistry = IRelayerRegistry(_relayerRegistry);\n        emit RelayerRegistryUpdated(_relayerRegistry);\n    }\n\n    /**\n     * @notice GOVERNANCE ONLY: Pauses intent creation and fulfillment functionality.\n     * \n     * Functionalities that are paused:\n     * - Intent creation (signalIntent)\n     * - Intent fulfillment (fulfillIntent)\n     *\n     * Functionalities that remain unpaused to allow users to recover funds:\n     * - Intent cancellation (cancelIntent)\n     * - Manual fund release by depositor (releaseFundsToPayer)\n     * - Intent pruning by escrow (pruneIntents)\n     * - All governance functions\n     * - All view functions\n     */\n    function pauseOrchestrator() external onlyOwner {\n        _pause();\n    }\n\n    /**\n     * @notice GOVERNANCE ONLY: Restarts paused functionality for the orchestrator.\n     */\n    function unpauseOrchestrator() external onlyOwner {\n        _unpause();\n    }\n\n    /* ============ External View Functions ============ */\n\n    function getIntent(bytes32 _intentHash) external view returns (Intent memory) {\n        return intents[_intentHash];\n    }\n\n    function getAccountIntents(address _account) external view returns (bytes32[] memory) {\n        return accountIntents[_account];\n    }\n\n    function getIntentMinAtSignal(bytes32 _intentHash) external view returns (uint256) {\n        return intentMinAtSignal[_intentHash];\n    }\n\n    /* ============ Internal Functions ============ */\n\n    /**\n     * @notice Validates an intent before it is signaled.\n     */\n    function _validateSignalIntent(SignalIntentParams memory _intent) internal view {\n        // Check if account can have multiple intents\n        bool canHaveMultipleIntents = relayerRegistry.isWhitelistedRelayer(msg.sender) || allowMultipleIntents;\n        if (!canHaveMultipleIntents && accountIntents[msg.sender].length > 0) {\n            revert AccountHasActiveIntent(msg.sender, accountIntents[msg.sender][0]);\n        }\n\n        if (_intent.to == address(0)) revert ZeroAddress();\n        \n        if (_intent.referrerFee > MAX_REFERRER_FEE) revert FeeExceedsMaximum(_intent.referrerFee, MAX_REFERRER_FEE);\n        if (_intent.referrer == address(0)) {\n            if (_intent.referrerFee != 0) revert InvalidReferrerFeeConfiguration();\n        }\n\n        if (address(_intent.postIntentHook) != address(0)) {\n            if (!postIntentHookRegistry.isWhitelistedHook(address(_intent.postIntentHook))) {\n                revert PostIntentHookNotWhitelisted(address(_intent.postIntentHook));\n            }\n        }\n\n        // Validate escrow is whitelisted\n        if (!escrowRegistry.isWhitelistedEscrow(_intent.escrow) && !escrowRegistry.isAcceptingAllEscrows()) {\n            revert EscrowNotWhitelisted(_intent.escrow);\n        }\n\n        // Verify payment method is still valid in registry\n        address verifier = paymentVerifierRegistry.getVerifier(_intent.paymentMethod);\n        if (verifier == address(0)) revert PaymentMethodDoesNotExist(_intent.paymentMethod);\n        \n        bool isPaymentMethodActive = IEscrow(_intent.escrow).getDepositPaymentMethodActive(_intent.depositId, _intent.paymentMethod);\n        if (!isPaymentMethodActive) revert PaymentMethodNotSupported(_intent.paymentMethod);\n        \n        uint256 minConversionRate = IEscrow(_intent.escrow).getDepositCurrencyMinRate(\n            _intent.depositId, _intent.paymentMethod, _intent.fiatCurrency\n        );\n        if (minConversionRate == 0) revert CurrencyNotSupported(_intent.paymentMethod, _intent.fiatCurrency);\n        if (_intent.conversionRate < minConversionRate) revert RateBelowMinimum(_intent.conversionRate, minConversionRate);\n\n        address intentGatingService = IEscrow(_intent.escrow).getDepositGatingService(_intent.depositId, _intent.paymentMethod);\n        if (intentGatingService != address(0)) {\n            // Check if signature has expired\n            if (block.timestamp > _intent.signatureExpiration) {\n                revert SignatureExpired(_intent.signatureExpiration, block.timestamp);\n            }\n\n            if (!_isValidIntentGatingSignature(_intent, intentGatingService)) {\n                revert InvalidSignature();\n            }\n        }\n    }\n\n    /**\n     * @notice Calculates a unique hash for an intent using the orchestrator address and counter.\n     */\n    function _calculateIntentHash() internal view returns (bytes32 intentHash) {\n        // Use orchestrator address + counter for global uniqueness\n        // Mod with circom prime field to make sure it fits in a 254-bit field\n        uint256 intermediateHash = uint256(\n            keccak256(\n                abi.encodePacked(\n                    address(this),    // Include orchestrator address for avoiding collisions when migrating to a new orchestrator\n                    // or when multiple orchestrators are deployed\n                    intentCounter     // unique counter within this orchestrator\n                )\n            ));\n        intentHash = bytes32(intermediateHash % CIRCOM_PRIME_FIELD);\n    }\n\n\n    /**\n     * @notice Deletes an intent from storage mappings.\n     */\n    function _pruneIntent(bytes32 _intentHash) internal {\n        Intent memory intent = intents[_intentHash];\n\n        accountIntents[intent.owner].removeStorage(_intentHash);\n        delete intents[_intentHash];\n        delete intentMinAtSignal[_intentHash];\n\n        emit IntentPruned(_intentHash);\n    }\n\n    /**\n     * @notice Calculates and transfers fees to the protocol fee recipient and referrer.\n     */\n    function _calculateAndTransferFees(\n        IERC20 _token,\n        Intent memory _intent, \n        uint256 _releaseAmount\n    ) internal returns (uint256 netFees) {\n        uint256 protocolFeeAmount;\n        uint256 referrerFeeAmount; \n\n        // Calculate protocol fee (taken from taker) - based on release amount\n        if (protocolFeeRecipient != address(0) && protocolFee > 0) {\n            protocolFeeAmount = (_releaseAmount * protocolFee) / PRECISE_UNIT;\n            _token.safeTransfer(protocolFeeRecipient, protocolFeeAmount);\n        }\n        \n        // Calculate referrer fee (taken from taker) - based on release amount\n        if (_intent.referrer != address(0) && _intent.referrerFee > 0) {\n            referrerFeeAmount = (_releaseAmount * _intent.referrerFee) / PRECISE_UNIT;\n            _token.safeTransfer(_intent.referrer, referrerFeeAmount);\n        }\n\n        netFees = protocolFeeAmount + referrerFeeAmount;\n    }\n\n    /**\n     * @notice Transfers funds to the intent recipient. Called by manual release.\n     */\n    function _collectFeesAndTransferFunds(\n        IERC20 _token, \n        bytes32 _intentHash, \n        Intent memory _intent,\n        uint256 _releaseAmount\n    ) internal {\n        uint256 netFees = _calculateAndTransferFees(_token, _intent, _releaseAmount);\n        uint256 netAmount = _releaseAmount - netFees;\n\n        _token.safeTransfer(_intent.to, netAmount);\n\n        emit IntentFulfilled(\n            _intentHash, \n            _intent.to, \n            netAmount, \n            true\n        );\n    }\n\n    /**\n     * @notice Handles fee calculations and transfers, then executes any post-intent hooks. Called by fulfillIntent.\n     */\n    function _collectFeesTransferFundsAndExecuteAction(\n        IERC20 _token, \n        bytes32 _intentHash, \n        Intent memory _intent, \n        uint256 _releaseAmount,\n        bytes memory _postIntentHookData\n    ) internal {\n        uint256 netFees = _calculateAndTransferFees(_token, _intent, _releaseAmount);\n        uint256 netAmount = _releaseAmount - netFees;\n\n        // If there's a post-intent hook, handle it; skip if manual release\n        address fundsTransferredTo = _intent.to;\n        if (address(_intent.postIntentHook) != address(0)) {\n            // Snapshot balance to enforce exact consumption by the hook\n            uint256 preBalance = _token.balanceOf(address(this));\n\n            // Grant exact allowance to the post-intent hook using SafeERC20 with zero-before-set\n            _token.safeApprove(address(_intent.postIntentHook), 0);\n            _token.safeApprove(address(_intent.postIntentHook), netAmount);\n            _intent.postIntentHook.execute(_intent, netAmount, _postIntentHookData);\n            \n            // Enforce that the hook pulled exactly netAmount to prevent stranded funds\n            uint256 postBalance = _token.balanceOf(address(this));\n            require(postBalance <= preBalance, \"PostIntentHook: unexpected balance increase\");\n            uint256 spent = preBalance - postBalance;\n            require(spent == netAmount, \"PostIntentHook: must pull exact netAmount\");\n\n            // Reset allowance to prevent residual balance drainage (and fail closed on non-standard ERC20s)\n            _token.safeApprove(address(_intent.postIntentHook), 0);\n\n            fundsTransferredTo = address(_intent.postIntentHook);\n        } else {\n            // Otherwise transfer directly to the intent recipient\n            _token.safeTransfer(_intent.to, netAmount);\n        }\n\n        emit IntentFulfilled(\n            _intentHash, \n            fundsTransferredTo, \n            netAmount, \n            false\n        );\n    }\n\n\n    /**\n     * @notice Checks if a intent gating service signature is valid.\n     */\n    function _isValidIntentGatingSignature(\n        SignalIntentParams memory _intent, \n        address _intentGatingService\n    ) \n        internal \n        view \n        returns(bool) \n    {\n        bytes memory message = abi.encodePacked(\n            address(this),\n            _intent.escrow, \n            _intent.depositId, \n            _intent.amount, \n            _intent.to, \n            _intent.paymentMethod, \n            _intent.fiatCurrency, \n            _intent.conversionRate, \n            _intent.signatureExpiration,\n            chainId\n        );\n\n        bytes32 verifierPayload = keccak256(message).toEthSignedMessageHash();\n        return _intentGatingService.isValidSignatureNow(verifierPayload, _intent.gatingServiceSignature);\n    }\n}\n"
    },
    "contracts/ProtocolViewer.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.18;\n\nimport { IEscrow } from \"./interfaces/IEscrow.sol\";\nimport { IProtocolViewer } from \"./interfaces/IProtocolViewer.sol\";\nimport { IOrchestrator } from \"./interfaces/IOrchestrator.sol\";\n\ncontract ProtocolViewer is IProtocolViewer {\n\n    /* ============ State Variables ============ */\n    IEscrow public immutable escrowContract;\n    IOrchestrator public immutable orchestrator;\n\n    /* ============ Constructor ============ */\n\n    constructor(address _escrow, address _orchestrator) {\n        escrowContract = IEscrow(_escrow);\n        orchestrator = IOrchestrator(_orchestrator);\n    }\n\n    /* ============ View Functions ============ */\n\n    /**\n     * @notice Gets details for a single deposit.\n     * @param _depositId The ID of the deposit.\n     * @return depositView The DepositView struct.\n     */\n    function getDeposit(uint256 _depositId) public view returns (IProtocolViewer.DepositView memory depositView) {\n        IEscrow.Deposit memory deposit = escrowContract.getDeposit(_depositId);\n        ( , uint256 reclaimableAmount) = escrowContract.getExpiredIntents(_depositId);\n        bytes32[] memory intentHashes = escrowContract.getDepositIntentHashes(_depositId);\n\n        bytes32[] memory paymentMethods = escrowContract.getDepositPaymentMethods(_depositId);\n        PaymentMethodDataView[] memory paymentMethodViews = new PaymentMethodDataView[](paymentMethods.length);\n        for (uint256 i = 0; i < paymentMethods.length; ++i) {\n            bytes32 paymentMethod = paymentMethods[i];\n            IEscrow.Currency[] memory currencies = new IEscrow.Currency[](escrowContract.getDepositCurrencies(_depositId, paymentMethod).length);\n            for (uint256 j = 0; j < currencies.length; ++j) {\n                bytes32 code = escrowContract.getDepositCurrencies(_depositId, paymentMethod)[j];\n                currencies[j] = IEscrow.Currency({\n                    code: code,\n                    minConversionRate: escrowContract.getDepositCurrencyMinRate(_depositId, paymentMethod, code)\n                });\n            }\n            paymentMethodViews[i] = PaymentMethodDataView({\n                paymentMethod: paymentMethod,\n                verificationData: escrowContract.getDepositPaymentMethodData(_depositId, paymentMethod),\n                currencies: currencies\n            });\n        }\n\n        depositView = DepositView({\n            depositId: _depositId,\n            deposit: deposit,\n            availableLiquidity: deposit.remainingDeposits + reclaimableAmount,\n            paymentMethods: paymentMethodViews,\n            intentHashes: intentHashes\n        });\n    }\n\n    /**\n     * @notice Gets deposit details for a list of deposit IDs.\n     * @param _depositIds Array of deposit IDs.\n     * @return depositArray Array of DepositView structs.\n     */\n    function getDepositFromIds(\n        uint256[] memory _depositIds\n    ) external view override returns (IProtocolViewer.DepositView[] memory depositArray) {\n        depositArray = new DepositView[](_depositIds.length);\n\n        for (uint256 i = 0; i < _depositIds.length; ++i) {\n            uint256 depositId = _depositIds[i];\n            depositArray[i] = getDeposit(depositId);\n        }\n    }\n\n    /**\n     * @notice Gets all deposits for a specific account.\n     * @param _account The account address.\n     * @return depositArray Array of DepositView structs.\n     */\n    function getAccountDeposits(address _account) external view returns (IProtocolViewer.DepositView[] memory depositArray) {\n        uint256[] memory accountDepositIds = escrowContract.getAccountDeposits(_account);\n        depositArray = new DepositView[](accountDepositIds.length);\n        \n        for (uint256 i = 0; i < accountDepositIds.length; ++i) {\n            uint256 depositId = accountDepositIds[i];\n            depositArray[i] = getDeposit(depositId);\n        }\n    }\n\n    /**\n     * @notice Gets details for a single intent.\n     * @param _intentHash The hash of the intent.\n     * @return intentView The IntentView struct.\n     */\n    function getIntent(bytes32 _intentHash) public view returns (IProtocolViewer.IntentView memory intentView) {\n        IOrchestrator.Intent memory intent = orchestrator.getIntent(_intentHash);\n        DepositView memory deposit = getDeposit(intent.depositId);\n        intentView = IntentView({\n            intentHash: _intentHash,\n            intent: intent,\n            deposit: deposit\n        });\n    }\n\n    /**\n     * @notice Gets details for a list of intent hashes.\n     * @param _intentHashes Array of intent hashes.\n     * @return intentArray Array of IntentView structs.\n     */\n    function getIntents(bytes32[] calldata _intentHashes) external view returns (IProtocolViewer.IntentView[] memory intentArray) {\n        intentArray = new IntentView[](_intentHashes.length);\n\n        for (uint256 i = 0; i < _intentHashes.length; ++i) {\n            intentArray[i] = getIntent(_intentHashes[i]);\n        }\n    }\n\n    /**\n     * @notice Gets the active intents for a specific account.\n     * @param _account The account address.\n     * @return intentViews Array of IntentView structs.\n     */\n    function getAccountIntents(address _account) external view returns (IProtocolViewer.IntentView[] memory intentViews) {\n        bytes32[] memory intentHashes = orchestrator.getAccountIntents(_account);\n        intentViews = new IntentView[](intentHashes.length);\n        \n        for (uint256 i = 0; i < intentHashes.length; ++i) {\n            intentViews[i] = getIntent(intentHashes[i]);\n        }\n    }\n}\n"
    },
    "contracts/registries/EscrowRegistry.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\nimport { Ownable } from \"@openzeppelin/contracts/access/Ownable.sol\";\nimport { AddressArrayUtils } from \"../external/AddressArrayUtils.sol\";\nimport { IEscrowRegistry } from \"../interfaces/IEscrowRegistry.sol\";\n\npragma solidity ^0.8.18;\n\ncontract EscrowRegistry is Ownable, IEscrowRegistry {\n\n    using AddressArrayUtils for address[];\n    \n    /* ============ Events ============ */\n    event EscrowAdded(address indexed escrow);\n    event EscrowRemoved(address indexed escrow);\n    event AcceptAllEscrowsUpdated(bool acceptAll);\n\n    /* ============ State Variables ============ */\n    bool public acceptAllEscrows;\n    mapping(address => bool) public isWhitelistedEscrow;\n    address[] public escrows;\n\n    /* ============ Constructor ============ */\n    constructor() Ownable() {}\n    \n    /* ============ External Functions ============ */\n\n    /**\n     * ONLY OWNER: Adds an escrow to the whitelist.\n     *\n     * @param _escrow   The escrow address to add\n     */\n    function addEscrow(address _escrow) external onlyOwner {\n        require(_escrow != address(0), \"Escrow cannot be zero address\");\n        require(!isWhitelistedEscrow[_escrow], \"Escrow already whitelisted\");\n        \n        isWhitelistedEscrow[_escrow] = true;\n        escrows.push(_escrow);\n        \n        emit EscrowAdded(_escrow);\n    }\n\n    /**\n     * ONLY OWNER: Removes an escrow from the whitelist.\n     *\n     * @param _escrow   The escrow address to remove\n     */\n    function removeEscrow(address _escrow) external onlyOwner {\n        require(isWhitelistedEscrow[_escrow], \"Escrow not whitelisted\");\n        \n        isWhitelistedEscrow[_escrow] = false;\n        escrows.removeStorage(_escrow);\n        \n        emit EscrowRemoved(_escrow);\n    }\n\n    /**\n     * ONLY OWNER: Sets whether all escrows can be used without whitelisting.\n     *\n     * @param _acceptAll   True to accept all escrows, false to require whitelisting\n     */\n    function setAcceptAllEscrows(bool _acceptAll) external onlyOwner {\n        acceptAllEscrows = _acceptAll;\n        \n        emit AcceptAllEscrowsUpdated(_acceptAll);\n    }\n\n    /* ============ External View Functions ============ */\n\n    function isAcceptingAllEscrows() external view returns (bool) {\n        return acceptAllEscrows;\n    }\n\n    function getWhitelistedEscrows() external view returns (address[] memory) {\n        return escrows;\n    }\n}"
    },
    "contracts/registries/NullifierRegistry.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\nimport { Ownable } from \"@openzeppelin/contracts/access/Ownable.sol\";\n\nimport { AddressArrayUtils } from \"../external/AddressArrayUtils.sol\";\nimport { INullifierRegistry } from \"../interfaces/INullifierRegistry.sol\";\n\npragma solidity ^0.8.18;\n\n\ncontract NullifierRegistry is Ownable, INullifierRegistry {\n\n    using AddressArrayUtils for address[];\n    \n    /* ============ Events ============ */\n    event NullifierAdded(bytes32 nullifier, address indexed writer);\n    event WriterAdded(address writer);\n    event WriterRemoved(address writer);\n\n    /* ============ Modifiers ============ */\n    modifier onlyWriter() {\n        require(isWriter[msg.sender], \"Only addresses with write permissions can call\");\n        _;\n    }\n\n    /* ============ State Variables ============ */\n    mapping(bytes32 => bool) public isNullified;\n    mapping(address => bool) public isWriter;\n    address[] public writers;\n\n    /* ============ Constructor ============ */\n    constructor() Ownable() {}\n    \n    /* ============ External Functions ============ */\n\n    /**\n     * ONLY WRITER: Only addresses with permission to write to this contract can call. Stores a nullifier for an email.\n     *\n     * @param _nullifier    The nullifier to store\n     */\n    function addNullifier(bytes32 _nullifier) external onlyWriter {\n        require(!isNullified[_nullifier], \"Nullifier already exists\");\n\n        isNullified[_nullifier] = true;\n\n        emit NullifierAdded(_nullifier, msg.sender);\n    }\n\n    /* ============ Admin Functions ============ */\n\n    /**\n     * ONLY OWNER: Add address that has write permissions to the registry. Writer must not have been previously added.\n     *\n     * @param _newWriter    The nullifier to store\n     */\n    function addWritePermission(address _newWriter) external onlyOwner {\n        require(!isWriter[_newWriter], \"Address is already a writer\");\n\n        isWriter[_newWriter] = true;\n        writers.push(_newWriter);\n\n        emit WriterAdded(_newWriter);\n    }\n\n    /**\n     * ONLY OWNER: Remove address that has write permissions to the registry. Writer must have been previously added.\n     *\n     * @param _removedWriter    The nullifier to store\n     */\n    function removeWritePermission(address _removedWriter) external onlyOwner {\n        require(isWriter[_removedWriter], \"Address is not a writer\");\n\n        isWriter[_removedWriter] = false;\n        writers.removeStorage(_removedWriter);\n\n        emit WriterRemoved(_removedWriter);\n    }\n\n    /* ============ External View Functions ============ */\n\n    function getWriters() external view returns(address[] memory) {\n       return writers;\n    }\n}\n"
    },
    "contracts/registries/PaymentVerifierRegistry.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\nimport { Ownable } from \"@openzeppelin/contracts/access/Ownable.sol\";\nimport { Bytes32ArrayUtils } from \"../external/Bytes32ArrayUtils.sol\";\nimport { IPaymentVerifierRegistry } from \"../interfaces/IPaymentVerifierRegistry.sol\";\n\npragma solidity ^0.8.18;\n\ncontract PaymentVerifierRegistry is Ownable, IPaymentVerifierRegistry {\n\n    using Bytes32ArrayUtils for bytes32[];\n\n    /* ============ Structs ============ */\n\n    struct PaymentMethodConfig {\n        bool initialized;\n        address verifier;\n        mapping(bytes32 => bool) isCurrency;\n        bytes32[] currencies;\n    }\n    \n    /* ============ Events ============ */\n\n    event PaymentMethodAdded(bytes32 indexed paymentMethod);\n    event PaymentMethodRemoved(bytes32 indexed paymentMethod);\n    event CurrencyAdded(bytes32 indexed paymentMethod, bytes32 indexed currencyCode);\n    event CurrencyRemoved(bytes32 indexed paymentMethod, bytes32 indexed currencyCode);\n\n    /* ============ State Variables ============ */\n    mapping(bytes32 => PaymentMethodConfig) public store;\n    bytes32[] public paymentMethods;\n\n    /* ============ Constructor ============ */\n    constructor() Ownable() {}\n    \n    /* ============ External Functions ============ */\n\n    /**\n     * Adds a new payment method with processors and currencies\n     * @param _paymentMethod The payment method hash; Hash the payment method name in lowercase\n     * @param _verifier The verifier address to add for this payment method\n     * @param _currencies Array of currency code hashes to support\n     */\n    function addPaymentMethod(\n        bytes32 _paymentMethod,\n        address _verifier,\n        bytes32[] calldata _currencies\n    ) external onlyOwner {\n        require(!store[_paymentMethod].initialized, \"Payment method already exists\");\n        require(_verifier != address(0), \"Invalid verifier\");\n        require(_currencies.length > 0, \"Invalid currencies length\");\n        \n        store[_paymentMethod].initialized = true;\n        store[_paymentMethod].verifier = _verifier;\n        \n        addCurrencies(_paymentMethod, _currencies);\n\n        paymentMethods.push(_paymentMethod);\n        \n        emit PaymentMethodAdded(_paymentMethod);\n    }\n    \n    /**\n     * Removes a payment method and associated configuration\n     * @param _paymentMethod The payment method to remove\n     * @dev Only callable by owner\n     */\n    function removePaymentMethod(bytes32 _paymentMethod) external onlyOwner {\n        require(store[_paymentMethod].initialized, \"Payment method does not exist\");\n        \n        bytes32[] memory currencies = store[_paymentMethod].currencies;\n        for (uint256 i = 0; i < currencies.length; i++) {\n            _removeCurrency(_paymentMethod, currencies[i]);\n        }\n        \n        delete store[_paymentMethod];\n        \n        paymentMethods.removeStorage(_paymentMethod);\n        \n        emit PaymentMethodRemoved(_paymentMethod);\n    }\n\n    /**\n     * Adds supported currencies for a specific payment method\n     * @param _paymentMethod The payment method hash\n     * @param _currencies Array of currency code hashes (e.g., keccak256(\"USD\"), keccak256(\"EUR\"))\n     */\n    function addCurrencies(bytes32 _paymentMethod, bytes32[] calldata _currencies) public onlyOwner {\n        require(store[_paymentMethod].initialized, \"Payment method does not exist\");\n        require(_currencies.length > 0, \"Invalid currencies length\");\n        \n        for (uint256 i = 0; i < _currencies.length; i++) {\n            _addCurrency(_paymentMethod, _currencies[i]);\n        }\n    }\n    \n    /**\n     * Removes supported currencies for a specific payment method\n     * @param _paymentMethod The payment method hash\n     * @param _currencies Array of currency code hashes to remove\n     */\n    function removeCurrencies(bytes32 _paymentMethod, bytes32[] calldata _currencies) external onlyOwner {\n        require(_currencies.length > 0, \"Invalid currencies length\");\n        \n        for (uint256 i = 0; i < _currencies.length; i++) {\n            _removeCurrency(_paymentMethod, _currencies[i]);\n        }\n    }\n\n    /* ============ External View Functions ============ */\n    \n    function isPaymentMethod(bytes32 _paymentMethod) external view returns (bool) {\n        return store[_paymentMethod].initialized;\n    }\n\n    function getPaymentMethods() external view returns (bytes32[] memory) {\n        return paymentMethods;\n    }\n    \n    function getVerifier(bytes32 _paymentMethod) external view returns (address) {\n        return store[_paymentMethod].verifier;\n    }\n\n    function isCurrency(bytes32 _paymentMethod, bytes32 _currencyCode) external view returns (bool) {\n        return store[_paymentMethod].isCurrency[_currencyCode];\n    }\n    \n    function getCurrencies(bytes32 _paymentMethod) external view returns (bytes32[] memory) {\n        return store[_paymentMethod].currencies;\n    }\n\n    /* ============ Internal Functions ============ */\n\n    function _addCurrency(bytes32 _paymentMethod, bytes32 _currencyCode) internal {\n        require(_currencyCode != bytes32(0), \"Invalid currency code\");\n        require(!store[_paymentMethod].isCurrency[_currencyCode], \"Currency already exists\");\n\n        store[_paymentMethod].isCurrency[_currencyCode] = true;\n        store[_paymentMethod].currencies.push(_currencyCode);\n        emit CurrencyAdded(_paymentMethod, _currencyCode);\n    }\n    \n    function _removeCurrency(bytes32 _paymentMethod, bytes32 _currencyCode) internal {\n        require(store[_paymentMethod].isCurrency[_currencyCode], \"Currency does not exist\");\n\n        store[_paymentMethod].isCurrency[_currencyCode] = false;\n        store[_paymentMethod].currencies.removeStorage(_currencyCode);\n        emit CurrencyRemoved(_paymentMethod, _currencyCode);\n    }\n} "
    },
    "contracts/registries/PostIntentHookRegistry.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\nimport { Ownable } from \"@openzeppelin/contracts/access/Ownable.sol\";\nimport { AddressArrayUtils } from \"../external/AddressArrayUtils.sol\";\nimport { IPostIntentHookRegistry } from \"../interfaces/IPostIntentHookRegistry.sol\";\n\npragma solidity ^0.8.18;\n\n\ncontract PostIntentHookRegistry is Ownable, IPostIntentHookRegistry {\n\n    using AddressArrayUtils for address[];\n    \n    /* ============ Events ============ */\n    event PostIntentHookAdded(address indexed hook);\n    event PostIntentHookRemoved(address indexed hook);\n\n    /* ============ State Variables ============ */\n    mapping(address => bool) public whitelistedHooks;\n    address[] public hooks;\n\n    /* ============ Constructor ============ */\n    constructor() Ownable() {}\n    \n    /* ============ External Functions ============ */\n\n    /**\n     * ONLY OWNER: Adds a post intent hook to the whitelist.\n     *\n     * @param _hook   The post intent hook address to add\n     */\n    function addPostIntentHook(address _hook) external onlyOwner {\n        require(_hook != address(0), \"Hook cannot be zero address\");\n        require(!whitelistedHooks[_hook], \"Hook already whitelisted\");\n        \n        whitelistedHooks[_hook] = true;\n        hooks.push(_hook);\n        \n        emit PostIntentHookAdded(_hook);\n    }\n\n    /**\n     * ONLY OWNER: Removes a post intent hook from the whitelist.\n     *\n     * @param _hook   The post intent hook address to remove\n     */\n    function removePostIntentHook(address _hook) external onlyOwner {\n        require(whitelistedHooks[_hook], \"Hook not whitelisted\");\n        \n        whitelistedHooks[_hook] = false;\n        hooks.removeStorage(_hook);\n        \n        emit PostIntentHookRemoved(_hook);\n    }\n\n    /* ============ External View Functions ============ */\n\n    function isWhitelistedHook(address _hook) external view returns (bool) {\n        return whitelistedHooks[_hook];\n    }\n\n    function getWhitelistedHooks() external view returns (address[] memory) {\n        return hooks;\n    }\n} "
    },
    "contracts/registries/RelayerRegistry.sol": {
      "content": "//SPDX-License-Identifier: MIT\n\nimport { Ownable } from \"@openzeppelin/contracts/access/Ownable.sol\";\nimport { AddressArrayUtils } from \"../external/AddressArrayUtils.sol\";\nimport { IRelayerRegistry } from \"../interfaces/IRelayerRegistry.sol\";\n\npragma solidity ^0.8.18;\n\ncontract RelayerRegistry is Ownable, IRelayerRegistry {\n\n    using AddressArrayUtils for address[];\n    \n    /* ============ Events ============ */\n    event RelayerAdded(address indexed relayer);\n    event RelayerRemoved(address indexed relayer);\n    \n\n    /* ============ State Variables ============ */\n    mapping(address => bool) public isWhitelistedRelayer;\n    address[] public relayers;\n\n    /* ============ Constructor ============ */\n    constructor() Ownable() {}\n    \n    /* ============ External Functions ============ */\n\n    /**\n     * ONLY OWNER: Adds a relayer to the whitelist.\n     *\n     * @param _relayer   The relayer address to add\n     */\n    function addRelayer(address _relayer) external onlyOwner {\n        require(_relayer != address(0), \"Relayer cannot be zero address\");\n        require(!isWhitelistedRelayer[_relayer], \"Relayer already whitelisted\");\n        \n        isWhitelistedRelayer[_relayer] = true;\n        relayers.push(_relayer);\n        \n        emit RelayerAdded(_relayer);\n    }\n\n    /**\n     * ONLY OWNER: Removes a relayer from the whitelist.\n     *\n     * @param _relayer   The relayer address to remove\n     */\n    function removeRelayer(address _relayer) external onlyOwner {\n        require(isWhitelistedRelayer[_relayer], \"Relayer not whitelisted\");\n        \n        isWhitelistedRelayer[_relayer] = false;\n        relayers.removeStorage(_relayer);\n        \n        emit RelayerRemoved(_relayer);\n    }\n\n\n\n    /* ============ External View Functions ============ */\n\n    function getWhitelistedRelayers() external view returns (address[] memory) {\n        return relayers;\n    }\n} "
    },
    "contracts/unifiedVerifier/BaseUnifiedPaymentVerifier.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.18;\n\nimport { Ownable } from \"@openzeppelin/contracts/access/Ownable.sol\";\nimport { Bytes32ArrayUtils } from \"../external/Bytes32ArrayUtils.sol\";\nimport { IAttestationVerifier } from \"../interfaces/IAttestationVerifier.sol\";\nimport { INullifierRegistry } from \"../interfaces/INullifierRegistry.sol\";\nimport { IOrchestrator } from \"../interfaces/IOrchestrator.sol\";\n\n/**\n * @title BaseUnifiedPaymentVerifier\n * @notice Base contract for unified payment verification that manages configuration for multiple payment methods.\n * \n * This contract handles:\n * - Supported payment methods\n * - Attestation verification through pluggable attestation verifiers\n * \n * @dev This is an abstract contract that must be inherited by concrete implementations.\n *      It replaces the previous BaseReclaimVerifier with a more flexible architecture.\n */\nabstract contract BaseUnifiedPaymentVerifier is Ownable {\n    \n    using Bytes32ArrayUtils for bytes32[];\n    \n    /* ============ Constants ============ */\n\n    uint256 internal constant PRECISE_UNIT = 1e18;\n\n    /* ============ Events ============ */\n    \n    event PaymentMethodAdded(bytes32 indexed paymentMethod);\n    event PaymentMethodRemoved(bytes32 indexed paymentMethod);\n    event AttestationVerifierUpdated(address indexed oldVerifier, address indexed newVerifier);\n\n    /* ============ State Variables ============ */\n    \n    IOrchestrator public immutable orchestrator;\n    INullifierRegistry public immutable nullifierRegistry;\n    IAttestationVerifier public attestationVerifier;\n\n    bytes32[] public paymentMethods;\n    mapping(bytes32 => bool) public isPaymentMethod;\n    \n    /* ============ Modifiers ============ */\n\n    /**\n     * Modifier to ensure only escrow can call\n     */\n    modifier onlyOrchestrator() {\n        require(msg.sender == address(orchestrator), \"Only orchestrator can call\");\n        _;\n    }\n\n    /* ============ Constructor ============ */\n    \n    /**\n     * @notice Initializes base payment verifier\n     * @param _orchestrator The orchestrator contract address that will be used to verify payments\n     * @param _nullifierRegistry The nullifier registry contract that will be used to prevent double-spends\n     * @param _attestationVerifier The attestation verifier contract that will be used to verify attestation by the\n     * offchain / ZK attestation service\n     */\n    constructor(\n        IOrchestrator _orchestrator,\n        INullifierRegistry _nullifierRegistry,\n        IAttestationVerifier _attestationVerifier\n    ) Ownable() {\n        orchestrator = _orchestrator;\n        nullifierRegistry = _nullifierRegistry;\n        attestationVerifier = _attestationVerifier;\n    }\n    \n    /* ============ External Functions ============ */\n    \n    /**\n     * ONLY OWNER: Adds a new payment method with timestamp buffer\n     * @param _paymentMethod The payment method hash; Hash the payment method name in lowercase\n     */\n    function addPaymentMethod(bytes32 _paymentMethod) external onlyOwner {\n        require(!isPaymentMethod[_paymentMethod], \"UPV: Payment method already exists\");\n        \n        isPaymentMethod[_paymentMethod] = true;\n        paymentMethods.push(_paymentMethod);\n        \n        emit PaymentMethodAdded(_paymentMethod);\n    }\n    \n    /**\n     * ONLY OWNER: Removes a payment method and associated configuration\n     * @param _paymentMethod The payment method to remove\n     */\n    function removePaymentMethod(bytes32 _paymentMethod) external onlyOwner {\n        require(isPaymentMethod[_paymentMethod], \"UPV: Payment method does not exist\");\n        \n        delete isPaymentMethod[_paymentMethod];\n        paymentMethods.removeStorage(_paymentMethod);\n        \n        emit PaymentMethodRemoved(_paymentMethod);\n    }\n    \n    /**\n     * @notice Updates the attestation verifier contract\n     * @param _newVerifier The new attestation verifier address\n     */\n    function setAttestationVerifier(address _newVerifier) external onlyOwner {\n        address oldVerifier = address(attestationVerifier);\n        require(_newVerifier != address(0), \"UPV: Invalid attestation verifier\");\n        require(_newVerifier != oldVerifier, \"UPV: Same verifier\");\n        \n        attestationVerifier = IAttestationVerifier(_newVerifier);\n        emit AttestationVerifierUpdated(oldVerifier, _newVerifier);\n    }\n                                                                                                               \n    \n    /* ============ View Functions ============ */\n    \n    function getPaymentMethods() external view returns (bytes32[] memory) {\n        return paymentMethods;\n    }\n    \n    /* ============ Internal Functions ============ */\n    \n    /**\n     * Validates and adds a nullifier to prevent double-spending\n     * @param _nullifier The nullifier to add\n     */\n    function _validateAndAddNullifier(bytes32 _nullifier) internal {\n        require(!nullifierRegistry.isNullified(_nullifier), \"Nullifier has already been used\");\n        nullifierRegistry.addNullifier(_nullifier);\n    }\n}\n"
    },
    "contracts/unifiedVerifier/SimpleAttestationVerifier.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.18;\n\nimport { Ownable } from \"@openzeppelin/contracts/access/Ownable.sol\";\nimport { ECDSA } from \"@openzeppelin/contracts/utils/cryptography/ECDSA.sol\";\nimport { IAttestationVerifier } from \"../interfaces/IAttestationVerifier.sol\";\nimport { ThresholdSigVerifierUtils } from \"../lib/ThresholdSigVerifierUtils.sol\";\n\n/**\n * @title SimpleAttestationVerifier\n * @notice Verifies attestations from off-chain verification service with on-chain trust anchors\n * @dev Simplified model with one offchain attestation service\n * \n * The verification flow:\n * 1. User runs the zkTLS protocol with the attestor to generate a zkTLS proof\n * 2. Off-chain service verifies zkTLS proofs and generates standardized attestation; Attestation is\n *    signed by the witness and includes metadata about verification\n * 3. On-chain verification checks witness signature AND validates trust anchors to ensure\n *    offchain verification integrity\n */\ncontract SimpleAttestationVerifier is IAttestationVerifier, Ownable {\n    \n    using ECDSA for bytes32;\n    \n    /* ============ Events ============ */\n    \n    event WitnessUpdated(address indexed oldWitness, address indexed newWitness);\n    \n    /* ============ Constants ============ */\n\n    uint256 public constant MIN_WITNESS_SIGNATURES = 1;\n    \n    /* ============ State Variables ============ */\n    \n    // Single witness that signs standardized attestations for offchain attestation service\n    address public witness;\n    \n    /* ============ Constructor ============ */\n    \n    /**\n     * @notice Initializes the attestation verifier\n     * @param _witness Initial witness address (can be zero to set later)\n     */\n    constructor(address _witness) Ownable() {\n        witness = _witness;\n    }\n    \n    /* ============ External Functions ============ */\n    \n    /**\n     * @notice Verifies attestations and trust anchor from off-chain verification service\n     * @param _digest The message digest to verify (EIP-712 formatted)\n     * @param _sigs Array with single signature from the witness\n     * @return isValid True if attestation and trust anchor are valid\n     */\n    function verify(\n        bytes32 _digest,\n        bytes[] calldata _sigs,\n        bytes calldata\n    ) external view override returns (bool isValid) {\n        address[] memory witnesses = new address[](1);\n        witnesses[0] = witness;\n        \n        // Verify signatures meet threshold\n        isValid = ThresholdSigVerifierUtils.verifyWitnessSignatures(\n            _digest,\n            _sigs,\n            witnesses,\n            MIN_WITNESS_SIGNATURES\n        );\n\n        // Only return isValid if it's true, otherwise library reverts\n        return isValid;\n    }\n    \n    /* ============ Governance Functions ============ */\n    \n    /**\n     * @notice Updates the witness address\n     * @param _newWitness New witness address\n     */\n    function setWitness(address _newWitness) external onlyOwner {\n        require(_newWitness != address(0), \"SimpleAttestationVerifier: Zero address\");\n        \n        address oldWitness = witness;\n        witness = _newWitness;\n        \n        emit WitnessUpdated(oldWitness, _newWitness);\n    }\n}\n"
    },
    "contracts/unifiedVerifier/UnifiedPaymentVerifier.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.18;\n\nimport { BaseUnifiedPaymentVerifier } from \"./BaseUnifiedPaymentVerifier.sol\";\nimport { INullifierRegistry } from \"../interfaces/INullifierRegistry.sol\";\nimport { IPaymentVerifier } from \"../interfaces/IPaymentVerifier.sol\";\nimport { IAttestationVerifier } from \"../interfaces/IAttestationVerifier.sol\";\nimport { IOrchestrator } from \"../interfaces/IOrchestrator.sol\";\nimport { IEscrow } from \"../interfaces/IEscrow.sol\";\n\n/**\n * @title UnifiedPaymentVerifier\n * @notice Verifies payment proofs for multiple payment methods. This is a unified verifier that \n * replaces individual payment verifiers (VenmoVerifier, PayPalVerifier, etc.) with a single \n * configurable contract.\n * \n * Key features:\n * - Supports multiple payment methods, each with custom configuration\n * - Uses AttestationVerifier to validate offchain payment attestations   \n * - Ensures trust anchor integrity for off-chain verification processes\n * - Verifies standardized payment details against provided data\n * @dev The payment attestation should be signed using the EIP-712 standard\n */\ncontract UnifiedPaymentVerifier is IPaymentVerifier, BaseUnifiedPaymentVerifier {\n\n    /* ============ Constants ============ */\n    \n    // Max timestamp buffer\n    uint256 private constant MAX_TIMESTAMP_BUFFER = 48 * 60 * 60 * 1000; // 48 hours\n\n    // EIP-712 Domain Separator\n    bytes32 private constant DOMAIN_TYPEHASH = keccak256(\n        \"EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)\"\n    );\n    \n    // EIP-712 Type Hash for PaymentAttestation\n    bytes32 private constant PAYMENT_ATTESTATION_TYPEHASH = keccak256(\n        \"PaymentAttestation(bytes32 intentHash,uint256 releaseAmount,bytes32 dataHash)\"\n    );\n\n    /* ============ State Variables ============ */\n\n    // EIP-712 Domain Separator (computed once at deployment)\n    bytes32 public immutable DOMAIN_SEPARATOR;\n\n    /* ============ Events ============ */\n\n    /**\n     * @notice Capture and emit payment details for offchain reconciliation\n     */\n    event PaymentVerified(\n        bytes32 indexed intentHash,\n        bytes32 indexed method,\n        bytes32 indexed currency,\n        uint256 amount,\n        uint256 timestamp,\n        bytes32 paymentId,\n        bytes32 payeeId\n    );\n\n    /* ============ Structs ============ */\n\n    struct PaymentDetails {\n        bytes32 method;           // Payment method hash (e.g., \"venmo\", \"paypal\", \"wise\")\n        bytes32 payeeId;          // Payment recipient ID (hashed payee details to preserve privacy)\n        uint256 amount;           // Payment amount in smallest currency unit (i.e. cents)\n        bytes32 currency;         // Payment currency hash (e.g., \"USD\", \"EUR\")\n        uint256 timestamp;        // Payment timestamp in UTC in milliseconds\n        bytes32 paymentId;        // Hashed payment identifier from the service (e.g. hashed venmo payment ID to preserve privacy)\n    }\n\n    struct IntentSnapshot {\n        bytes32 intentHash;\n        uint256 amount;\n        bytes32 paymentMethod;\n        bytes32 fiatCurrency;\n        bytes32 payeeDetails;\n        uint256 conversionRate;\n        uint256 signalTimestamp;\n        uint256 timestampBuffer;\n    }\n\n    struct PaymentAttestation {\n        bytes32 intentHash;       // Binds the payment to the intent on Orchestrator\n        uint256 releaseAmount;    // Final token amount to release on-chain after FX\n        bytes32 dataHash;         // Hash of the additional data to verify integrity\n        bytes[] signatures;       // Array of signatures from witnesses\n        bytes data;               // Data for verification\n        bytes metadata;           // Additional metadata; isn't signed by the witnesses\n    }\n\n    /* ============ Constructor ============ */\n    \n    constructor(\n        IOrchestrator _orchestrator,\n        INullifierRegistry _nullifierRegistry,\n        IAttestationVerifier _attestationVerifier\n    ) BaseUnifiedPaymentVerifier(\n        _orchestrator,\n        _nullifierRegistry,\n        _attestationVerifier\n    ) {\n        // Compute EIP-712 domain separator\n        DOMAIN_SEPARATOR = keccak256(\n            abi.encode(\n                DOMAIN_TYPEHASH,\n                keccak256(bytes(\"UnifiedPaymentVerifier\")), // name\n                keccak256(bytes(\"1\")),                      // version\n                block.chainid,                              // chainId\n                address(this)                               // verifyingContract\n            )\n        );\n    }\n\n    /* ============ External Functions ============ */\n    \n    /**\n     * ONLY ORCHESTRATOR: Verifies a standardized payment attestation generated by the attestation service.\n     * \n     * @param _verifyPaymentData Payment proof and intent details required for verification\n     * @return result The payment verification result containing success status, intent hash, and release amount\n     * @dev Ensure the orchestrator verifies the intent exists before calling this function\n     */\n    function verifyPayment(\n        VerifyPaymentData calldata _verifyPaymentData\n    )\n        external\n        override\n        onlyOrchestrator()\n        returns (PaymentVerificationResult memory result)\n    {\n        PaymentAttestation memory attestation = _decodeAttestation(_verifyPaymentData.paymentProof);\n        \n        (\n            PaymentDetails memory paymentDetails, \n            IntentSnapshot memory intentSnapshot\n        ) = _decodeAttestationPayload(attestation.data);\n        require(isPaymentMethod[paymentDetails.method], \"UPV: Invalid payment method\");\n        \n        _validateIntentSnapshot(_verifyPaymentData.intentHash, intentSnapshot);\n\n        bool isValid = _verifyAttestation(attestation);\n        require(isValid, \"UPV: Invalid attestation\");\n                \n        // Nullify the payment to prevent double-spending\n        _nullifyPayment(paymentDetails.method, paymentDetails.paymentId);\n        \n        _emitPaymentDetails(attestation.intentHash, paymentDetails);\n    \n        uint256 releaseAmount = _calculateReleaseAmount(attestation.releaseAmount, intentSnapshot.amount);\n\n        result = PaymentVerificationResult({\n            success: true,\n            intentHash: attestation.intentHash,\n            releaseAmount: releaseAmount\n        });\n\n        return result;\n    }\n\n    /* ============ Internal Functions ============ */\n\n    function _decodeAttestation(bytes memory paymentProof) internal pure returns (PaymentAttestation memory) {\n        return abi.decode(paymentProof, (PaymentAttestation));\n    }\n\n    function _decodeAttestationPayload(bytes memory paymentData)\n        internal\n        pure\n        returns (PaymentDetails memory paymentDetails, IntentSnapshot memory intentSnapshot)\n    {\n        (paymentDetails, intentSnapshot) = abi.decode(paymentData, (PaymentDetails, IntentSnapshot));\n    }\n\n    /**\n     * Verifies the EIP-712 attestation using the attestation verifier. Also verifies the integrity of the \n     * verify payment data using the data hash attached to the attestation.\n     */\n    function _verifyAttestation(PaymentAttestation memory attestation) internal view returns (bool) {\n        bytes32 structHash = keccak256(\n            abi.encode(\n                PAYMENT_ATTESTATION_TYPEHASH,\n                attestation.intentHash,\n                attestation.releaseAmount,\n                attestation.dataHash\n            )\n        );\n        \n        bytes32 digest = keccak256(\n            abi.encodePacked(\n                \"\\x19\\x01\",\n                DOMAIN_SEPARATOR,\n                structHash\n            )\n        );\n\n        // Verify data integrity - the data hash must match what was signed\n        require(\n            keccak256(attestation.data) == attestation.dataHash,\n            \"UPV: Data hash mismatch\"\n        );\n\n        bool isValid = attestationVerifier.verify(\n            digest, \n            attestation.signatures,\n            attestation.data\n        );\n\n        return isValid;\n    }\n\n    function _validateIntentSnapshot(\n        bytes32 intentHash,\n        IntentSnapshot memory snapshot\n    ) internal view {\n        require(snapshot.intentHash == intentHash, \"UPV: Snapshot hash mismatch\");\n\n        IOrchestrator.Intent memory intent = IOrchestrator(orchestrator).getIntent(intentHash);\n        require(snapshot.amount == intent.amount, \"UPV: Snapshot amount mismatch\");\n        require(snapshot.paymentMethod == intent.paymentMethod, \"UPV: Snapshot method mismatch\");\n        require(snapshot.fiatCurrency == intent.fiatCurrency, \"UPV: Snapshot currency mismatch\");\n        require(snapshot.conversionRate == intent.conversionRate, \"UPV: Snapshot rate mismatch\");\n        require(snapshot.signalTimestamp == intent.timestamp, \"UPV: Snapshot timestamp mismatch\");\n        require(snapshot.timestampBuffer <= MAX_TIMESTAMP_BUFFER, \"UPV: Snapshot timestamp buffer exceeds maximum\");\n\n        IEscrow.DepositPaymentMethodData memory paymentMethodData = IEscrow(intent.escrow)\n            .getDepositPaymentMethodData(intent.depositId, snapshot.paymentMethod);\n        require(snapshot.payeeDetails == paymentMethodData.payeeDetails, \"UPV: Snapshot payee mismatch\");\n    }\n\n    /**\n     * Nullifies a payment to prevent double-spending\n     * @dev Creates a unique nullifier by encoding both the payment method and payment ID together.\n     * This prevents collisions where the same payment ID could exist across different payment\n     * methods (e.g., Venmo transaction #123 vs PayPal transaction #123).\n     */\n    function _nullifyPayment(bytes32 paymentMethod, bytes32 paymentId) internal {\n        bytes32 nullifier = keccak256(abi.encodePacked(paymentMethod, paymentId));\n        _validateAndAddNullifier(nullifier);\n    }\n\n    /**\n     * Calculates the release amount for an intent by capping the release amount to the intent amount\n     */\n    function _calculateReleaseAmount(uint256 releaseAmount, uint256 intentAmount) internal pure returns (uint256) {\n        if (releaseAmount > intentAmount) {\n            return intentAmount;\n        }\n        return releaseAmount;\n    }\n\n    /**\n     * Emits the payment details for offchain reconciliation\n     */\n    function _emitPaymentDetails(bytes32 intentHash, PaymentDetails memory paymentDetails) internal {\n        emit PaymentVerified(\n            intentHash,                 // Tie the payment details to the intent hash\n            paymentDetails.method,\n            paymentDetails.currency,\n            paymentDetails.amount,\n            paymentDetails.timestamp,\n            paymentDetails.paymentId,\n            paymentDetails.payeeId\n        );\n    }\n}\n"
    }
  },
  "settings": {
    "optimizer": {
      "enabled": true,
      "runs": 200
    },
    "outputSelection": {
      "*": {
        "*": [
          "abi",
          "evm.bytecode",
          "evm.deployedBytecode",
          "evm.methodIdentifiers",
          "metadata",
          "devdoc",
          "userdoc",
          "storageLayout",
          "evm.gasEstimates"
        ],
        "": [
          "ast"
        ]
      }
    },
    "metadata": {
      "useLiteralContent": true
    }
  }
}